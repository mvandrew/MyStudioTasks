#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Обработчики подписок на события.

// Добавление полей, на основании которых будет формироваться представления бизнес процесса.
//
// Параметры:
//  МенеджерОбъекта      - БизнесПроцессМенеджер - Менеджер бизнес процесса.
//  Поля                 - Структура - Поля, из которых формируется представление бизнес процесса.
//  СтандартнаяОбработка - Булево - если установить Ложь, то стандартная обработка заполнения не будет
//                                  выполнена.
//
Процедура ОбработкаПолученияПолейПредставленияБизнесПроцесса(МенеджерОбъекта, Поля, СтандартнаяОбработка) Экспорт
	
	Поля.Добавить("Наименование");
	Поля.Добавить("Дата");
	СтандартнаяОбработка = Ложь;

КонецПроцедуры

// Обработка получения представления бизнес процесса на основание полей данных.
//
// Параметры:
//  МенеджерОбъекта      - БизнесПроцессМенеджер - Менеджер бизнес процесса.
//  Данные               - Структура - Поля, из которых формируется представление бизнес процесса.
//  Представление        - Строка - Представление бизнес процесса.
//  СтандартнаяОбработка - Булево - Если установить Ложь, то стандартная обработка заполнения не будет
//                                  выполнена.
//
Процедура ОбработкаПолученияПредставленияБизнесПроцесса(МенеджерОбъекта, Данные, Представление, СтандартнаяОбработка) Экспорт
	
	Наименование = ?(ПустаяСтрока(Данные.Наименование), НСтр("ru = 'Без описания'"), Данные.Наименование);
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ТолстыйКлиентУправляемоеПриложение Или ВнешнееСоединение Тогда
	Дата = Формат(Данные.Дата, ?(ПолучитьФункциональнуюОпцию("ИспользоватьДатуИВремяВСрокахЗадач"), "ДЛФ=DT", "ДЛФ=D"));
	Представление = Метаданные.НайтиПоТипу(ТипЗнч(МенеджерОбъекта)).Представление();
#Иначе	
	Дата = Формат(Данные.Дата, "ДЛФ=D");
	Представление = НСтр("ru = 'Бизнес-процесс'");
#КонецЕсли
	ШаблонПредставления = НСтр("ru = '%1 от %2 (%3)'");
	Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПредставления, Наименование, Дата, Представление);
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти
