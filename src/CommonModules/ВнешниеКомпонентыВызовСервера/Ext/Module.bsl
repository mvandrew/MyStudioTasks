#Область ПрограммныйИнтерфейс

// Возвращает информацию о внешней компоненте по идентификатору и версии.
//
// Параметры:
//  Идентификатор - Строка               - идентификатор объекта внешней компоненты.
//  Версия        - Строка, Неопределено - версия компоненты. 
//
// Возвращаемое значение:
//  Структура - информация о компоненте
//      * Существует     - Булево - признак отсутствия компоненты.
//      * ОписаниеОшибки - Строка - краткое описание ошибки.
//      * Идентификатор  - Строка - идентификатор объекта внешней компоненты.
//      * Версия         - Строка - версия компоненты.
//      * Наименование   - Строка - наименование и краткая информация о компоненте.
//
// Пример:
//
//  Результат = ВнешниеКомпонентыВызовСервера.ИнформацияОКомпоненте("InputDevice", "8.1.7.10");
//
//  Если Результат.Существует Тогда
//      Идентификатор = Результат.Идентификатор;
//      Версия        = Результат.Версия;
//      Наименование  = Результат.Наименование;
//  Иначе
//      ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.ОписаниеОшибки);
//  КонецЕсли;
//
Функция ИнформацияОКомпоненте(Идентификатор, Версия = Неопределено) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Существует", Ложь);
		
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
	Если Версия = Неопределено Тогда 
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ВнешниеКомпоненты.Идентификатор КАК Идентификатор,
			|	МАКСИМУМ(ВнешниеКомпоненты.ДатаВерсии) КАК ДатаВерсии
			|ПОМЕСТИТЬ ВТ_МаксимальнаяВерсияКомпоненты
			|ИЗ
			|	Справочник.ВнешниеКомпоненты КАК ВнешниеКомпоненты
			|ГДЕ
			|	ВнешниеКомпоненты.Идентификатор = &Идентификатор
			|
			|СГРУППИРОВАТЬ ПО
			|	ВнешниеКомпоненты.Идентификатор
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВнешниеКомпоненты.Идентификатор КАК Идентификатор,
			|	ВнешниеКомпоненты.Версия КАК Версия,
			|	ВнешниеКомпоненты.Наименование КАК Наименование,
			|	ВнешниеКомпоненты.Использование КАК Использование
			|ИЗ
			|	ВТ_МаксимальнаяВерсияКомпоненты КАК ВТ_МаксимальнаяВерсияКомпоненты
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВнешниеКомпоненты КАК ВнешниеКомпоненты
			|		ПО (ВнешниеКомпоненты.Идентификатор = ВТ_МаксимальнаяВерсияКомпоненты.Идентификатор)
			|			И ВТ_МаксимальнаяВерсияКомпоненты.ДатаВерсии = ВнешниеКомпоненты.ДатаВерсии";
	Иначе 
		Запрос.УстановитьПараметр("Версия", Версия);
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ВнешниеКомпоненты.Идентификатор КАК Идентификатор,
			|	ВнешниеКомпоненты.Версия КАК Версия,
			|	ВнешниеКомпоненты.Наименование КАК Наименование,
			|	ВнешниеКомпоненты.Использование КАК Использование
			|ИЗ
			|	Справочник.ВнешниеКомпоненты КАК ВнешниеКомпоненты
			|ГДЕ
			|	ВнешниеКомпоненты.Идентификатор = &Идентификатор
			|	И ВнешниеКомпоненты.Версия = &Версия";

	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
		
	Если РезультатЗапроса.Пустой() Тогда
		
		Результат.Вставить("ОписаниеОшибки", НСтр("ru = 'Внешняя компонента не найдена'"));
		Возврат Результат;
		
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Если Выборка.Использование = Перечисления.ВариантыИспользованияВнешнихКомпонент.Отключена Тогда
		
		Результат.Вставить("ОписаниеОшибки", НСтр("ru = 'Внешняя компонента отключена'"));
		Возврат Результат;
		
	КонецЕсли;
	
	Результат.Вставить("Идентификатор", Выборка.Идентификатор);
	Результат.Вставить("Версия"       , Выборка.Версия);
	Результат.Вставить("Наименование" , Выборка.Наименование);
	
	Результат.Вставить("Существует", Истина);
	Возврат Результат;
	
КонецФункции

#КонецОбласти