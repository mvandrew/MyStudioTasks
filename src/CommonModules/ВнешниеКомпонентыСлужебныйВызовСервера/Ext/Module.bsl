#Область СлужебныеПроцедурыИФункции

// Возвращает ссылку на справочник внешней компоненты по идентификатору и версии.
//
// Параметры:
//  Идентификатор - Строка               - идентификатор объекта внешнего компонента.
//  Версия        - Строка, Неопределено - версия компоненты.
//
// Возвращаемое значение:
//  СправочникСсылка.ВнешниеКомпоненты - ссылка на контейнер внешней компоненты в информационной базе.
//
Функция СсылкаПоИдентификатору(Идентификатор, Версия = Неопределено) Экспорт
	
	Возврат ВнешниеКомпонентыСлужебный.СсылкаПоИдентификатору(Идентификатор, Версия);
	
КонецФункции

// Выполняет подготовку компоненты к подключению и возвращает информацию для проверки
// совместимости компоненты на клиенте.
// В случае отсутствия компоненты, выполняет ее получение средствами подсистемы интернет поддержки.
// Компонента помещается во временное хранилище до завершения сеанса,
// Для оптимизации памяти после попытки подключения рекомендуется выполнить очистку хранилища.
//
// Параметры:
//  Идентификатор           - Строка                  - идентификатор объекта внешнего компонента.
//  Версия                  - Строка, Неопределено    - версия компоненты. 
//
// Возвращаемое значение:
//  Структура - параметры выполнения задания (см. ДлительныеОперации.ВыполнитьВФоне).
//
Функция НачатьПодготовкуКомпонентыКПодключению(Идентификатор, Версия = Неопределено) Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("Идентификатор", Идентификатор);
	Параметры.Вставить("Версия"       , Версия);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Новый УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Подготовка компоненты...'");
	
	Возврат ДлительныеОперации.ВыполнитьВФоне("ВнешниеКомпонентыСлужебный.ПодготовитьКомпонентуКПодключению",
			Параметры, ПараметрыВыполнения);
	
КонецФункции

// Получает результат подготовки компоненты к подключению возвращает его и очищает память.
//
Функция РезультатПодготовкиКомпонентыКПодключению(АдресРезультата) Экспорт
	
	Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
	УдалитьИзВременногоХранилища(АдресРезультата);
	Возврат Результат;
	
КонецФункции

// Имя файла компоненты для сохранения в файл.
//
Функция ИмяФайлаКомпоненты(Ссылка) Экспорт 
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ИмяФайла");
	
КонецФункции

// Проверяет доступность
//
Функция ДоступнаЗагрузкаКомпонентыИзФайла() Экспорт 
	
	Возврат Пользователи.ЭтоПолноправныйПользователь(,, Ложь);
	
КонецФункции

#КонецОбласти

