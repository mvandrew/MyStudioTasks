#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции по выполнению проверок ведению учета и получению их результатов.

// Выполняет указанную проверку ведения учета с заданными параметрами.
//
// Параметры:
//   Проверка                    - СправочникСсылка.ПравилаПроверкиУчета, Строка - правило проверки,
//                                 которая будет выполняться либо строковый идентификатор указанного правила.
//   ПараметрыВыполненияПроверки - Структура, Массив - произвольные дополнительные параметры проверки,
//                                 которые уточняют, что и как именно проверять. 
//                                 См. КонтрольВеденияУчета.ПараметрыВыполненияПроверки.
//     - Структура - один параметр проверки:
//       * Свойство1 - ЛюбаяСсылка, Булево, Число, Строка, Дата - первое значение параметра.
//       * Свойство2 - ЛюбаяСсылка, Булево, Число, Строка, Дата - второе значение параметра.
//       * Свойство3 - ЛюбаяСсылка, Булево, Число, Строка, Дата - третье значение параметра.
//       ...                                              
//     - Массив - несколько параметров проверки (элементы массива типа Структура, как описано выше). 
//
// Пример:
//   1. Проверка = КонтрольВеденияУчета.ПроверкаПоИдентификатору("ПроверитьСсылочнуюЦелостность");
//      КонтрольВеденияУчета.ВыполнитьПроверку(Проверка);
//   2. ПараметрыВыполненияПроверки = Новый Массив;
//      Параметр1 = КонтрольВеденияУчета.ПараметрыВыполненияПроверки("ЗакрытиеМесяца", Организация1, ЗакрываемыйМесяц);
//      ПараметрыВыполненияПроверки.Добавить(Параметр1);
//      Параметр2 = КонтрольВеденияУчета.ПараметрыВыполненияПроверки("ЗакрытиеМесяца", Организация2, ЗакрываемыйМесяц);
//      ПараметрыВыполненияПроверки.Добавить(Параметр2);
//      ВыполнитьПроверку("ПроверитьПроведениеДокументов", ПараметрыВыполненияПроверки);
//
Процедура ВыполнитьПроверку(Знач Проверка, Знач ПараметрыВыполненияПроверки = Неопределено) Экспорт
	
	Если ТипЗнч(Проверка) = Тип("Строка") Тогда
		ВыполняемаяПроверка = ПроверкаПоИдентификатору(Проверка);
		Если ВыполняемаяПроверка.Пустая() Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Проверка ведения учета с идентификатором ""%1"" не существует (см. КонтрольВеденияУчетаПереопределяемый.ПриОпределенииПроверок)'"),
				Проверка);
		КонецЕсли;
	Иначе
		ОбщегоНазначенияКлиентСервер.ПроверитьПараметр("КонтрольВеденияУчета.ВыполнитьПроверку", "Проверка",
			Проверка, Тип("СправочникСсылка.ПравилаПроверкиУчета"));
		ВыполняемаяПроверка = Проверка;
	КонецЕсли;
	
	Если ПараметрыВыполненияПроверки <> Неопределено Тогда
		
		Если ТипЗнч(ПараметрыВыполненияПроверки) = Тип("Структура") Тогда
			ПараметрыВыполнения = Новый Массив;
			ПараметрыВыполнения.Добавить(ПараметрыВыполненияПроверки);
			ПараметрыВыполненияПроверки = ПараметрыВыполнения;
		КонецЕсли;	
		
		ОбщегоНазначенияКлиентСервер.ПроверитьПараметр("КонтрольВеденияУчета.ВыполнитьПроверку", "ПараметрыВыполненияПроверки",
			ПараметрыВыполненияПроверки, Тип("Массив"));
			
		Для Каждого ПараметрПроверки Из ПараметрыВыполненияПроверки Цикл
			ОбщегоНазначенияКлиентСервер.ПроверитьПараметр("КонтрольВеденияУчета.ВыполнитьПроверку", "ПараметрыВыполненияПроверки.Элемент",
				ПараметрПроверки, Тип("Структура"));
			Для Каждого ТекущийПараметр Из ПараметрПроверки Цикл
				ОбщегоНазначенияКлиентСервер.ПроверитьПараметр("КонтрольВеденияУчета.ВыполнитьПроверку",
					ТекущийПараметр.Ключ, ТекущийПараметр.Значение, КонтрольВеденияУчетаСлужебный.ОжидаемыеТипыСвойствВидовПроверок());
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
		
	КонтрольВеденияУчетаСлужебный.ВыполнитьПроверку(ВыполняемаяПроверка, ПараметрыВыполненияПроверки);
	
КонецПроцедуры

// Выполняет проверки по заданному контексту - общему признаку, связывающему воедино пакет проверок.
// Если указанный признак установлен у группы проверок, то выполняются все проверки этой группы. 
// В этом случае наличие (или отсутствие) указанного признака у самой проверки значения не имеет.
// Проверки с флагом Использование, установленным в значение Ложь, пропускаются.
//
// Параметры:
//    КонтекстПроверокВеденияУчета - ОпределяемыйТип.КонтекстПроверокВеденияУчета - Контекст выполняемых проверок.
//
// Пример:
//    КонтрольВеденияУчета.ВыполнитьПроверкиВКонтексте(Перечисления.ХозяйственныеОперации.ЗакрытиеМесяца);
//
Процедура ВыполнитьПроверкиВКонтексте(КонтекстПроверокВеденияУчета) Экспорт
	
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр("КонтрольВеденияУчета.ВыполнитьПроверкиВКонтексте",
		"КонтекстПроверокВеденияУчета", КонтекстПроверокВеденияУчета,
		Метаданные.ОпределяемыеТипы.КонтекстПроверокВеденияУчета.Тип);
	
	ПроверкиПоКонтексту = КонтрольВеденияУчетаСлужебный.ПроверкиПоКонтексту(КонтекстПроверокВеденияУчета);
	
	Для Каждого Проверка Из ПроверкиПоКонтексту Цикл
		ВыполнитьПроверку(Проверка);
	КонецЦикла;
	
КонецПроцедуры

// Возвращает сводку по количеству выявленных проблем указанного вида проверки.
//
// Параметры:
//   ВидПроверок                - СправочникСсылка.ВидыПроверок - ссылка на вид проверки.
//                              - Строка - строковый идентификатор вида проверки Свойство1.
//                              - Массив - строковые идентификаторы вида проверки Свойство1...СвойствоN.
//   ПоискПоТочномуСоответствию - Булево - Регулирует возможности точности. Если Истина, то поиск ведется
//                                по переданным свойствам на равенство, остальные свойства должны быть равны
//                                Неопределено (табличная часть дополнительных свойств должна быть пуста).
//                                Если Ложь, то значения остальных свойств могут быть произвольными, главное
//                                чтобы соответствующие свойства были равны свойствам структуры. По умолчанию Истина.
//
// Возвращаемое значение:
//  Структура - сводка:
//    * Количество - Число - общее количество найденных проблем.
//    * ЕстьОшибки - Булево - признак того, имеются ли ошибки среди найденных проблем (с важностью "Ошибка").
//
// Пример:
//   1) Результат = СводнаяИнформацияПоВидамПроверок("СистемныеПроверки");
//   2) ВидПроверок = Новый Массив;
//      ВидПроверок.Добавить("ЗакрытиеМесяца");
//      ВидПроверок.Добавить(Организация);
//      ВидПроверок.Добавить(МесяцЗакрытия);
//      Результат = СводнаяИнформацияПоВидамПроверок(ВидПроверок);
//
Функция СводнаяИнформацияПоВидамПроверок(ВидПроверок, ПоискПоТочномуСоответствию = Истина) Экспорт
	
	ИмяПроцедуры = "КонтрольВеденияУчета.СводнаяИнформацияПоВидамПроверок";
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(ИмяПроцедуры, "ВидПроверок", ВидПроверок, КонтрольВеденияУчетаСлужебный.ОписаниеТипаВидПроверки());
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(ИмяПроцедуры, "ПоискПоТочномуСоответствию", ПоискПоТочномуСоответствию, Тип("Булево"));
	
	СводнаяИнформация = Новый Структура;
	СводнаяИнформация.Вставить("Количество", 0);
	СводнаяИнформация.Вставить("ЕстьОшибки", Ложь);
	
	МассивВидовПроверок = Новый Массив;
	Если ТипЗнч(ВидПроверок) = Тип("СправочникСсылка.ВидыПроверок") Тогда
		МассивВидовПроверок.Добавить(ВидПроверок);
	Иначе
		МассивВидовПроверок = КонтрольВеденияУчетаСлужебный.ПолучитьВидыПроверок(ВидПроверок, ПоискПоТочномуСоответствию);
		Если МассивВидовПроверок.Количество() = 0 Тогда
			Возврат СводнаяИнформация;
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО(*) КАК Количество,
	|	ЕСТЬNULL(МАКСИМУМ(ВЫБОР
	|				КОГДА РезультатыПроверкиУчета.ВажностьПроблемы = ЗНАЧЕНИЕ(Перечисление.ВажностьПроблемыУчета.Ошибка)
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ), ЛОЖЬ) КАК ЕстьОшибки
	|ИЗ
	|	РегистрСведений.РезультатыПроверкиУчета КАК РезультатыПроверкиУчета
	|ГДЕ
	|	НЕ РезультатыПроверкиУчета.ИгнорироватьПроблему
	|	И РезультатыПроверкиУчета.ВидПроверки В (&МассивВидовПроверок)");
	
	Запрос.УстановитьПараметр("МассивВидовПроверок", МассивВидовПроверок);
	Результат = Запрос.Выполнить().Выбрать();
	Результат.Следующий();
	
	ЗаполнитьЗначенияСвойств(СводнаяИнформация, Результат);
	
	Возврат СводнаяИнформация;
	
КонецФункции

// Возвращает подробные сведения о выявленных проблемах одного или нескольких интересующих видов проверки.
//
// Параметры:
//   ВидПроверок                - СправочникСсылка.ВидыПроверок - ссылка на вид проверки.
//                              - Строка - строковый идентификатор вида проверки Свойство1.
//                              - Массив - строковые идентификаторы вида проверки Свойство1...СвойствоN.
//   ПоискПоТочномуСоответствию - Булево - если Истина, то вид проверки определяется по точному соответствию со
//                                всеми переданными значениям свойств в параметре ВидПроверок (см. пример № 2).
//                                Если Ложь, то вид проверки определяется по указанным значениям свойств
//                                и с любыми значениями неуказанных свойств в параметре ВидПроверок (см. пример № 3).
//
// Возвращаемое значение:
//   ТаблицаЗначений - выявленные проблемы:
//     * ПроблемныйОбъект         - ЛюбаяСсылка - ссылка на объект, с которым связана проблема.
//     * ВажностьПроблемы         - ПеречислениеСсылка.ВажностьПроблемыУчета - важность проблемы учета
//                                  "Информация", "Предупреждение", "Ошибка", "ПолезныйСовет" и "ВажнаяИнформация".
//     * ПравилоПроверки          - СправочникСсылка.ПравилаПроверкиУчета - выполненная проверка с описанием проблемы.
//     * ВидПроверок              - СправочникСсылка.ВидыПроверок - вид проверки.
//     * УточнениеПроблемы        - Строка - текстовое уточнение найденной проблемы.
//     * Ответственный            - СправочникСсылка.Пользователи - заполнен, если по выявленной проблеме
//                                  алгоритм проверки определил конкретного ответственного.
//     * Выявлено                 - Дата - дата и время выявления проблемы.
//     * ДополнительнаяИнформация - ХранилищеЗначений - произвольные дополнительные сведения, связанные 
//                                  с выявленной проблемой.
//
// Пример:
//   1) Результат = ПодробнаяИнформацияПоВидамПроверок("СистемныеПроверки");
//   2) ВидПроверок = Новый Массив;
//      ВидПроверок.Добавить("ЗакрытиеМесяца");
//      ВидПроверок.Добавить(Организация);
//      ВидПроверок.Добавить(МесяцЗакрытия);
//      Результат = ПодробнаяИнформацияПоВидамПроверок(ВидПроверок);
//   3) Выбрать все проблемы закрытия месяца по всем периодам для указанной организации:
//      ВидПроверок = Новый Массив;
//      ВидПроверок.Добавить("ЗакрытиеМесяца");
//      ВидПроверок.Добавить(Организация);
//      Результат = ПодробнаяИнформацияПоВидамПроверок(ВидПроверок, Ложь); 
//
Функция ПодробнаяИнформацияПоВидамПроверок(ВидПроверок, ПоискПоТочномуСоответствию = Истина) Экспорт
	
	ИмяПроцедуры = "КонтрольВеденияУчета.ПодробнаяИнформацияПоВидамПроверок";
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(ИмяПроцедуры, "ВидПроверок", ВидПроверок, КонтрольВеденияУчетаСлужебный.ОписаниеТипаВидПроверки());
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(ИмяПроцедуры, "ПоискПоТочномуСоответствию", ПоискПоТочномуСоответствию, Тип("Булево"));
	
	ПодробнаяИнформация        = Новый ТаблицаЗначений;
	КолонкиПодробнойИнформации = ПодробнаяИнформация.Колонки;
	КолонкиПодробнойИнформации.Добавить("ПроблемныйОбъект",         ОбщегоНазначения.ОписаниеТипаВсеСсылки());
	КолонкиПодробнойИнформации.Добавить("ВажностьПроблемы",         Новый ОписаниеТипов("ПеречислениеСсылка.ВажностьПроблемыУчета"));
	КолонкиПодробнойИнформации.Добавить("ПравилоПроверки",          Новый ОписаниеТипов("СправочникСсылка.ПравилаПроверкиУчета"));
	КолонкиПодробнойИнформации.Добавить("ВидПроверки",              Новый ОписаниеТипов("СправочникСсылка.ВидыПроверок"));
	КолонкиПодробнойИнформации.Добавить("УточнениеПроблемы",        Новый ОписаниеТипов("Строка"));
	КолонкиПодробнойИнформации.Добавить("Ответственный",            Новый ОписаниеТипов("СправочникСсылка.Пользователи"));
	КолонкиПодробнойИнформации.Добавить("Выявлено",                 Новый ОписаниеТипов("Дата"));
	КолонкиПодробнойИнформации.Добавить("ДополнительнаяИнформация", Новый ОписаниеТипов("ХранилищеЗначения"));
	
	МассивВидовПроверок = Новый Массив;
	
	Если ТипЗнч(ВидПроверок) = Тип("СправочникСсылка.ВидыПроверок") Тогда
		МассивВидовПроверок.Добавить(ВидПроверок);
	Иначе
		МассивВидовПроверок = КонтрольВеденияУчетаСлужебный.ПолучитьВидыПроверок(ВидПроверок, ПоискПоТочномуСоответствию);
	КонецЕсли;
	
	Если МассивВидовПроверок.Количество() = 0 Тогда
		Возврат ПодробнаяИнформация;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РезультатыПроверкиУчета.ПроблемныйОбъект КАК ПроблемныйОбъект,
	|	РезультатыПроверкиУчета.ПравилоПроверки КАК ПравилоПроверки,
	|	РезультатыПроверкиУчета.ВажностьПроблемы КАК ВажностьПроблемы,
	|	РезультатыПроверкиУчета.ВидПроверки КАК ВидПроверки,
	|	РезультатыПроверкиУчета.УточнениеПроблемы КАК УточнениеПроблемы,
	|	РезультатыПроверкиУчета.Ответственный КАК Ответственный,
	|	РезультатыПроверкиУчета.Выявлено КАК Выявлено,
	|	РезультатыПроверкиУчета.ДополнительнаяИнформация КАК ДополнительнаяИнформация
	|ИЗ
	|	РегистрСведений.РезультатыПроверкиУчета КАК РезультатыПроверкиУчета
	|ГДЕ
	|	НЕ РезультатыПроверкиУчета.ИгнорироватьПроблему
	|	И РезультатыПроверкиУчета.ВидПроверки В(&МассивВидовПроверок)");
	
	Запрос.УстановитьПараметр("МассивВидовПроверок", МассивВидовПроверок);
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		ПодробнаяИнформация = Результат.Выгрузить();
	КонецЕсли;
	
	Возврат ПодробнаяИнформация;
	
КонецФункции

// Возвращает проверку по переданному идентификатору.
//
// Параметры:
//   Идентификатор - Строка - Строковый идентификатор проверки. Например, "ПроверитьСсылочнуюЦелостность".
//
// Возвращаемое значение: 
//   СправочникСсылка.ПравилаПроверкиУчета - ссылка на проверку или пустая ссылка, 
//      если проверка с таким идентификатором не существует.
//
Функция ПроверкаПоИдентификатору(Идентификатор) Экспорт
	
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр("КонтрольВеденияУчета.ПроверкаПоИдентификатору", "Идентификатор", Идентификатор, Тип("Строка"));
	Возврат КонтрольВеденияУчетаСлужебный.ПроверкаПоИдентификатору(Идентификатор);
	
КонецФункции

// Возвращает количество проблем, выявленных у переданного объекта.
//
// Параметры:
//   СсылкаНаОбъект - ЛюбаяСсылка - Ссылка на объект, для которого нужно вычислить количество проблем.
//
// Возвращаемое значение:
//   Число - Количество найденных проблем по переданной ссылке на объект.
//
Функция КоличествоПроблемПоОбъекту(СсылкаНаОбъект) Экспорт
	
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр("КонтрольВеденияУчета.КоличествоПроблемПоОбъекту", "СсылкаНаОбъект",
		СсылкаНаОбъект, ОбщегоНазначения.ОписаниеТипаВсеСсылки());
	
	ТекущийПользовательПолноправный = Пользователи.ЭтоПолноправныйПользователь();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО(*) КАК Количество
	|ИЗ
	|	РегистрСведений.РезультатыПроверкиУчета КАК РезультатыПроверкиУчета
	|ГДЕ
	|	РезультатыПроверкиУчета.ПроблемныйОбъект = &ПроблемныйОбъект
	|	И НЕ РезультатыПроверкиУчета.ИгнорироватьПроблему");
	Запрос.УстановитьПараметр("ПроблемныйОбъект", СсылкаНаОбъект);
	
	Если Не ТекущийПользовательПолноправный Тогда
		УстановитьПривилегированныйРежим(Истина);
	КонецЕсли;
	
	Результат = Запрос.Выполнить().Выбрать();
	Если Результат.Следующий() Тогда
		КоличествоПроблем = Результат.Количество;
	Иначе
		КоличествоПроблем = 0;
	КонецЕсли;
	
	Если Не ТекущийПользовательПолноправный Тогда
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Запрос = Неопределено;
	
	Возврат КоличествоПроблем;
	
КонецФункции

// Вычисляет количество проблем, выявленных переданным правилом проверки.
//
// Параметры:
//   ПравилоПроверки - СправочникСсылка.ПравилаПроверкиУчета - Ссылка на правило, для которого
//                     нужно вычислить количество проблем.
//
// Возвращаемое значение:
//   Число - Количество найденных проблем по переданному правилу проверки.
//
Функция КоличествоПроблемПоПравилуПроверки(ПравилоПроверки) Экспорт
	
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр("КонтрольВеденияУчета.КоличествоПроблемПоПравилуПроверки", "ПравилоПроверки",
		ПравилоПроверки, Тип("СправочникСсылка.ПравилаПроверкиУчета"));
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КОЛИЧЕСТВО(*) КАК Количество
		|ИЗ
		|	РегистрСведений.РезультатыПроверкиУчета КАК РезультатыПроверкиУчета
		|ГДЕ
		|	РезультатыПроверкиУчета.ПравилоПроверки = &ПравилоПроверки
		|	И НЕ РезультатыПроверкиУчета.ИгнорироватьПроблему");
	Запрос.УстановитьПараметр("ПравилоПроверки", ПравилоПроверки);
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить().Выбрать();
	Если Результат.Следующий() Тогда
		КоличествоПроблем = Результат.Количество;
	Иначе
		КоличествоПроблем = 0;
	КонецЕсли;
	
	Возврат КоличествоПроблем;
	
КонецФункции

// Формирует параметры выполнения проверки для передачи в процедуры и функции ВыполнитьПроверку, ОписаниеПроблемы,
// ВидПроверки и другие.
// Параметры содержат уточнение, для чего именно требуется выполнить проверку,
// например, проверить закрытие месяца для конкретной организации по конкретному периоду.
// Порядок следования параметров учитывается.
//
// Параметры:
//     Параметр1     - ЛюбаяСсылка, Булево, Число, Строка, Дата - первый параметр проверки.
//     Параметр2     - ЛюбаяСсылка, Булево, Число, Строка, Дата - второй параметр проверки.
//     Параметр3     - ЛюбаяСсылка, Булево, Число, Строка, Дата - третий параметр проверки.
//     Параметр4     - ЛюбаяСсылка, Булево, Число, Строка, Дата - четвертый параметр проверки.
//     Параметр5     - ЛюбаяСсылка, Булево, Число, Строка, Дата - пятый параметр проверки.
//     ДругиеПараметры - Массив - другие параметры проверки (элементы типов ЛюбаяСсылка, Булево, Число, Строка, Дата).
//
// Возвращаемое значение:
//    Структура - параметры проверки в виде:
//       * Наименование - Строка - представление вида проверки. 
//       * Свойство1 - ЛюбаяСсылка, Булево, Число, Строка, Дата - первый параметр проверки.
//       * Свойство2 - ЛюбаяСсылка, Булево, Число, Строка, Дата - второй параметр проверки.
//       * Свойство3 - ЛюбаяСсылка, Булево, Число, Строка, Дата - третий параметр проверки.
//       ...                                              
//       * СвойствоN - ЛюбаяСсылка, Булево, Число, Строка, Дата - последний параметр вида проверки.
//
// Пример:
//     1. Параметры = ПараметрыВыполненияПроверки("СистемныеПроверки");
//     2. Параметры = ПараметрыВыполненияПроверки("ЗакрытиеМесяца", ОрганизацияСсылка, ЗакрываемыйМесяц);
//
Функция ПараметрыВыполненияПроверки(Знач Параметр1, Знач Параметр2 = Неопределено, Знач Параметр3 = Неопределено,
	Знач Параметр4 = Неопределено, Знач Параметр5 = Неопределено, Знач ДругиеПараметры = Неопределено) Экспорт
	
	Возврат КонтрольВеденияУчетаСлужебный.ПараметрыВыполненияПроверки(Параметр1, Параметр2, Параметр3, Параметр4, Параметр5, ДругиеПараметры);
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции для добавления проверок ведения учета.

// Формирует описание проблемы в виде структуры.
//
// Параметры:
//   ПроблемныйОбъект  - ЛюбаяСсылка - Ссылка на объект "Источник" проблем.
//   ПараметрыПроверки - Структура - Параметры проверки которую необходимо выполнить:
//       * Проверка         - СправочникСсылка.ПравилаПроверкиУчета - Ссылка на выполненную проверку.
//       * ВидПроверки      - СправочникСсылка.ВидыПроверок - Ссылка на вид проверки, к которому относится
//                            выполненная проверка.
//       * ВажностьПроблемы - ПеречислениеСсылка.ВажностьПроблемыУчета - Важность проблемы учета
//                            "Информация", "Предупреждение", "Ошибка" и "ПолезныйСовет".
//
// Возвращаемое значение:
//   Структура - описание проблемы:
//       * ПроблемныйОбъект         - ЛюбаяСсылка - Ссылка на объект "Источник" проблем.
//       * Проверка                 - СправочникСсылка.ПравилаПроверкиУчета - Ссылка на выполненную проверку.
//                                    Взято из переданной структуры ПараметрыПроверки.
//       * ВидПроверки              - СправочникСсылка.ВидыПроверок - Ссылка на вид проверки, к которому относится
//                                    выполненная проверка. Взято из переданной структуры ПараметрыПроверки
//       * ВажностьПроблемы         - СправочникСсылка.ВидыПроверок - Ссылка на вид проверки, к которому относится
//                                    выполненная проверка. Взято из переданной структуры ПараметрыПроверки.
//       * УточнениеПроблемы        - Строка - Строка уточнения проблемы. По умолчанию не заполнена.
//       * КлючУникальности         - УникальныйИдентификатор - Ключ уникальности проблемы.
//       * Выявлено                 - Дата - Серверное время обнаружения проблемы.
//       * ДополнительнаяИнформация - ХранилищеЗначений - Служебное свойство с дополнительными
//                                    сведениями, связанными с выявленной проблемой.
//                                    По умолчанию Неопределено.
//       * Ответственный            - СправочникСсылка.Пользователи - Заполнен если есть возможность
//                                    идентифицировать ответственного в проблемном объекте.
//                                    По умолчанию Неопределено.
//
Функция ОписаниеПроблемы(ПроблемныйОбъект, ПараметрыПроверки) Экспорт
	
	ИмяПроцедуры = "КонтрольВеденияУчета.ОписаниеПроблемы";
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(ИмяПроцедуры, "ПроблемныйОбъект", ПроблемныйОбъект, 
		ОбщегоНазначения.ОписаниеТипаВсеСсылки());
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(ИмяПроцедуры, "ПараметрыПроверки", ПараметрыПроверки, Тип("Структура"), 
		КонтрольВеденияУчетаСлужебный.ОжидаемыеТипыСвойствПараметровПроверки());
		
	Возврат КонтрольВеденияУчетаСлужебный.ОписаниеПроблемы(ПроблемныйОбъект, ПараметрыПроверки);
	
КонецФункции

// Записывает результат выполнения проверки.
//
// Параметры:
//   Проблема - Структура - описание проблемы.
//       * ПроблемныйОбъект         - ЛюбаяСсылка - ссылка на объект "Источник" проблем.
//       * ПравилоПроверки          - СправочникСсылка.ПравилаПроверкиУчета - ссылка на выполненную проверку.
//       * ВидПроверки              - СправочникСсылка.ВидыПроверок - ссылка на вид проверки, к которому 
//                                    относится выполненная проверка.
//       * КлючУникальности         - УникальныйИдентификатор - ключ уникальности проблемы.
//       * УточнениеПроблемы        - Строка - строка-уточнение найденной проблемы.
//       * ВажностьПроблемы         - ПеречислениеСсылка.ВажностьПроблемыУчета - важность проблемы учета
//                                    "Информация", "Предупреждение", "Ошибка" и "ПолезныйСовет".
//       * Ответственный            - СправочникСсылка.Пользователи - заполнен если есть возможность
//                                    идентифицировать ответственного в проблемном объекте.
//       * ИгнорироватьПроблему     - Булево - флаг игнорирования проблемы. Если имеет значение "Истина",
//                                    запись о проблеме игнорируется подсистемой.
//       * ДополнительнаяИнформация - ХранилищеЗначений - служебное свойство с дополнительными
//                                    сведениями, связанными с выявленной проблемой.
//       * Выявлено                 - Дата - серверное время идентификации проблемы.
//
//   ПараметрыПроверки - Структура - описание дополнительных параметров проверки. Необязательный параметр.
//      * ЛимитПроблем     - Число - количество проверяемых объектов.
//                           По умолчанию 1000. Если указано 0, то следует проверить все объекты.
//      * ИтерацияПроверки - Число - очередная итерация проверки. Используется совместно с параметром
//                           ЛимитПроблем для определения предельного количества итераций.
//
Процедура ЗаписатьПроблему(Проблема, ПараметрыПроверки = Неопределено) Экспорт
	
	ИмяПроцедуры = "КонтрольВеденияУчета.ЗаписатьПроблему";
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(ИмяПроцедуры, "Проблема", Проблема, Тип("Структура"), 
		КонтрольВеденияУчетаСлужебный.ОжидаемыеТипыСвойствОписанияПроблемы());
	Если ПараметрыПроверки <> Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(ИмяПроцедуры, "ПараметрыПроверки", ПараметрыПроверки, Тип("Структура"), 
			КонтрольВеденияУчетаСлужебный.ОжидаемыеТипыСвойствПараметровПроверки());
	КонецЕсли;
	
	КонтрольВеденияУчетаСлужебный.ЗаписатьПроблему(Проблема, ПараметрыПроверки);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции для встраивания подсистемы в формы объектов конфигурации.

// В форме списка выводит колонку с картинкой, сигнализирующей о наличии проблем с объектами в строках. 
// Вызывается из события ПриСозданииНаСервере формы списка.
// У динамических списков должна быть определена основная таблица. 
//
// Параметры:
//   Форма                  - УправляемаяФорма - форма списка.
//   ИменаСписков           - Строка - Имена динамических списков через запятую.
//   ДополнительныеСвойства - Структура, Неопределено - дополнительные свойства.
//
Процедура ПриСозданииНаСервереФормыСписка(Форма, ИменаСписков, ДополнительныеСвойства = Неопределено) Экспорт
	
	ИмяПроцедуры = "КонтрольВеденияУчета.ПриСозданииНаСервереФормыСписка";
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(ИмяПроцедуры, "Форма", Форма, Тип("УправляемаяФорма"));
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(ИмяПроцедуры, "ИменаСписков", ИменаСписков, Тип("Строка"));
	Если ДополнительныеСвойства <> Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(ИмяПроцедуры, "ДополнительныеСвойства", ДополнительныеСвойства, Тип("Структура"));
	КонецЕсли;
	
	Если Не КонтрольВеденияУчетаСлужебный.ПодсистемаДоступна() Тогда
		Возврат;
	КонецЕсли;
	
	ГлобальныеНастройки = КонтрольВеденияУчетаСлужебный.ГлобальныеНастройки();
	СписокИмен          = СтрРазделить(ИменаСписков, ",");
	Элементы            = Форма.Элементы;
	
	Для Каждого ИмяСписка Из СписокИмен Цикл
		ТаблицаФормы = Элементы.Найти(СокрЛП(ИмяСписка));
		Если ТаблицаФормы = Неопределено Тогда
			Продолжить;
		КонецЕсли;
			
		ТекущийСписок   = Форма[ТаблицаФормы.ПутьКДанным];
		ОсновнаяТаблица = ТекущийСписок.ОсновнаяТаблица;
		Если Не ЗначениеЗаполнено(ОсновнаяТаблица) Тогда
			Продолжить;
		КонецЕсли;
			
		ТекстЗапроса = "";
		Если ТекущийСписок.ПроизвольныйЗапрос Тогда
			ТекстЗапроса = ТекущийСписок.ТекстЗапроса;
		Иначе
			ИсполняемаяСхема               = ТаблицаФормы.ПолучитьИсполняемуюСхемуКомпоновкиДанных();
			НаборДанныхДинамическогоСписка = ИсполняемаяСхема.НаборыДанных.Найти("НаборДанныхДинамическогоСписка");
			Если НаборДанныхДинамическогоСписка <> Неопределено Тогда
				ТекущийСписок.ПроизвольныйЗапрос = Истина;
				ТекстЗапроса = НаборДанныхДинамическогоСписка.Запрос;
			КонецЕсли;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ТекстЗапроса) Тогда
			Продолжить;
		КонецЕсли;
			
		НачалоЗапроса = Лев(ТекстЗапроса, 7);
		Если НачалоЗапроса <> "ВЫБРАТЬ" Тогда
			Продолжить;
		КонецЕсли;
			
		ИмяКолонки = "ИндикаторОшибки_" + ОбщегоНазначения.КонтрольнаяСуммаСтрокой(Форма.ИмяФормы + ПолучитьРазделительПути() + ИмяСписка);
		РазделенноеИмя = СтрРазделить(ОсновнаяТаблица, ".");
		
		ДополнительныеСвойстваКомпоновщика = ТекущийСписок.КомпоновщикНастроек.Настройки.ДополнительныеСвойства;
		ДополнительныеСвойстваКомпоновщика.Вставить("КолонкаИндикатора",    ИмяКолонки);
		ДополнительныеСвойстваКомпоновщика.Вставить("ВидОбъектаМетаданных", РазделенноеИмя.Получить(0));
		ДополнительныеСвойстваКомпоновщика.Вставить("ИмяОбъектаМетаданных", РазделенноеИмя.Получить(1));
		ДополнительныеСвойстваКомпоновщика.Вставить("ИмяСписка",            ИмяСписка);
		
		СтруктураСвойствДинамическогоСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
		СтруктураСвойствДинамическогоСписка.ТекстЗапроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ВЫБРАТЬ 0 КАК %1, %2",
			ИмяКолонки, Прав(ТекстЗапроса, СтрДлина(ТекстЗапроса) - 7));
		ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(ТаблицаФормы, СтруктураСвойствДинамическогоСписка);
			
		ПараметрыКолонкиИндикации = Новый Структура;
		
		КонтрольВеденияУчетаСлужебный.ПриОпределенииПараметровКолонкиИндикации(ПараметрыКолонкиИндикации, ОсновнаяТаблица);
		КонтрольВеденияУчетаПереопределяемый.ПриОпределенииПараметровКолонкиИндикации(ПараметрыКолонкиИндикации, ОсновнаяТаблица);
		
		КолонкаИндикаторОшибки = Элементы.Добавить(ИмяКолонки, Тип("ПолеФормы"), ТаблицаФормы);
		КолонкаИндикаторОшибки.Вид                = ВидПоляФормы.ПолеКартинки;
		КолонкаИндикаторОшибки.ПутьКДанным        = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1.%2", ИмяСписка, ИмяКолонки);
		КолонкаИндикаторОшибки.ПоложениеЗаголовка = ПараметрыКолонкиИндикации.ПоложениеЗаголовка;
		КолонкаИндикаторОшибки.КартинкаШапки      = ГлобальныеНастройки.КартинкаИндикатораПроблем;
		КолонкаИндикаторОшибки.КартинкаЗначений   = ГлобальныеНастройки.КартинкаИндикатораПроблем;
		КолонкаИндикаторОшибки.Заголовок          = НСтр("ru = 'Индикатор ошибки'");
		
		КолонкиСписка = ТаблицаФормы.ПодчиненныеЭлементы;
		Если КолонкиСписка.Количество() > 0 Тогда
			Если ПараметрыКолонкиИндикации.ВыводитьПоследней Тогда
				Элементы.Переместить(КолонкаИндикаторОшибки, ТаблицаФормы);
			Иначе
				Элементы.Переместить(КолонкаИндикаторОшибки, ТаблицаФормы, КолонкиСписка.Получить(0));
			КонецЕсли;
		КонецЕсли;
		
		ТаблицаФормы.УстановитьДействие("Выбор", "Подключаемый_Выбор");
		
	КонецЦикла;
	
КонецПроцедуры

// В форме списка выводит колонку с картинкой, сигнализирующей о наличии проблем с объектами в строках. 
// Вызывается из события ПриПолученииДанныхНаСервере формы списка.
//
// Параметры:
//   Настройки              - НастройкиКомпоновкиДанных - Содержит копию полных настроек динамического списка.
//   Строки                 - СтрокиДинамическогоСписка - Коллекция содержит данные и оформление всех строк,
//                            получаемых в списке, кроме строк группировок.
//   ИмяКлючевогоПоля       - Строка - "Ссылка" или заданное имя колонки, содержащую ссылку объекта.
//   ДополнительныеСвойства - Структура, Неопределено - Содержит дополнительные свойства в случае
//                            необходимости их использования.
//
Процедура ПриПолученииДанныхНаСервере(Настройки, Строки, ИмяКлючевогоПоля = "Ссылка", ДополнительныеСвойства = Неопределено) Экспорт
	
	ИмяПроцедуры = "КонтрольВеденияУчета.ПриПолученииДанныхНаСервере";
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(ИмяПроцедуры, "Настройки", Настройки, Тип("НастройкиКомпоновкиДанных"));
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(ИмяПроцедуры, "Строки", Строки, Тип("СтрокиДинамическогоСписка"));
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(ИмяПроцедуры, "ИмяКлючевогоПоля", ИмяКлючевогоПоля, Тип("Строка"));
	Если ДополнительныеСвойства <> Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(ИмяПроцедуры, "ДополнительныеСвойства", ДополнительныеСвойства, Тип("Структура"));
	КонецЕсли;
	
	Если Не КонтрольВеденияУчетаСлужебный.ПодсистемаДоступна() Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеСвойстваКомпоновщика = Настройки.ДополнительныеСвойства;
	Если ДополнительныеСвойстваКомпоновщика.Свойство("КолонкаИндикатора") Тогда
		
		КолонкаИндикатора = Настройки.ДополнительныеСвойства.КолонкаИндикатора;
		
		Если ИмяКлючевогоПоля = "Ссылка" Тогда
			КлючиСтрок = Строки.ПолучитьКлючи();
		Иначе
			НачальныеКлючи = Строки.ПолучитьКлючи();
			КлючиСтрок     = Новый Массив;
			Для Каждого НачальныйКлюч Из НачальныеКлючи Цикл
				КлючиСтрок.Добавить(НачальныйКлюч[ИмяКлючевогоПоля]);
			КонецЦикла;
		КонецЕсли;
		
		ПроблемныеОбъекты = КонтрольВеденияУчетаСлужебный.ПроблемныеОбъекты(КлючиСтрок);
		
		Для Каждого КлючСтроки Из КлючиСтрок Цикл
			
			СтрокаСписка = Строки[КлючСтроки];
			Если Не СтрокаСписка.Данные.Свойство(КолонкаИндикатора) Тогда
				Продолжить;
			КонецЕсли;
			
			ТекстЯчейки = СтрокаСписка.Оформление.Получить(КолонкаИндикатора).НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Текст"));
			
			Если ПроблемныеОбъекты.Найти(КлючСтроки) = Неопределено Тогда
				СтрокаСписка.Данные[КолонкаИндикатора] = 1;
				Если ТекстЯчейки <> Неопределено Тогда
					ТекстЯчейки.Значение = 0;
				КонецЕсли;
			Иначе
				СтрокаСписка.Данные[КолонкаИндикатора] = 0;
				Если ТекстЯчейки <> Неопределено Тогда
					ТекстЯчейки.Значение = 1;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// В форме объекта выводит группу с картинкой и надписью, сигнализирующими о наличии проблем с этим объектом. 
// Вызывается из события ПриЧтенииНаСервере формы объекта.
//
// Параметры:
//   Форма         - УправляемаяФорма - Форма объекта.
//   ТекущийОбъект - ДокументОбъект.ИмяДокумента, СправочникОбъект.ИмяСправочника,
//                         ПланОбменаОбъект.ИмяПланаОбмена, ПланВидовХарактеристикОбъект.ИмяПланаВидаХарактеристик,
//                         ПланСчетовОбъект.ИмяПланаСчетов, ПланВидовРасчетаОбъект.ИмяПланаВидовРасчета,
//                         ЗадачаОбъект.ИмяЗадачи - Объект, который будет прочитан.
//
Процедура ПриЧтенииНаСервере(Форма, ТекущийОбъект) Экспорт
	
	ИмяПроцедуры = "КонтрольВеденияУчета.ПриЧтенииНаСервере";
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(ИмяПроцедуры, "Форма", Форма, Тип("УправляемаяФорма"));
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(ИмяПроцедуры, "ТекущийОбъект", ТекущийОбъект, 
		КонтрольВеденияУчетаСлужебныйПовтИсп.ОписаниеТипаВсеОбъекты());
	
	Если Не КонтрольВеденияУчетаСлужебный.ПодсистемаДоступна() Тогда
		Возврат;
	КонецЕсли;
	
	ГлобальныеНастройки        = КонтрольВеденияУчетаСлужебный.ГлобальныеНастройки();
	СсылкаНаОбъект             = ТекущийОбъект.Ссылка;
	ЭлементыУправляемойФормы   = Форма.Элементы;
	КоличествоПроблемПоОбъекту = КоличествоПроблемПоОбъекту(СсылкаНаОбъект);
	КлючУникальностиИмен       = ОбщегоНазначения.КонтрольнаяСуммаСтрокой(СсылкаНаОбъект.Метаданные().ПолноеИмя()
		+ ПолучитьРазделительПути() + Форма.ИмяФормы);
		
	ДекорацияГруппа = ЭлементыУправляемойФормы.Найти("ГруппаИндикатораОшибки_" + КлючУникальностиИмен);
		
	Если КоличествоПроблемПоОбъекту = 0 Тогда
		
		Если ДекорацияГруппа <> Неопределено Тогда
			ЭлементыУправляемойФормы.Удалить(ДекорацияГруппа);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ПараметрыГруппыИндикации = Новый Структура;
	КонтрольВеденияУчетаСлужебный.ПриОпределенииПараметровГруппыИндикации(ПараметрыГруппыИндикации, ТипЗнч(СсылкаНаОбъект));
	КонтрольВеденияУчетаПереопределяемый.ПриОпределенииПараметровГруппыИндикации(ПараметрыГруппыИндикации, ТипЗнч(СсылкаНаОбъект));
	
	Если ДекорацияГруппа <> Неопределено Тогда
		
		ДекорацияНадпись = ЭлементыУправляемойФормы.Найти("ДекорацияНадпись_" + КлючУникальностиИмен);
		Если ДекорацияНадпись <> Неопределено Тогда
			ДекорацияНадпись.Заголовок = КонтрольВеденияУчетаСлужебный.СформироватьОбщуюСтрокуИндикатор(Форма, СсылкаНаОбъект, КоличествоПроблемПоОбъекту,
				ГлобальныеНастройки.ПояснениеИндикатораПроблем, ГлобальныеНастройки.ГиперссылкаИндикатораПроблем);
		КонецЕсли;
		
	Иначе
		
		ГруппаИндикатораОшибки = КонтрольВеденияУчетаСлужебный.РазместитьГруппуИндикатораОшибки(Форма, КлючУникальностиИмен,
			ПараметрыГруппыИндикации.ИмяРодителяГруппы, ПараметрыГруппыИндикации.ВыводитьСнизу);
		
		ОбщаяСтрокаИндикатор = КонтрольВеденияУчетаСлужебный.СформироватьОбщуюСтрокуИндикатор(Форма, СсылкаНаОбъект, КоличествоПроблемПоОбъекту,
			ГлобальныеНастройки.ПояснениеИндикатораПроблем, ГлобальныеНастройки.ГиперссылкаИндикатораПроблем);
		
		КонтрольВеденияУчетаСлужебный.ЗаполнитьГруппуИндикатораОшибки(Форма, ГруппаИндикатораОшибки, КлючУникальностиИмен,
			ОбщаяСтрокаИндикатор, ГлобальныеНастройки.КартинкаИндикатораПроблем);
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Прочие процедуры и функции.

// Возвращает виды проверки по переданным параметрам.
//
// Параметры:
//   ВидПроверок                - Строка, Массив - строковый идентификатор вида проверки (Свойство1)
//                                либо массив идентификаторов (Свойство1...СвойствоN), либо ссылка на вид проверки.
//   ПоискПоТочномуСоответствию - Булево - Регулирует возможности точности. Если Истина, то поиск ведется
//                                по переданным свойствам на равенство, остальные свойства должны быть равны
//                                Неопределено (табличная часть дополнительных свойств должна быть пуста).
//                                Если Ложь, то значения остальных свойств могут быть произвольными, главное
//                                чтобы соответствующие свойства были равны свойствам структуры. По умолчанию Истина.
//
// Возвращаемое значение:
//   Массив из СправочникСсылка.ВидыПроверок - Найденные элементы справочника, либо пустой массив
//   в случае если поиск не дал результата. в случае поиска по точному соответствию массив
//   содержит единственный элемент.
//
Функция ВидыПроверок(ВидПроверок, ПоискПоТочномуСоответствию = Истина) Экспорт
	
	ИмяПроцедуры = "КонтрольВеденияУчета.ВидыПроверок";
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(ИмяПроцедуры, "ВидПроверок", ВидПроверок, КонтрольВеденияУчетаСлужебный.ОписаниеТипаВидПроверки());
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(ИмяПроцедуры, "ПоискПоТочномуСоответствию", ПоискПоТочномуСоответствию, Тип("Булево"));
	
	Возврат КонтрольВеденияУчетаСлужебный.ПолучитьВидыПроверок(ВидПроверок, ПоискПоТочномуСоответствию);
	
КонецФункции

// Возвращает существующий или создает новый элемент справочника ВидыПроверок 
// для регистрации или отбора результатов ведения учета.
//
// Параметры:
//     ПараметрыВыполненияПроверки - Структура - см. КонтрольВеденияУчета.ПараметрыВыполненияПроверки.
//     ТолькоПоиск - Булево - если Истина и вид проверки с заданными параметрами не существует, 
//                   возвращается пустая ссылка; если Ложь, то создается элемент и возвращается ссылка на него.
//
// Возвращаемое значение:
//   СправочникСсылка.ВидыПроверок - найденный существующий или созданный элемент справочника.
//      В случае если осуществлялся только поиск (параметр ТолькоПоиск = Истина) 
//      и элемент не был найден, возвращается пустая ссылка на справочник СправочникСсылка.ВидыПроверок.
//
Функция ВидПроверки(Знач ПараметрыВыполненияПроверки, ТолькоПоиск = Ложь) Экспорт
	
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр("КонтрольВеденияУчета.ВидПроверки",
		"ПараметрыВыполненияПроверки", ПараметрыВыполненияПроверки, Тип("Структура"));
	Для Каждого ТекущееПоле Из ПараметрыВыполненияПроверки Цикл
		ОбщегоНазначенияКлиентСервер.ПроверитьПараметр("КонтрольВеденияУчета.ВидПроверки",
			"ПараметрыВыполненияПроверки." + ТекущееПоле.Ключ, ТекущееПоле.Значение, КонтрольВеденияУчетаСлужебный.ОжидаемыеТипыСвойствВидовПроверок());
	КонецЦикла;
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр("КонтрольВеденияУчета.ВидПроверки", "ТолькоПоиск", ТолькоПоиск, Тип("Булево"));
	
	Возврат КонтрольВеденияУчетаСлужебный.ВидПроверки(ПараметрыВыполненияПроверки, ТолькоПоиск);
	
КонецФункции

#Область УстаревшиеПроцедурыИФункции

// Устарела. Следует использовать функцию ПодробнаяИнформацияПоВидамПроверок.
// Возвращает подробные сведения о выявленных проблемах указанного вида проверки.
//
// Параметры:
//   ВидПроверок                - СправочникСсылка.ВидыПроверок - ссылка на вид проверки.
//                              - Строка - строковый идентификатор вида проверки Свойство1.
//                              - Массив - строковые идентификаторы вида проверки Свойство1...СвойствоN.
//   ПоискПоТочномуСоответствию - Булево - Регулирует возможности точности. Если Истина, то поиск ведется
//                                по переданным свойствам на равенство, остальные свойства должны быть равны
//                                Неопределено (табличная часть дополнительных свойств должна быть пуста).
//                                Если Ложь, то значения остальных свойств могут быть произвольными, главное
//                                чтобы соответствующие свойства были равны свойствам структуры. По умолчанию Истина.
//
// Возвращаемое значение:
//   ТаблицаЗначений            - таблица проблем:
//     * ПроблемныйОбъект         - ЛюбаяСсылка - Ссылка на объект "Источник" проблем.
//     * ПравилоПроверки          - СправочникСсылка.ПравилаПроверкиУчета - Ссылка на выполненную проверку.
//     * УточнениеПроблемы        - Строка - Строка-уточнение найденной проблемы.
//     * ВажностьПроблемы         - ПеречислениеСсылка.ВажностьПроблемыУчета - Важность проблемы учета
//                                  "Информация", "Предупреждение", "Ошибка" и "ПолезныйСовет".
//     * Ответственный            - СправочникСсылка.Пользователи - Заполнен если есть возможность
//                                  идентифицировать ответственного в проблемном объекте.
//     * ДополнительнаяИнформация - ХранилищеЗначений - Служебное свойство с дополнительными
//                                  сведениями, связанными с выявленной проблемой
//     * Выявлено                 - Дата - Серверное время идентификации проблемы.
//
// Пример:
//   1) Результат = ПодробнаяИнформацияПоВидамПроверок("СистемныеПроверки");
//   2) ВидПроверок = Новый Массив;
//      ВидПроверок.Добавить("ЗакрытиеМесяца");
//      ВидПроверок.Добавить(Организация);
//      ВидПроверок.Добавить(МесяцЗакрытия);
//      Результат = ПодробнаяИнформацияПоВидамПроверок(ВидПроверок);
//
Функция ПодробнаяИнформацияПоВидамПроверки(ВидПроверок, ПоискПоТочномуСоответствию = Истина) Экспорт
	
	ИмяПроцедуры = "КонтрольВеденияУчета.ПодробнаяИнформацияПоВидамПроверок";
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(ИмяПроцедуры, "ВидПроверок", ВидПроверок, КонтрольВеденияУчетаСлужебный.ОписаниеТипаВидПроверки());
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(ИмяПроцедуры, "ПоискПоТочномуСоответствию", ПоискПоТочномуСоответствию, Тип("Булево"));
	
	ПодробнаяИнформация = Новый ТаблицаЗначений;
	МассивВидовПроверок = Новый Массив;
	
	Если ТипЗнч(ВидПроверок) = Тип("СправочникСсылка.ВидыПроверок") Тогда
		МассивВидовПроверок.Добавить(ВидПроверок);
	Иначе
		МассивВидовПроверок = КонтрольВеденияУчетаСлужебный.ПолучитьВидыПроверок(ВидПроверок, ПоискПоТочномуСоответствию);
	КонецЕсли;
	
	Если МассивВидовПроверок.Количество() = 0 Тогда
		Возврат ПодробнаяИнформация;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РезультатыПроверкиУчета.ПроблемныйОбъект КАК ПроблемныйОбъект,
	|	РезультатыПроверкиУчета.ВажностьПроблемы КАК ВажностьПроблемы,
	|	РезультатыПроверкиУчета.ПравилоПроверки КАК ПравилоПроверки,
	|	РезультатыПроверкиУчета.ВидПроверки КАК ВидПроверки
	|ИЗ
	|	РегистрСведений.РезультатыПроверкиУчета КАК РезультатыПроверкиУчета
	|ГДЕ
	|	НЕ РезультатыПроверкиУчета.ИгнорироватьПроблему
	|	И РезультатыПроверкиУчета.ВидПроверки В (&МассивВидовПроверок)");
	
	Запрос.УстановитьПараметр("МассивВидовПроверок", МассивВидовПроверок);
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		ПодробнаяИнформация = Результат.Выгрузить();
	КонецЕсли;
	
	Возврат ПодробнаяИнформация;
	
КонецФункции

// Устарела. Следует использовать функцию СводнаяИнформацияПоВидамПроверок.
// Возвращает сводку по количеству выявленных проблем указанного вида проверки.
//
// Параметры:
//   ВидПроверок                - СправочникСсылка.ВидыПроверок - ссылка на вид проверки.
//                              - Строка - строковый идентификатор вида проверки Свойство1.
//                              - Массив - строковые идентификаторы вида проверки Свойство1...СвойствоN.
//   ПоискПоТочномуСоответствию - Булево - Регулирует возможности точности. Если Истина, то поиск ведется
//                                по переданным свойствам на равенство, остальные свойства должны быть равны
//                                Неопределено (табличная часть дополнительных свойств должна быть пуста).
//                                Если Ложь, то значения остальных свойств могут быть произвольными, главное
//                                чтобы соответствующие свойства были равны свойствам структуры. По умолчанию Истина.
//
// Возвращаемое значение:
//  Структура - сводка:
//    * Количество - Число - общее количество найденных проблем.
//    * ЕстьОшибки - Булево - признак того, имеются ли ошибки среди найденных проблем (с важностью "Ошибка").
//
// Пример:
//   1) Результат = СводнаяИнформацияПоВидамПроверок("СистемныеПроверки");
//   2) ВидПроверок = Новый Массив;
//      ВидПроверок.Добавить("ЗакрытиеМесяца");
//      ВидПроверок.Добавить(Организация);
//      ВидПроверок.Добавить(МесяцЗакрытия);
//      Результат = СводнаяИнформацияПоВидамПроверок(ВидПроверок);
//
Функция СводнаяИнформацияПоВидамПроверки(ВидПроверок, ПоискПоТочномуСоответствию = Истина) Экспорт
	
	ИмяПроцедуры = "КонтрольВеденияУчета.СводнаяИнформацияПоВидамПроверок";
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(ИмяПроцедуры, "ВидПроверок", ВидПроверок, КонтрольВеденияУчетаСлужебный.ОписаниеТипаВидПроверки());
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(ИмяПроцедуры, "ПоискПоТочномуСоответствию", ПоискПоТочномуСоответствию, Тип("Булево"));
	
	СводнаяИнформация = Новый Структура;
	СводнаяИнформация.Вставить("Количество", 0);
	СводнаяИнформация.Вставить("ЕстьОшибки", Ложь);
	
	МассивВидовПроверок = Новый Массив;
	Если ТипЗнч(ВидПроверок) = Тип("СправочникСсылка.ВидыПроверок") Тогда
		МассивВидовПроверок.Добавить(ВидПроверок);
	Иначе
		МассивВидовПроверок = КонтрольВеденияУчетаСлужебный.ПолучитьВидыПроверок(ВидПроверок, ПоискПоТочномуСоответствию);
		Если МассивВидовПроверок.Количество() = 0 Тогда
			Возврат СводнаяИнформация;
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО(*) КАК Количество,
	|	ЕСТЬNULL(МАКСИМУМ(ВЫБОР
	|				КОГДА РезультатыПроверкиУчета.ВажностьПроблемы = ЗНАЧЕНИЕ(Перечисление.ВажностьПроблемыУчета.Ошибка)
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ), ЛОЖЬ) КАК ЕстьОшибки
	|ИЗ
	|	РегистрСведений.РезультатыПроверкиУчета КАК РезультатыПроверкиУчета
	|ГДЕ
	|	НЕ РезультатыПроверкиУчета.ИгнорироватьПроблему
	|	И РезультатыПроверкиУчета.ВидПроверки В (&МассивВидовПроверок)");
	
	Запрос.УстановитьПараметр("МассивВидовПроверок", МассивВидовПроверок);
	Результат = Запрос.Выполнить().Выбрать();
	Результат.Следующий();
	
	ЗаполнитьЗначенияСвойств(СводнаяИнформация, Результат);
	
	Возврат СводнаяИнформация;
	
КонецФункции

#КонецОбласти

#КонецОбласти
