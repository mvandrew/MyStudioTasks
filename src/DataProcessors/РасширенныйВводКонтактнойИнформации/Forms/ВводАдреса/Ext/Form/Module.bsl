// Форма параметризуется:
//
//      Заголовок     - Строка  - заголовок формы.
//      ЗначенияПолей - Строка  - сериализованное значение контактной информации или пустая строка для 
//                                ввода нового.
//      Представление - Строка  - представление адреса (используется только при работе со старыми данными).
//      ВидКонтактнойИнформации - СправочникСсылка.ВидыКонтактнойИнформации, Структура - описание того, что мы
//                                редактируем.
//      Комментарий  - Строка   - необязательный комментарий, для подстановки в поле "Комментарий".
//
//      ВозвращатьСписокЗначений - Булево - необязательный флаг того, что возвращаемое значение поля.
//                                 КонтактнаяИнформация будет иметь тип СписокЗначений (совместимость).
//
//  Результат выбора:
//      Структура - поля:
//          * КонтактнаяИнформация   - Строка - XML контактной информации.
//          * Представление          - Строка - Представление.
//          * Комментарий            - Строка - Комментарий.
//          * ВведеноВСвободнойФорме - Булево - флаг ввода.
//

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.Свойство("ОткрытаПоСценарию") Тогда
		ВызватьИсключение НСтр("ru = 'Обработка не предназначена для непосредственного использования.'");
	КонецЕсли;
	
	// Настройки формы
	ЭтоВебСервис = Обработки.РасширенныйВводКонтактнойИнформации.КлассификаторДоступенЧерезВебСервис();
	ЕстьПравоЗагружатьКлассификатор = Обработки.РасширенныйВводКонтактнойИнформации.ЕстьВозможностьИзмененияАдресногоКлассификатора() И Не ЭтоВебСервис;
	ЕстьКлассификатор = УправлениеКонтактнойИнформациейСлужебныйПовтИсп.АдресныйКлассификаторДоступен();
	ЭтоТакси = КлиентскоеПриложение.ТекущийВариантИнтерфейса() = ВариантИнтерфейсаКлиентскогоПриложения.Такси; // Группа команд "все действия" зависит от интерфейса.
	
	УстановитьВозможностьПодбораИндексаИНомеровДомов(ЭтоВебСервис);
	
	Параметры.Свойство("ВозвращатьСписокЗначений", ВозвращатьСписокЗначений);
	// Внутренняя инициализация
	ЦветФонаУправляющегоПоля = ЦветаСтиля.ФонУправляющегоПоля;
	ЦветФонаФормы            = ЦветаСтиля.ЦветФонаФормы;
	АвтоЦвет                 = Новый Цвет;
	ОсновнаяСтрана           = РаботаСАдресами.ОсновнаяСтрана();
	
	ВидКонтактнойИнформации = УправлениеКонтактнойИнформациейСлужебный.СтруктураВидаКонтактнойИнформации(Параметры.ВидКонтактнойИнформации);
	
	Если ВидКонтактнойИнформации.ХранитьИсториюИзменений Тогда
		Если Параметры.Свойство("КонтактнаяИнформацияОписаниеДополнительныхРеквизитов") Тогда
			Для каждого СтрокаКИ Из Параметры.КонтактнаяИнформацияОписаниеДополнительныхРеквизитов Цикл
				НоваяСтрока = КонтактнаяИнформацияОписаниеДополнительныхРеквизитов.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаКИ);
			КонецЦикла;
		Иначе
			Элементы.ИсторияИзменений.Видимость           = Ложь;
			Элементы.ПоказатьИсториюВсеДействия.Видимость = Ложь;
		КонецЕсли;
		Элементы.ИсторияИзмененийГиперссылка.Видимость = НЕ Параметры.Свойство("ИзФормыИстории");
		ВводНовогоАдреса = ?(Параметры.Свойство("ВводНовогоАдреса"), Параметры.ВводНовогоАдреса, Ложь);
		Если ВводНовогоАдреса Тогда
			ДействуетС = Параметры.ДействуетС;
		Иначе
			ДействуетС = ?(ЗначениеЗаполнено(Параметры.ДействуетС), Параметры.ДействуетС, ТекущаяДатаСеанса());
		КонецЕсли;
		ОтобразитьИнформациюОДатахДействияАдреса(ДействуетС);
	Иначе
		Элементы.ИсторияИзменений.Видимость           = Ложь;
		Элементы.ПоказатьИсториюВсеДействия.Видимость = Ложь;
		Элементы.ГруппаИсторическийАдрес.Видимость    = Ложь;
	КонецЕсли;
	
	Команды.ПроверитьЗаполнениеАдреса.Подсказка = ?(ВидКонтактнойИнформации.ПроверятьПоФИАС,
		НСтр("ru = 'Проверить заполнение адреса в формате ФИАС'"),
		НСтр("ru = 'Проверить заполнение адреса в формате КЛАДР'"));
	
	ВидКонтактнойИнформации.Вставить("ФорматАдреса", "ФИАС"); // Всегда используем разрезы ФИАС.
	Заголовок = ?(ПустаяСтрока(Параметры.Заголовок), ВидКонтактнойИнформации.Наименование, Параметры.Заголовок);
	
	СкрыватьНеактуальныеАдреса  = ВидКонтактнойИнформации.СкрыватьНеактуальныеАдреса;
	ТолькоНациональныйАдрес     = ВидКонтактнойИнформации.ТолькоНациональныйАдрес;
	ТипКонтактнойИнформации     = ВидКонтактнойИнформации.Тип;
	
	ЗаполнитьПредопределенныеВариантыАдреса(Параметры);
	
	// Пытаемся заполнить из параметров.
	Если УправлениеКонтактнойИнформациейКлиентСервер.ЭтоКонтактнаяИнформацияВXML(Параметры.ЗначенияПолей)
			И ТипКонтактнойИнформации = Перечисления.ТипыКонтактнойИнформации.Адрес Тогда
		РезультатыЧтения = Новый Структура;
		XDTOКонтактная = УправлениеКонтактнойИнформациейСлужебный.КонтактнаяИнформацияИзXML(Параметры.ЗначенияПолей, ТипКонтактнойИнформации, РезультатыЧтения);
		Если РезультатыЧтения.Свойство("ТекстОшибки") Тогда
			// Распознали с ошибками, сообщим при открытии.
			ТекстПредупрежденияПриОткрытии = РезультатыЧтения.ТекстОшибки;
			XDTOКонтактная.Представление   = Параметры.Представление;
			XDTOКонтактная.Состав.Страна   = Строка(ОсновнаяСтрана);
		КонецЕсли;
	Иначе
		XDTOКонтактная = УправлениеКонтактнойИнформациейСлужебный.АдресXMLВXDTO(Параметры.ЗначенияПолей, Параметры.Представление, );
		Если Параметры.Свойство("Страна") И ЗначениеЗаполнено(Параметры.Страна) Тогда
			Если ТипЗнч(Параметры.Страна) = ТипЗнч(Справочники.СтраныМира.ПустаяСсылка()) Тогда
				XDTOКонтактная.Состав.Страна = Параметры.Страна.Наименование;
			Иначе
				XDTOКонтактная.Состав.Страна = Строка(Параметры.Страна);
			КонецЕсли;
		Иначе
			XDTOКонтактная.Состав.Страна = ОсновнаяСтрана.Наименование;
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Комментарий <> Неопределено Тогда
		// Ставим новый комментарий, иначе он придет из информации.
		XDTOКонтактная.Комментарий = Параметры.Комментарий;
	КонецЕсли;
	
	ЗначениеРеквизитовПоКонтактнойИнформации(ЭтотОбъект, XDTOКонтактная);
	
	Если ЗначениеЗаполнено(Страна) Тогда
		КодСтраны = Страна.Код;
	Иначе
		Страна    = ОсновнаяСтрана;
		КодСтраны = ОсновнаяСтрана.Код;
	КонецЕсли;
	
	ОтобразитьДополнительныеЗданияИПомещения();
	
	Если ТолькоНациональныйАдрес Тогда
		Элементы.Страна.Доступность = Ложь;
		Элементы.Страна.ЦветФона = АвтоЦвет;
		// Проверяем на корректность
		Если Страна <> ОсновнаяСтрана Тогда
			// Считаем адрес российским
			ПредставлениеАдресаИзменено = Истина;
			ТекстПредупрежденияПриОткрытии = НСтр("ru = 'Адрес введен некорректно: допускается ввод только национальных адресов. 
			|Значение поля ""Страна"" было изменено на %1, необходимо проверить остальные поля.'");
			ТекстПредупрежденияПриОткрытии = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстПредупрежденияПриОткрытии, ОсновнаяСтрана.Наименование);
			ПолеПредупрежденияПриОткрытии = "Страна";
			Модифицированность = Истина;
		КонецЕсли;
		Страна = ОсновнаяСтрана;
	КонецЕсли;
	
	// Проверка какой адрес
	Если Страна = ОсновнаяСтрана Тогда
		Элементы.ОКТМО.Видимость = ВидКонтактнойИнформации.УказыватьОКТМО;
		
		Если Не ЭтоВебСервис Тогда
			// российский адрес
			Если XDTOКонтактная.Состав.Состав <> Неопределено И ТипЗнч(XDTOКонтактная.Состав.Состав) <> Тип("Строка") Тогда
				Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.АдресныйКлассификатор") Тогда
					МодульАдресныйКлассификаторСлужебный = ОбщегоНазначения.ОбщийМодуль("АдресныйКлассификаторСлужебный");
					СведенияОРегионе = МодульАдресныйКлассификаторСлужебный.СведенияОРегионе(XDTOКонтактная.Состав.Состав.СубъектРФ);
					Если СведенияОРегионе.Загружен = Ложь Тогда
						ДоступностьКлассификатора = Новый Структура("Отказ, КраткоеПредставлениеОшибки");
						ДоступностьКлассификатора.Отказ = Истина;
						ДоступностьКлассификатора.КраткоеПредставлениеОшибки = 
							СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Необходимо загрузить адресные сведения по региону ""%1"" (в меню ""%2"").'"),
							XDTOКонтактная.Состав.Состав.СубъектРФ,
							?(ЭтоТакси, НСтр("ru = 'Еще'"), НСтр("ru = 'Все действия'")));
						Элементы.АвторизацияНаСайтеПоддержкиПользователей.Видимость = Ложь;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ УправлениеКонтактнойИнформациейСлужебный.XDTOКонтактнаяИнформацияЗаполнена(XDTOКонтактная) Тогда
			РазрешитьВводАдресаВСвободнойФорме = Ложь;
		КонецЕсли;
	Иначе
		ЕстьПравоЗагружатьКлассификатор = Ложь;
	КонецЕсли;
	
	ОпределитьОтображениеЭлементовНаФорме(ЭтоТакси);
	
	ДоступностьКлассификатора = Неопределено;
	Элементы.АдресСтраницаКомментарий.Картинка = ОбщегоНазначенияКлиентСервер.КартинкаКомментария(Комментарий);
	СообщениеОНеобходимостиОбновленияКлассификатора(НаселенныйПунктДетально.Регион.Идентификатор);
	
	УстановитьКлючИспользованияФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.ДобавитьОбъект.Доступность = МожноДобавлятьДополнительныеОбъекты();
	ОбработкаИзмененияСтраныКлиент();
	Если ЗначениеЗаполнено(ТекстПредупрежденияПриОткрытии) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстПредупрежденияПриОткрытии,, ПолеПредупрежденияПриОткрытии);
	КонецЕсли;
	
	ПроверитьДоступностьКлассификатора();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("ПодтвердитьИЗакрыть", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, ЗавершениеРаботы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "АдресныйКлассификаторАктуализирован" ИЛИ ИмяСобытия = "ЗагруженАдресныйКлассификатор" Тогда
		Если ЕстьКлассификатор И СведенияОРегионе(НаселенныйПунктДетально.Регион.Представление).Загружен = Истина Тогда
			Элементы.ГруппаОписаниеНедоступностиСервера.ТекущаяСтраница = Элементы.СервисДоступен;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтранаПриИзменении(Элемент)
	ОбработкаИзмененияСтраныКлиент();
	
	Контекст = КонтекстФормыКлиент();
	ЗаполнитьПредставлениеАдреса(Контекст);
	КонтекстФормыКлиент(Контекст);
	
#Если ВебКлиент Тогда
	// Обход особенности платформы.
	Элемент.ОбновитьТекстРедактирования();
#КонецЕсли

	// Выводим всегда представление.
	Элементы.АдресПредставлениеКомментарий.ТекущаяСтраница = Элементы.АдресСтраницаПредставление;
КонецПроцедуры

&НаКлиенте
Процедура СтранаОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура СтранаАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	Если Ожидание = 0 Тогда
		// Формирование списка быстрого выбора.
		Если ПустаяСтрока(Текст) Тогда
			ДанныеВыбора = Новый СписокЗначений;
		КонецЕсли;
		Возврат;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СтранаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ПустаяСтрока(Текст) Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
#Если ВебКлиент Тогда
	// Обход особенности платформы.
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора         = Новый СписокЗначений;
	ДанныеВыбора.Добавить(Страна);
#КонецЕсли

КонецПроцедуры

&НаКлиенте
Процедура СтранаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	УправлениеКонтактнойИнформациейКлиент.СтранаМираОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ИндексИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	Если СтрДлина(СокрЛП(Текст)) = 6 И СтрСравнить(Индекс, Текст) <> 0 И Страна = ОсновнаяСтрана Тогда
		СтандартнаяОбработка = Ложь;
		ФормаВыбораУлицПоИндексу(Текст);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИндексОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	Если СтрДлина(СокрЛП(Текст)) = 6 И СтрСравнить(Индекс, Текст) <> 0 И Страна = ОсновнаяСтрана Тогда
		СтандартнаяОбработка = Ложь;
		ФормаВыбораУлицПоИндексу(Текст);
	Иначе
		Индекс = Текст;
		ОбновитьПредставлениеАдреса();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ОткрытьФормуВыборАдресаПоПочтовомуИндексу(Результат, ПараметрыФормы) Экспорт
	Если Результат <> Неопределено Тогда
		Оповещение = Новый ОписаниеОповещения("ПослеВыбораАдресаПоИндексу", ЭтотОбъект);
		ОткрытьФорму("Обработка.РасширенныйВводКонтактнойИнформации.Форма.ВыборАдресаПоПочтовомуИндексу", ПараметрыФормы, Элементы.Индекс,,,, Оповещение);
	Иначе
		ОбновитьПредставлениеАдреса();
	КонецЕсли;
КонецФункции

&НаКлиенте
Процедура ФормаВыбораУлицПоИндексу(Знач Текст)
	
	Модифицированность = Истина;
	Индекс = Текст;
	Если ЕстьКлассификатор Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Индекс", СокрЛП(Текст));
		ПараметрыФормы.Вставить("СкрыватьНеактуальныеАдреса", СкрыватьНеактуальныеАдреса);
		ПараметрыФормы.Вставить("ФорматАдреса", ВидКонтактнойИнформации.ФорматАдреса);
		ПараметрыФормы.Вставить("ВыборАдресаПоПочтовомуИндексу", Истина);
		
		Если ЭтоВебСервис ИЛИ НаличиеИндексаВКлассификаторе(Число(Текст)) Тогда
			Оповещение = Новый ОписаниеОповещения("ПослеВыбораАдресаПоИндексу", ЭтотОбъект);
			ОткрытьФорму("Обработка.РасширенныйВводКонтактнойИнформации.Форма.ВыборАдресаПоПочтовомуИндексу", ПараметрыФормы, Элементы.Индекс,,,, Оповещение);
		ИначеЕсли ЕстьПравоЗагружатьКлассификатор Тогда
			Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.АдресныйКлассификатор") Тогда
				ОповещениеОЗакрытие = Новый ОписаниеОповещения("ОткрытьФормуВыборАдресаПоПочтовомуИндексу", ЭтотОбъект, ПараметрыФормы);
				МодульАдресныйКлассификаторКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("АдресныйКлассификаторКлиент");
				МодульАдресныйКлассификаторКлиент.ПоказатьФормуЗагрузкиАдресногоКлассификатора(ОповещениеОЗакрытие, ПараметрыФормы);
			КонецЕсли;
		КонецЕсли;
	Иначе
		ОбновитьПредставлениеАдреса();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораАдресаПоИндексу(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		Если Результат.Отказ Тогда
			Если Результат.Свойство("Индекс") И НЕ ПустаяСтрока(Результат.Индекс) Тогда
				ОбновитьПредставлениеАдреса();
				Возврат;
			Иначе
				Если НЕ ПодборПоИндексуДоступен Тогда
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Индекс %1 не найден в адресном классификаторе'"),
						Формат(Результат.Индекс, "ЧРГ=' '; ЧГ=0"));
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,, "Объект.Индекс");
				Иначе
					ПроверитьДоступностьКлассификатора();
				КонецЕсли;
			КонецЕсли;
			Возврат;
		КонецЕсли;
		
		Контекст = КонтекстФормыКлиент();
		ЗаполнитьАдресПоДаннымИндекса(Контекст, Результат);
		КонтекстФормыКлиент(Контекст);
		Модифицированность = Истина;
		ТекущийЭлемент = Элементы.Дом;
	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьАдресПоДаннымИндекса(Контекст, Знач ДанныеИндекса)
	
	ОчиститьАдресСервер(Контекст);
	
	XDTOКонтактнаяИнфо = КонтактнаяИнформацияПоЗначениюРеквизитов(Контекст);
	
	// Предполагаем, что адрес все еще российский.
	XDTOАдрес = XDTOКонтактнаяИнфо.Состав.Состав;
	
	Обработки.РасширенныйВводКонтактнойИнформации.ПочтовыйИндексАдреса(XDTOАдрес, ДанныеИндекса.Индекс);
	Обработки.РасширенныйВводКонтактнойИнформации.УстановитьНаселенныйПунктАдресаПоИдентификатору(XDTOАдрес, ДанныеИндекса.Идентификатор);
	Обработки.РасширенныйВводКонтактнойИнформации.УстановитьУлицуАдресаПоИдентификатору(XDTOАдрес, ДанныеИндекса.Идентификатор);
	
	ЗначениеРеквизитовПоКонтактнойИнформации(Контекст, XDTOКонтактнаяИнфо);
	ЗаполнитьПредставлениеАдреса(Контекст);
КонецПроцедуры

&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	
	ПодключитьОбработчикОжидания("УстановитьПиктограммуКомментария", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ТипДомаПриИзменении(Элемент)
	
	Контекст = КонтекстФормыКлиент();
	ЗаполнитьПредставлениеАдреса(Контекст);
	КонтекстФормыКлиент(Контекст);
	
КонецПроцедуры

&НаКлиенте
Процедура ДомПриИзменении(Элемент)
	ОбновитьИндексИПредставление();
КонецПроцедуры

&НаКлиенте
Процедура ДомОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		СтандартнаяОбработка = Ложь;
		ЗаполнитьПоляДомовИСтроений(ВыбранноеЗначение);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СтроениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
			СтандартнаяОбработка = Ложь;
		ЗаполнитьПоляДомовИСтроений(ВыбранноеЗначение);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТипСтроенияПриИзменении(Элемент)
	
	Контекст = КонтекстФормыКлиент();
	ЗаполнитьПредставлениеАдреса(Контекст);
	КонтекстФормыКлиент(Контекст);
	
КонецПроцедуры

&НаКлиенте
Процедура СтроениеПриИзменении(Элемент)
	ОбновитьИндексИПредставление();
КонецПроцедуры

&НаКлиенте
Процедура СтроениеАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) 
		И (ЗначениеЗаполнено(ЭтотОбъект.ИдентификаторНаселенногоПункта)
		ИЛИ ЗначениеЗаполнено(ЭтотОбъект.ИдентификаторУлицы)) Тогда
			Идентификатор = ?(ЗначениеЗаполнено(ИдентификаторУлицы), 
				ИдентификаторУлицы, ИдентификаторНаселенногоПункта);

			ДанныеВыбора = СписокАвтоподбораВариантовДомов(Идентификатор, Дом);
			Если ДанныеВыбора.Количество() > 1 Тогда
				СтандартнаяОбработка = Ложь;
			КонецЕсли;
		КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ТипПомещенияПриИзменении(Элемент)
	
	Если Элемент.ВыделенныйТекст = НаименованиеПриДобавлениеПроизвольногоПомещения() Тогда
		ЭтотОбъект[Элемент.Имя] = "";
	КонецЕсли;
	
	Контекст = КонтекстФормыКлиент();
	ЗаполнитьПредставлениеАдреса(Контекст);
	КонтекстФормыКлиент(Контекст);
	
КонецПроцедуры

&НаКлиенте
Процедура ПомещениеПриИзменении(Элемент)
	
	Контекст = КонтекстФормыКлиент();
	ЗаполнитьПредставлениеАдреса(Контекст);
	КонтекстФормыКлиент(Контекст);
	
КонецПроцедуры

&НаКлиенте
Процедура НаселенныйПунктПриИзменении(Элемент)
	
	Контекст = КонтекстФормыКлиент();
	ОбработкаИзмененияНаселенногоПунктаСервер(Контекст);
	КонтекстФормыКлиент(Контекст);
	
КонецПроцедуры

&НаКлиенте
Процедура НаселенныйПунктОчистка(Элемент, СтандартнаяОбработка)
	ИдентификаторНаселенногоПункта = Неопределено;
КонецПроцедуры

&НаКлиенте
Процедура НаселенныйПунктНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	// Если пришли впрямую после редактирования, то сбрасываем адрес.
	Если Элемент.ТекстРедактирования <> НаселенныйПункт Тогда
		Модифицированность = Истина;
		НаселенныйПункт    = Элемент.ТекстРедактирования;
		
		ИдентификаторНаселенногоПункта = Неопределено;
		
		Контекст = КонтекстФормыКлиент();
		ОбработкаИзмененияНаселенногоПунктаСервер(Контекст, Истина);
		КонтекстФормыКлиент(Контекст);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НаселенныйПунктДетально", НаселенныйПунктДетально);
	ПараметрыФормы.Вставить("ИдентификаторНаселенногоПункта", ИдентификаторНаселенногоПункта);
	ПараметрыФормы.Вставить("СкрыватьНеактуальныеАдреса", СкрыватьНеактуальныеАдреса);
	ПараметрыФормы.Вставить("ФорматАдреса", ВидКонтактнойИнформации.ФорматАдреса);
	ПараметрыФормы.Вставить("СервисКлассификатораНедоступен", НЕ ЕстьКлассификатор);
	
	ОткрытьФорму("Обработка.РасширенныйВводКонтактнойИнформации.Форма.НаселенныйПунктАдреса", ПараметрыФормы, Элемент, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура НаселенныйПунктОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ВыбранноеЗначение=Неопределено Тогда
		Возврат;
	КонецЕсли;
	Модифицированность = Истина;
	ТипЗначения = ТипЗнч(ВыбранноеЗначение);
	ФормироватьДетально = Истина;
	
	Если ТипЗначения = Тип("Структура") Тогда
		
		Если ВыбранноеЗначение.Представление <> НаселенныйПункт Тогда
			ИдентификаторУлицы = Неопределено;
		КонецЕсли;
		
		// Результат автоподбора или выбора кнопкой, берем все оттуда.
		ИдентификаторНаселенногоПункта = ВыбранноеЗначение.Идентификатор;
		НаселенныйПункт                = ВыбранноеЗначение.Представление;
		
		Если ВыбранноеЗначение.Свойство("НаселенныйПунктДетально") Тогда
			// Выбор из формы детального ввода.
			НаселенныйПунктДетально = ВыбранноеЗначение.НаселенныйПунктДетально;
			ФормироватьДетально = Ложь;
		Иначе 
			Если НаселенныйПунктДетально <> Неопределено Тогда
				НовыеДанныеПоНаселенномуПунктуДетально = НаселенныйПунктДетальноПоИдентификатору(ИдентификаторНаселенногоПункта, ВидКонтактнойИнформации.ФорматАдреса);
				Для каждого ЧастьАдреса Из НовыеДанныеПоНаселенномуПунктуДетально Цикл
					Если ЧастьАдреса.Значение.Уровень < 7 Тогда
						НаселенныйПунктДетально[ЧастьАдреса.Ключ] = ЧастьАдреса.Значение;
					КонецЕсли;
				КонецЦикла;
			Иначе
				НаселенныйПунктДетально = НаселенныйПунктДетальноПоИдентификатору(ИдентификаторНаселенногоПункта, ВидКонтактнойИнформации.ФорматАдреса);
			КонецЕсли;
		КонецЕсли;
		
		РегионНеЗагружен = ВыбранноеЗначение.Свойство("РегионЗагружен") И (Не ВыбранноеЗначение.РегионЗагружен);
		Если ЕстьПравоЗагружатьКлассификатор И РегионНеЗагружен Тогда
			// Предлагаем загрузить классификатор.
			ПредложениеЗагрузкиКлассификатора(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Данные для ""%1"" не загружены.'"), ВыбранноеЗначение.Представление),
				ВыбранноеЗначение.Представление);
		КонецЕсли;
		
	Иначе
		// Иной источник, будет попытка разобрать.
		ИдентификаторНаселенногоПункта = Неопределено;
	 	НаселенныйПункт = Строка(ВыбранноеЗначение);
	КонецЕсли;
	
	Элементы.НаселенныйПункт.ОбновитьТекстРедактирования();
	Контекст = КонтекстФормыКлиент();
	// Так как изменился населенный пункт, то перепроверяем улицу, индекс и представление обновятся там.
	ОбработкаИзмененияУлицыСервер(Контекст);
	КонтекстФормыКлиент(Контекст);
	
	
КонецПроцедуры

&НаКлиенте
Процедура НаселенныйПунктАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	ДанныеВыбора = Новый СписокЗначений;
	Если Ожидание = 0 Тогда
		// Формирование списка быстрого выбора, стандартную обработку не надо трогать.
		Возврат;
	ИначеЕсли НедоступностьСервиса() Тогда
		Возврат;
	КонецЕсли;
	
	ТекстДляАвтоПодбора = СокрЛП(Текст);
	Элементы.НаселенныйПункт.ЦветФона = АвтоЦвет;
	Если СтрДлина(ТекстДляАвтоПодбора) < 3 Тогда
		// Нет вариантов, список пуст, стандартную обработку не надо трогать.
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ФорматАдреса", ВидКонтактнойИнформации.ФорматАдреса);
	ДополнительныеПараметры.Вставить("СкрыватьНеактуальные",              СкрыватьНеактуальныеАдреса);
	
	ДанныеКлассификатора = СписокАвтоподбораНаселенногоПункта(ТекстДляАвтоПодбора, ДополнительныеПараметры);
	ПроверитьДоступностьКлассификатора();
	Если ДанныеКлассификатора.Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеВыбора = ДанныеКлассификатора.Данные;
	// Стандартную обработку отключаем, только если есть наши варианты.
	Если ДанныеВыбора.Количество() > 0 Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НаселенныйПунктОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	// Выход из поля с текстом, измененным руками.
	
	Модифицированность = Истина;

	ДанныеВыбора = Новый СписокЗначений;
	ДанныеВыбора.Добавить(Текст);
	
	// Населенный пункт становится недостоверным.
	ИдентификаторНаселенногоПункта = Неопределено;
	НаселенныйПункт                = Текст;
	
	// Улица становится недостоверной.
	ИдентификаторУлицы = Неопределено;
	
	#Если ВебКлиент Тогда
		// Обход особенности платформы.
		Контекст = КонтекстФормыКлиент();
		ОбработкаИзмененияНаселенногоПунктаСервер(Контекст);
		КонтекстФормыКлиент(Контекст);
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура УлицаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	// Если пришли впрямую после редактирования, то сбрасываем.
	Если Элемент.ТекстРедактирования <> Улица Тогда
		Улица = Элемент.ТекстРедактирования;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СкрыватьНеактуальныеАдреса", СкрыватьНеактуальныеАдреса);
	ПараметрыФормы.Вставить("ФорматАдреса", ВидКонтактнойИнформации.ФорматАдреса);
	ПараметрыФормы.Вставить("НаселенныйПунктДетально", НаселенныйПунктДетально);
	Если ЗначениеЗаполнено(ИдентификаторНаселенногоПункта) Тогда
		ПараметрыФормы.Вставить("ИдентификаторНаселенногоПункта", ИдентификаторНаселенногоПункта);
		ПараметрыФормы.Вставить("Улица", Улица);
	Иначе
		// Родитель улицы неопределен, открываем форму в которой ничего нет.
		ПараметрыФормы.Вставить("ИдентификаторНаселенногоПункта", -1);
		ПараметрыФормы.Вставить("Улица", Улица);
		ПараметрыФормы.Вставить("Заголовок", НаселенныйПункт + " " + НСтр("ru = '(не найден)'"));
	КонецЕсли;
	
	НачалоВыбораУлицы(Элемент, ИдентификаторНаселенногоПункта, Улица, ПараметрыФормы);
КонецПроцедуры

&НаКлиенте
Процедура УлицаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыбранноеЗначение.Свойство("Отказ") И ВыбранноеЗначение.Отказ = Истина Тогда
		Если НЕ ПустаяСтрока(ВыбранноеЗначение.КраткоеПредставлениеОшибки) Тогда
			ПоказатьПредупреждение(, ВыбранноеЗначение.КраткоеПредставлениеОшибки);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	ТипВыбора = ТипЗнч(ВыбранноеЗначение);
	Если ТипВыбора = Тип("Структура") Тогда
		Если ВыбранноеЗначение.Свойство("НаселенныйПунктДетально") Тогда
			// Результат автоподбора или выбора кнопкой, берем все оттуда.
			НаселенныйПунктДетально = ВыбранноеЗначение.НаселенныйПунктДетально;
		Иначе
			НаселенныйПунктДетально.Улица.Представление = ВыбранноеЗначение.Улица;
			НаселенныйПунктДетально.ДополнительныйЭлемент.Представление = ВыбранноеЗначение.ДополнительныйЭлемент;
			Если ЗначениеЗаполнено(ВыбранноеЗначение.ДополнительныйЭлемент) Тогда
				НаименованиеСокращение = УправлениеКонтактнойИнформациейКлиентСервер.НаименованиеСокращение(ВыбранноеЗначение.ДополнительныйЭлемент);
				НаселенныйПунктДетально.ДополнительныйЭлемент.ПутьXPath = РаботаСАдресамиКлиентСервер.XPathДополнительногоОбъектаАдресации(90, НаименованиеСокращение.Сокращение);
			КонецЕсли;
			НаселенныйПунктДетально.ПодчиненныйЭлемент.Представление = ВыбранноеЗначение.ПодчиненныйЭлемент;
			НаселенныйПунктДетально.Улица.Идентификатор = ВыбранноеЗначение.Идентификатор;
		КонецЕсли;
		Улица = ВыбранноеЗначение.Представление;
		ИдентификаторУлицы = ВыбранноеЗначение.Идентификатор;
	Иначе
		// Иной источник, будет попытка разобрать.
		ИдентификаторУлицы = Неопределено;
		Улица = Строка(ВыбранноеЗначение);
	КонецЕсли;
	
	Элементы.Улица.ОбновитьТекстРедактирования();
	Контекст = КонтекстФормыКлиент();
	УстановитьИндексИПредставление(Контекст);
	КонтекстФормыКлиент(Контекст);
	
КонецПроцедуры

&НаКлиенте
Процедура УлицаАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	ДанныеВыбора = Новый СписокЗначений;
	Если Ожидание = 0 Тогда
		// Формирование списка быстрого выбора, стандартную обработку не надо трогать.
		Возврат;
	ИначеЕсли НедоступностьСервиса() Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.Улица.ЦветФона = АвтоЦвет;
	Если СтрДлина(Текст) < 3 Или Не ЗначениеЗаполнено(ИдентификаторНаселенногоПункта)Тогда 
		// Нет вариантов, список пуст, стандартную обработку не надо трогать.
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ФорматАдреса", ВидКонтактнойИнформации.ФорматАдреса);
	ДополнительныеПараметры.Вставить("СкрыватьНеактуальные",              СкрыватьНеактуальныеАдреса);
	
	ДанныеКлассификатора = СписокАвтоподбораУлицы(ИдентификаторНаселенногоПункта, Текст, ДополнительныеПараметры);
	ПроверитьДоступностьКлассификатора();
	Если ДанныеКлассификатора.Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеВыбора = ДанныеКлассификатора.Данные;
	
	// Стандартную обработку отключаем, только если есть наши варианты.
	Если ДанныеВыбора.Количество() > 0 Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УлицаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	// Выход из поля с текстом, измененным руками.
	Модифицированность = Истина;
	
	ДанныеВыбора = Новый СписокЗначений;
	ДанныеВыбора.Добавить(Текст);
	Улица = Текст;
	
	// Улица становится непроверенной.
	ИдентификаторУлицы = Неопределено;
	
	Контекст = КонтекстФормыКлиент();
	ОбработкаИзмененияУлицыСервер(Контекст);
	КонтекстФормыКлиент(Контекст);

КонецПроцедуры

&НаКлиенте
Процедура УлицаОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	// Улица становится непроверенной.
	ИдентификаторУлицы = Неопределено;
	Улица = "";
	Для каждого ЭлементАдреса Из НаселенныйПунктДетально Цикл
		Если ЭлементАдреса.Значение.Уровень > 6 Тогда
			ЭлементАдреса.Значение.Представление = "";
			ЭлементАдреса.Значение.Идентификатор = Неопределено;
		КонецЕсли;
	КонецЦикла;
	
	Контекст = КонтекстФормыКлиент();
	ОбработкаИзмененияУлицыСервер(Контекст);
	КонтекстФормыКлиент(Контекст);

КонецПроцедуры


&НаКлиенте
Процедура ПредставлениеАдресаПриИзменении(Элемент)
	ПредставлениеАдресаИзменено = Истина;
	ПредставлениеАдресаПриИзмененииСервер();
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеИностранногоАдресаПриИзменении(Элемент)
	ПредставлениеАдресаИзменено = Истина;
	ПредставлениеАдресаПриИзмененииСервер();
КонецПроцедуры

&НаКлиенте
Процедура ДомАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Текст) Тогда
		Идентификатор = ?(ЗначениеЗаполнено(Улица), ИдентификаторУлицы, ИдентификаторНаселенногоПункта);
		Если ЗначениеЗаполнено(Идентификатор) Тогда
			ДанныеВыбора = СписокАвтоподбораВариантовДомов(Идентификатор, Текст + "%");
			СтандартнаяОбработка = (ДанныеВыбора.Количество() = 0);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НаселенныйПунктИностранныйАдресаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	// Если пришли впрямую после редактирования, то сбрасываем адрес.
	Если Элемент.ТекстРедактирования <> НаселенныйПункт Тогда
		Модифицированность = Истина;
		НаселенныйПункт    = Элемент.ТекстРедактирования;
		
		ИдентификаторНаселенногоПункта = Неопределено;
		ОбновитьНаселенныйПунктИностранногоАдреса();
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НаселенныйПунктДетально", НаселенныйПунктДетально);
	ПараметрыФормы.Вставить("ФорматАдреса","КЛАДР");
	ПараметрыФормы.Вставить("СервисКлассификатораНедоступен", Истина);
	ОткрытьФорму("Обработка.РасширенныйВводКонтактнойИнформации.Форма.НаселенныйПунктАдреса", ПараметрыФормы, Элемент, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура НаселенныйПунктИностранныйАдресаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ВыбранноеЗначение=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	ТипЗначения = ТипЗнч(ВыбранноеЗначение);
	
	ФормироватьДетально = Истина;
	
	Если ТипЗначения = Тип("Структура") Тогда
		
		НаселенныйПункт = ВыбранноеЗначение.Представление;
		
		Если ВыбранноеЗначение.Свойство("НаселенныйПунктДетально") Тогда
			// Выбор из формы детального ввода.
			НаселенныйПунктДетально = ВыбранноеЗначение.НаселенныйПунктДетально;
			ФормироватьДетально = Ложь;
		Иначе 
			Если НаселенныйПунктДетально <> Неопределено Тогда
				НовыеДанныеПоНаселенномуПунктуДетально = НаселенныйПунктДетальноПоИдентификатору(ИдентификаторНаселенногоПункта, ВидКонтактнойИнформации.ФорматАдреса);
				Для каждого ЧастьАдреса Из НовыеДанныеПоНаселенномуПунктуДетально Цикл
					Если ЧастьАдреса.Значение.Уровень < 7 Тогда
						НаселенныйПунктДетально[ЧастьАдреса.Ключ] = ЧастьАдреса.Значение;
					КонецЕсли;
				КонецЦикла;
			Иначе
				НаселенныйПунктДетально = НаселенныйПунктДетальноПоИдентификатору(ИдентификаторНаселенногоПункта, ВидКонтактнойИнформации.ФорматАдреса);
			КонецЕсли;
		КонецЕсли;
		
		РегионНеЗагружен = ВыбранноеЗначение.Свойство("РегионЗагружен") И (Не ВыбранноеЗначение.РегионЗагружен);
		Если ЕстьПравоЗагружатьКлассификатор И РегионНеЗагружен Тогда
			// Предлагаем загрузить классификатор.
			ПредложениеЗагрузкиКлассификатора(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Данные для ""%1"" не загружены.'"), ВыбранноеЗначение.Представление),
				ВыбранноеЗначение.Представление);
		КонецЕсли;
		
	Иначе
		// Иной источник, будет попытка разобрать.
	 	НаселенныйПункт = Строка(ВыбранноеЗначение);
	КонецЕсли;
	
	Контекст = КонтекстФормыКлиент();
	// Так как изменился населенный пункт, то перепроверяем улицу, индекс и представление обновятся там.
	ЗаполнитьПредставлениеАдреса(Контекст);
	КонтекстФормыКлиент(Контекст);
	
КонецПроцедуры

&НаКлиенте
Процедура НаселенныйПунктИностранныйАдресаПриИзменении(Элемент)
	
	ОбновитьНаселенныйПунктИностранногоАдреса();

КонецПроцедуры

&НаКлиенте
Процедура УлицаИностранныйАдресаПриИзменении(Элемент)
	Контекст = КонтекстФормыКлиент();
	ЗаполнитьПредставлениеАдреса(Контекст);
	КонтекстФормыКлиент(Контекст);
 
КонецПроцедуры

&НаКлиенте
Процедура ДомИностранныйАдресаПриИзменении(Элемент)
	ОбновитьПредставлениеАдреса();
КонецПроцедуры

&НаКлиенте
Процедура СтроениеИностранныйАдресаПриИзменении(Элемент)
	ОбновитьПредставлениеАдреса();
КонецПроцедуры

&НаКлиенте
Процедура ПомещениеИностранныйАдресаПриИзменении(Элемент)
	ОбновитьПредставлениеАдреса();
КонецПроцедуры

&НаКлиенте
Процедура АдресНаДатуАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	Если СтрСравнить(Текст, НСтр("ru='начало учета'")) = 0 Или ПустаяСтрока(Текст) Тогда
		Элементы.АдресНаДату.ФорматРедактирования = "";
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура АдресНаДатуПриИзменении(Элемент)
	
	Если Не ВводНовогоАдреса Тогда
		Отбор = Новый Структура("Вид", ВидКонтактнойИнформации.Ссылка);
		НайденныеСтроки = КонтактнаяИнформацияОписаниеДополнительныхРеквизитов.НайтиСтроки(Отбор);
		Результат = ОпределитьДатуДействия(АдресНаДату, НайденныеСтроки);
		
		Контекст = КонтекстФормыКлиент();
		
		Если Результат.ТекущаяСтрока <> Неопределено Тогда
			ЗначенияПолей = Результат.ТекущаяСтрока.ЗначенияПолей;
			Тип = Результат.ТекущаяСтрока.Тип;
			АдресДействуетС = Результат.ДействуетС;
		Иначе
			ЗначенияПолей = "";
			Тип = ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Адрес");
			АдресДействуетС = АдресНаДату;
		КонецЕсли;
		
		ПерезаполнитьФормуВводаАдреса(Контекст, ЗначенияПолей, Тип, ОсновнаяСтрана);
		КонтекстФормыКлиент(Контекст);
		ОтобразитьДополнительныеЗданияИПомещения();

		Если ЗначениеЗаполнено(Результат.ДействуетПо) Тогда
			ТекстИсторическийАдрес = " " + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'действует по %1'"), Формат(Результат.ДействуетПо - 10, "ДЛФ=DD"));
		Иначе
			ТекстИсторическийАдрес = НСтр("ru = 'действует по настоящее время.'");
		КонецЕсли;
		Элементы.ТекстПроДатуДействия.Заголовок = ТекстИсторическийАдрес;
	Иначе
		АдресДействуетС = АдресНаДату;
	КонецЕсли;
	
	ТекстНачалаУчета = НСтр("ru = 'начало учета'");
	Элементы.АдресНаДату.ФорматРедактирования = ?(ЗначениеЗаполнено(АдресНаДату), "", "ДФ='""" + ТекстНачалаУчета  + """'");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	ПодтвердитьИЗакрыть();
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	Модифицированность = Ложь;
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаполнениеАдреса(Команда)
	
	Если РазрешитьВводАдресаВСвободнойФорме Тогда
		ПоказатьПредупреждение(, НСтр("ru='Адрес не может быть проверен, так как он введен в свободной форме.'"));
		Возврат;
	КонецЕсли;

	ПредупреждатьОбОтсутствииОшибок = Истина;
	ТребуетсяОбновление = Ложь;
	
	Контекст = КонтекстФормыКлиент();
	СписокОшибок = СписокОшибокЗаполнения(Контекст, ПредупреждатьОбОтсутствииОшибок, ТребуетсяОбновление);
	СообщитьОбОшибкахЗаполнения(СписокОшибок, ПредупреждатьОбОтсутствииОшибок, ТребуетсяОбновление);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьАдрес(Команда)
	
	ОчиститьАдресКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоПочтовомуИндексу(Команда)
	
	Если ЕстьКлассификатор И Не ПустаяСтрока(Индекс) Тогда
		ПараметрыФормы = Новый Структура("Индекс, СкрыватьНеактуальныеАдреса", СокрЛП(Индекс), СкрыватьНеактуальныеАдреса);
		ОткрытьФорму("Обработка.РасширенныйВводКонтактнойИнформации.Форма.ВыборАдресаПоПочтовомуИндексу", ПараметрыФормы, Элементы.Индекс);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьКлассификатор(Команда)
	
	СведенияОРегионе = СведенияОРегионе(НаселенныйПунктДетально.Регион.Представление);
	ДополнительныеПараметры = Новый Структура("КодРегионаДляЗагрузки", СведенияОРегионе.КодСубъектаРФ);
	
	ЗагрузитьАдресныйКлассификатор(ДополнительныеПараметры);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьОбъект(Команда)
	Варианты = НеиспользованныеЭлементыДополнительнойТаблицы(ДополнительныеЗдания, Элементы.ТипСтроения, 1);
	Для Каждого ЭлементОбъект Из НеиспользованныеЭлементыДополнительнойТаблицы(ДополнительныеПомещения, Элементы.ТипПомещения, 2) Цикл
		ЗаполнитьЗначенияСвойств(Варианты.Добавить(), ЭлементОбъект);
	КонецЦикла;
	
	КоличествоВариантов = Варианты.Количество();
	Если КоличествоВариантов > 0 Тогда
		ДополнительныеПараметры = Новый Структура("КоличествоВариантов", КоличествоВариантов);
		Оповещение = Новый ОписаниеОповещения("ДобавитьОбъектЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВыборИзМеню(Оповещение, Варианты, Элементы.ДобавитьОбъект);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВвестиАдресВСвободнойФорме(Команда)
	
	Если РазрешитьВводАдресаВСвободнойФорме Тогда
		ТекстВопроса = НСтр("ru='Изменения, введенные вручную, будут потеряны.
								|Исходный адрес будет перемещен в комментарий.
		                        |Продолжить?'");
	Иначе
		ТекстВопроса = НСтр("ru='Ввести адрес в свободной форме?
		                        |Адреса, введенные в свободной форме, могут не пройти проверку по адресному классификатору.'");
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ВвестиАдресВСвободнойФормеЗавершение", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , , НСтр("ru='Подтверждение'"));
КонецПроцедуры

&НаКлиенте
Процедура АвторизацияНаСайтеПоддержкиПользователей(Команда)
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("АвторизацияНаСайтеПоддержкиПользователейЗавершение", ЭтотОбъект);
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтернетПоддержкаПользователей") Тогда
		МодульИнтернетПоддержкаПользователейКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ИнтернетПоддержкаПользователейКлиент");
		МодульИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(ОповещениеОЗакрытии, ЭтотОбъект);
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ДомНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ПоказатьСписокДомов(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура СтроениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ПоказатьСписокДомов(Истина);
КонецПроцедуры

&НаКлиенте
Процедура ИсторияИзменений(Команда)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Контекст", КонтекстФормыКлиент());
	
	ОписаниеДополнительныхРеквизитов = КонтактнаяИнформацияОписаниеДополнительныхРеквизитов;
	СписокКонтактнойИнформации = ЗаполнитьСписокКонтактнойИнформации(ВидКонтактнойИнформации.Ссылка, ОписаниеДополнительныхРеквизитов, ДополнительныеПараметры.Контекст);
	
	ПараметрыФормы = Новый Структура("СписокКонтактнойИнформации", СписокКонтактнойИнформации);
	ПараметрыФормы.Вставить("ВидКонтактнойИнформации", ВидКонтактнойИнформации.Ссылка);
	ПараметрыФормы.Вставить("ТолькоПросмотр", ЭтотОбъект.ТолькоПросмотр);
	ПараметрыФормы.Вставить("ИзФормыВводаАдреса", Истина);
	ПараметрыФормы.Вставить("ДействуетС", АдресНаДату);
	
	ОповещениеОЗакрытие = Новый ОписаниеОповещения("ПослеЗакрытияФормыИстории", ЭтотОбъект, ДополнительныеПараметры);
	ОткрытьФорму("Обработка.ВводКонтактнойИнформации.Форма.ИсторияКонтактнойИнформации", ПараметрыФормы, ЭтотОбъект,,,, ОповещениеОЗакрытие);
	
КонецПроцедуры

&НаКлиенте
Процедура АдресНаЯндексКарты(Команда)
	УправлениеКонтактнойИнформациейКлиент.ПоказатьАдресНаКарте(ПредставлениеАдреса, "Яндекс.Карты");
КонецПроцедуры

&НаКлиенте
Процедура АдресНаGoogleMaps(Команда)
	УправлениеКонтактнойИнформациейКлиент.ПоказатьАдресНаКарте(ПредставлениеАдреса, "GoogleMaps");
КонецПроцедуры

&НаКлиенте
Процедура АдресныйКлассификаторУстарел(Команда)
	
	СообщениеОПроверкеАдреса = НСтр("ru='Адресные сведения ФИАС, загруженные в программу, устарели.
		|Рекомендуется выполнить обновление, чтобы проверка адреса выполнялась корректно.'");
	Если ЕстьПравоЗагружатьКлассификатор Тогда
		ВопросОбОбновлениеАдресногоКлассификатора(СообщениеОПроверкеАдреса);
	Иначе
		СообщениеОПроверкеАдреса = СообщениеОПроверкеАдреса + Символы.ПС
			+ НСтр("ru = 'Для обновления адресного классификатора необходимо обратиться к администратору.'");
		ПоказатьПредупреждение(, СообщениеОПроверкеАдреса);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПоказатьСписокДомов(ВключитьОтбор)
	
	Идентификатор = ?(ЗначениеЗаполнено(Улица), ИдентификаторУлицы, ИдентификаторНаселенногоПункта);
	
	Если ЗначениеЗаполнено(Идентификатор) И ЕстьСписокДомов(Идентификатор) Тогда
		ПараметрыФормы = Новый Структура("Идентификатор, Дом, Строение", Идентификатор, Дом, Строение);
		ПараметрыФормы.Вставить("ТипСтроения", ТипСтроения);
		ПараметрыФормы.Вставить("Отбор", ВключитьОтбор);
		ОповещениеОЗакрытие = Новый ОписаниеОповещения("ВыбранДома", ЭтотОбъект);
		ОткрытьФорму("Обработка.РасширенныйВводКонтактнойИнформации.Форма.ВыборДома", ПараметрыФормы,,,,, ОповещениеОЗакрытие, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Иначе
		Если ЗначениеЗаполнено(ПредставлениеАдреса) Тогда
			ТекстПредупреждения = НСтр("ru = 'Выбор из списка недоступен, т.к в адресном классификаторе отсутствует информация о нумерации домов для введенного адреса.'");
		Иначе
			ТекстПредупреждения = НСтр("ru = 'Для просмотра список домов заполните поля адреса.'");
		КонецЕсли;
		ПоказатьПредупреждение(, ТекстПредупреждения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросОбОбновлениеАдресногоКлассификатора(Знач СообщениеОПроверкеАдреса)
	
	Кнопки = Новый СписокЗначений();
	Кнопки.Добавить("Закрыть", НСтр("ru = 'Закрыть'"));
	Кнопки.Добавить("ОбновитьКлассификатор", НСтр("ru = 'Обновить классификатор'"));
	Оповещение = Новый ОписаниеОповещения("ПослеВопросОбОбновление", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, СообщениеОПроверкеАдреса, Кнопки);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранДома(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Ответ = Новый Структура;
		Если Результат.Свойство("Дом") Тогда
			Ответ.Вставить("Дом", Результат.Дом);
		КонецЕсли;
		Если Результат.Свойство("Корпус") Тогда
			Ответ.Вставить("Корпус", Результат.Корпус);
		КонецЕсли;
		Если Результат.Свойство("Строение") Тогда
			Ответ.Вставить("Строение", Результат.Строение);
		КонецЕсли;
		
		ЗаполнитьПоляДомовИСтроений(Ответ);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УстановитьПиктограммуКомментария()
	Элементы.АдресСтраницаКомментарий.Картинка = ОбщегоНазначенияКлиентСервер.КартинкаКомментария(Комментарий);
КонецПроцедуры

&НаКлиенте
Процедура ПодтвердитьИЗакрыть(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Модифицированность Тогда // При немодифицированности работает как "отмена".
		Контекст = КонтекстФормыКлиент();
		Результат = РезультатаВыбораПоОбновлениюФлагов(Контекст, ВозвращатьСписокЗначений);
		
		// Флаги вида были прочитаны заново.
		ВидКонтактнойИнформации = Контекст.ВидКонтактнойИнформации;
		
		Если ВидКонтактнойИнформации.ПроверятьКорректность
			И (Не РазрешитьВводАдресаВСвободнойФорме)
			И Результат.ОшибкиЗаполнения.Количество() > 0 Тогда
				СообщитьОбОшибкахЗаполнения(Результат.ОшибкиЗаполнения, Ложь);
					Возврат;
		КонецЕсли;
		
		Результат = Результат.ДанныеВыбора;
		Если ВидКонтактнойИнформации.ХранитьИсториюИзменений Тогда
			ОбработатьКонтактнуюИнформациюСИсторией(Результат);
		КонецЕсли;
		
		Если ТипЗнч(Результат) = Тип("Структура") Тогда
			Результат.Вставить("КонтактнаяИнформацияОписаниеДополнительныхРеквизитов", КонтактнаяИнформацияОписаниеДополнительныхРеквизитов);
		КонецЕсли;
		
		СброситьМодифицированностьПриВыборе();
#Если ВебКлиент Тогда
		ФлагЗакрытия = ЗакрыватьПриВыборе;
		ЗакрыватьПриВыборе = Ложь;
		ОповеститьОВыборе(Результат);
		ЗакрыватьПриВыборе = ФлагЗакрытия;
#Иначе
		ОповеститьОВыборе(Результат);
#КонецЕсли
		СохранитьСостояниеФормы();
		
	ИначеЕсли Комментарий <> КопияКомментария Тогда
		// Изменен только комментарий, пробуем вернуть обновленное.
		Результат = РезультатВыбораТолькоКомментария(Параметры.ЗначенияПолей, Параметры.Представление, Комментарий);
		Результат = Результат.ДанныеВыбора;
		
		СброситьМодифицированностьПриВыборе();
#Если ВебКлиент Тогда
		ФлагЗакрытия = ЗакрыватьПриВыборе;
		ЗакрыватьПриВыборе = Ложь;
		ОповеститьОВыборе(Результат);
		ЗакрыватьПриВыборе = ФлагЗакрытия;
#Иначе
		ОповеститьОВыборе(Результат);
#КонецЕсли
		СохранитьСостояниеФормы();
		
	Иначе
		Результат = Неопределено;
	КонецЕсли;
	
	Если (МодальныйРежим Или ЗакрыватьПриВыборе) И Открыта() Тогда
		СброситьМодифицированностьПриВыборе();
		СохранитьСостояниеФормы();
		Закрыть(Результат);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьКонтактнуюИнформациюСИсторией(Результат)
	
	Результат.Вставить("ДействуетС", ?(ВводНовогоАдреса, АдресНаДату, АдресДействуетС));
	ИмяРеквизита = "";
	Отбор = Новый Структура("Вид", Результат.Вид);
	
	СтрокаДействующегоАдреса = Неопределено;
	ДатаБылаИзменена         = Истина;
	ТекущаяДатаАдреса        = ОбщегоНазначенияКлиент.ДатаСеанса();
	Дельта                   = АдресНаДату - ТекущаяДатаАдреса;
	МинимальнаяДельта        = ?(Дельта > 0, Дельта, -Дельта);
	НайденныеСтроки          = КонтактнаяИнформацияОписаниеДополнительныхРеквизитов.НайтиСтроки(Отбор);
	Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
		Если ЗначениеЗаполнено(НайденнаяСтрока.ИмяРеквизита) Тогда
			ИмяРеквизита = НайденнаяСтрока.ИмяРеквизита;
		КонецЕсли;
		Если НайденнаяСтрока.ДействуетС = АдресНаДату Тогда
			ДатаБылаИзменена = Ложь;
			СтрокаДействующегоАдреса = НайденнаяСтрока;
			Прервать;
		КонецЕсли;
		
		Дельта = ТекущаяДатаАдреса - НайденнаяСтрока.ДействуетС;
		Дельта = ?(Дельта > 0, Дельта, -Дельта);
		Если Дельта <= МинимальнаяДельта Тогда
			МинимальнаяДельта = Дельта;
			СтрокаДействующегоАдреса = НайденнаяСтрока;
		КонецЕсли;
	КонецЦикла;
	
	Если ДатаБылаИзменена Тогда
		
		Отбор = Новый Структура("ДействуетС, Вид", АдресДействуетС, Результат.Вид);
		СтрокиСАдресом = КонтактнаяИнформацияОписаниеДополнительныхРеквизитов.НайтиСтроки(Отбор);
		
		ПредставлениеРедактируемогоАдреса = ?(СтрокиСАдресом.Количество() > 0, СтрокиСАдресом[0].Представление, "");
		Если СтрСравнить(Результат.Представление, ПредставлениеРедактируемогоАдреса) <> 0 Тогда
			НоваяКонтактнаяИнформация = КонтактнаяИнформацияОписаниеДополнительныхРеквизитов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяКонтактнаяИнформация, Результат);
			НоваяКонтактнаяИнформация.ЗначенияПолей           = Результат.КонтактнаяИнформация;
			НоваяКонтактнаяИнформация.ДействуетС              = АдресНаДату;
			НоваяКонтактнаяИнформация.ХранитьИсториюИзменений = Истина;
			Если СтрокаДействующегоАдреса = Неопределено Тогда
				Отбор = Новый Структура("ЭтоИсторическаяКонтактнаяИнформация, Вид", Ложь, Результат.Вид);
				НайденныеСтроки = КонтактнаяИнформацияОписаниеДополнительныхРеквизитов.НайтиСтроки(Отбор);
				Для каждого НайденнаяСтрока Из НайденныеСтроки Цикл
					НайденнаяСтрока.ЭтоИсторическаяКонтактнаяИнформация = Истина;
					НайденнаяСтрока.ИмяРеквизита = "";
				КонецЦикла;
				НоваяКонтактнаяИнформация.ИмяРеквизита = ИмяРеквизита;
				НоваяКонтактнаяИнформация.ЭтоИсторическаяКонтактнаяИнформация = Ложь;
			Иначе
				НоваяКонтактнаяИнформация.ЭтоИсторическаяКонтактнаяИнформация = Истина;
				Результат.Представление        = СтрокаДействующегоАдреса.Представление;
				Результат.КонтактнаяИнформация = СтрокаДействующегоАдреса.ЗначенияПолей;
			КонецЕсли;
		ИначеЕсли СтрСравнить(Результат.Комментарий, СтрокаДействующегоАдреса.Комментарий) <> 0 И СтрокиСАдресом.Количество() > 0 Тогда
			// Поменяли только комментарий.
			СтрокиСАдресом[0].Комментарий = Результат.Комментарий;
		КонецЕсли;
	Иначе
		Если СтрСравнить(Результат.Представление, СтрокаДействующегоАдреса.Представление) <> 0
			ИЛИ СтрСравнить(Результат.Комментарий, СтрокаДействующегоАдреса.Комментарий) <> 0 Тогда
				ЗаполнитьЗначенияСвойств(СтрокаДействующегоАдреса, Результат);
				СтрокаДействующегоАдреса.ЗначенияПолей                       = Результат.КонтактнаяИнформация;
				СтрокаДействующегоАдреса.ИмяРеквизита                        = ИмяРеквизита;
				СтрокаДействующегоАдреса.ЭтоИсторическаяКонтактнаяИнформация = Ложь;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияФормыИстории(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВводНовогоАдреса = ?(Результат.Свойство("ВводНовогоАдреса"), Результат.ВводНовогоАдреса, Ложь);
	Если ВводНовогоАдреса Тогда
		Контекст = ДополнительныеПараметры.Контекст;
		Контекст.АдресНаДату = Результат.ТекущийАдрес;
		АдресДействуетС = Контекст.АдресНаДату;
		ПерезаполнитьФормуВводаАдреса(Контекст, "", ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Адрес"), ОсновнаяСтрана);
		КонтекстФормыКлиент(Контекст);
		ОтобразитьДополнительныеЗданияИПомещения();
	Иначе
		Отбор = Новый Структура("Вид", ВидКонтактнойИнформации.Ссылка);
		НайденныеСтроки = КонтактнаяИнформацияОписаниеДополнительныхРеквизитов.НайтиСтроки(Отбор);
		
		ИмяРеквизита = "";
		Для Каждого СтрокаКонтактнойИнформации Из НайденныеСтроки Цикл
			Если НЕ СтрокаКонтактнойИнформации.ЭтоИсторическаяКонтактнаяИнформация Тогда
				ИмяРеквизита = СтрокаКонтактнойИнформации.ИмяРеквизита;
			КонецЕсли;
			КонтактнаяИнформацияОписаниеДополнительныхРеквизитов.Удалить(СтрокаКонтактнойИнформации);
		КонецЦикла;
		
		ЭтоИсторическаяКонтактнаяИнформация = Ложь;
		ПараметрыОбновления = Новый Структура;
		Для Каждого СтрокаКонтактнойИнформации Из Результат.История Цикл
			ДанныеСтроки = КонтактнаяИнформацияОписаниеДополнительныхРеквизитов.Добавить();
			ЗаполнитьЗначенияСвойств(ДанныеСтроки, СтрокаКонтактнойИнформации);
			Если НЕ СтрокаКонтактнойИнформации.ЭтоИсторическаяКонтактнаяИнформация Тогда
				ДанныеСтроки.ИмяРеквизита = ИмяРеквизита;
			КонецЕсли;
			Если НачалоДня(Результат.ТекущийАдрес) = НачалоДня(СтрокаКонтактнойИнформации.ДействуетС) Тогда
				Контекст = ДополнительныеПараметры.Контекст;
				Контекст.АдресНаДату = Результат.ТекущийАдрес;
				ПерезаполнитьФормуВводаАдреса(Контекст, СтрокаКонтактнойИнформации.ЗначенияПолей, СтрокаКонтактнойИнформации.Тип, ОсновнаяСтрана);
				КонтекстФормыКлиент(Контекст);
				ОтобразитьДополнительныеЗданияИПомещения();
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ОтобразитьИнформациюОДатахДействияАдреса(АдресНаДату);
	
	Если НЕ ЭтотОбъект.Модифицированность Тогда
		ЭтотОбъект.Модифицированность = Результат.Модифицированность;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьСостояниеФормы()
	УстановитьКлючИспользованияФормы();
	СохраняемыеВНастройкахДанныеМодифицированы = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьОбъектЗавершение(Знач ВыбранныйЭлемент, Знач ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		// Отказ от выбора
		Возврат;
	КонецЕсли;
	
	Если ВыбранныйЭлемент.Значение = 1 Тогда
		Строка = ДополнительныеЗдания.Добавить();
		
		Строка.Тип = ВыбранныйЭлемент.Представление;
		Строка.ПутьXPath = РаботаСАдресамиКлиентСервер.XPathНомераДополнительногоОбъектаАдресации(Строка.Тип);
		ИмяТекущего = ОтобразитьДополнительныеЗдания();
	Иначе
		Строка = ДополнительныеПомещения.Добавить();
		
		Строка.Тип = ВыбранныйЭлемент.Представление;
		Строка.ПутьXPath = РаботаСАдресамиКлиентСервер.XPathНомераДополнительногоОбъектаАдресации(Строка.Тип);
		ИмяТекущего = ОтобразитьДополнительныеПомещения(ВыбранныйЭлемент.Представление = НаименованиеПриДобавлениеПроизвольногоПомещения());
	КонецЕсли;
	
	// Запрещаем добавлять больше чем есть вариантов.
	Элементы.ДобавитьОбъект.Доступность = ДополнительныеПараметры.КоличествоВариантов > 1;
	
	Если ИмяТекущего<>Неопределено Тогда
		Если ВыбранныйЭлемент.Представление = НаименованиеПриДобавлениеПроизвольногоПомещения() Тогда
			ЭтотОбъект[ИмяТекущего] = "";
		КонецЕсли;
		ТекущийЭлемент = Элементы[ИмяТекущего];
	КонецЕсли;
	
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ВвестиАдресВСвободнойФормеЗавершение(Знач РезультатВопроса, Знач ДополнительныеПараметры) Экспорт
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Если РазрешитьВводАдресаВСвободнойФорме И ЗначениеЗаполнено(ПредставлениеАдреса) Тогда
		Комментарий = ?(НЕ ПустаяСтрока(Комментарий), Комментарий + Символы.ПС, "")
			+ НСтр("ru = 'Исходный адрес:'") + " " + ПредставлениеАдреса;
		ПодключитьОбработчикОжидания("УстановитьПиктограммуКомментария", 0.1, Истина);
	КонецЕсли;
	РазрешитьВводАдресаВСвободнойФорме = Не РазрешитьВводАдресаВСвободнойФорме;
	
	Если ЭтоСтранаУчастникЕАЭС(Страна) Тогда
		// ЕАЭС
		Контекст = КонтекстФормыКлиент();
		ЗначениеРеквизитовПоКонтактнойИнформацииАдресИностранныйАдрес(ПредставлениеАдреса, Контекст);
		КонтекстФормыКлиент(Контекст);
	КонецЕсли;
	
	ОпределениеРежимаВводаАдреса();
	
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СброситьМодифицированностьПриВыборе()
	Модифицированность = Ложь;
	КопияКомментария   = Комментарий;
КонецПроцедуры

&НаСервереБезКонтекста
Функция РезультатаВыбораПоОбновлениюФлагов(Контекст, ВозвращатьСписокЗначений = Ложь)
	// Обновляем некоторые флаги
	ЗначениеФлагов = УправлениеКонтактнойИнформациейСлужебный.СтруктураВидаКонтактнойИнформации(Контекст.ВидКонтактнойИнформации.Ссылка);
	
	Контекст.ВидКонтактнойИнформации.ТолькоНациональныйАдрес = ЗначениеФлагов.ТолькоНациональныйАдрес;
	Контекст.ВидКонтактнойИнформации.ПроверятьКорректность   = ЗначениеФлагов.ПроверятьКорректность;

	Возврат РезультатВыбора(Контекст, ВозвращатьСписокЗначений);
КонецФункции

&НаСервереБезКонтекста
Функция РезультатВыбора(Контекст, ВозвращатьСписокЗначений = Ложь)
	XDTOИнформация = КонтактнаяИнформацияПоЗначениюРеквизитов(Контекст);
	Результат      = Новый Структура("ДанныеВыбора, ОшибкиЗаполнения");
	
	Если ВозвращатьСписокЗначений Тогда
		ДанныеВыбора = Обработки.РасширенныйВводКонтактнойИнформации.КонтактнаяИнформацияВСтаруюСтруктуру(XDTOИнформация);
		ДанныеВыбора = ДанныеВыбора.ЗначенияПолей;
	ИначеЕсли Контекст.Страна = Контекст.ОсновнаяСтрана И ПустаяСтрока(XDTOИнформация.Представление) Тогда
		ДанныеВыбора = "";
	Иначе
		ДанныеВыбора = УправлениеКонтактнойИнформациейСлужебный.КонтактнаяИнформацияXDTOВXML(XDTOИнформация);
	КонецЕсли;
	
	Результат.ДанныеВыбора = Новый Структура;
	Результат.ДанныеВыбора.Вставить("КонтактнаяИнформация", ДанныеВыбора);
	Результат.ДанныеВыбора.Вставить("Представление", XDTOИнформация.Представление);
	Результат.ДанныеВыбора.Вставить("Комментарий", XDTOИнформация.Комментарий);
	Результат.ДанныеВыбора.Вставить("ВведеноВСвободнойФорме",
		УправлениеКонтактнойИнформациейСлужебный.АдресВведенВСвободнойФорме(XDTOИнформация));
	
	Результат.ОшибкиЗаполнения = Обработки.РасширенныйВводКонтактнойИнформации.ОшибкиЗаполненияАдресаXDTO(
		XDTOИнформация.Состав, Контекст.ВидКонтактнойИнформации);
		
	Если Контекст.ВидКонтактнойИнформации.Тип = Перечисления.ТипыКонтактнойИнформации.Адрес 
		И Контекст.ВидКонтактнойИнформации.РедактированиеТолькоВДиалоге Тогда
			АдресВВидеГиперссылки = Истина;
	Иначе
			АдресВВидеГиперссылки = Ложь;
	КонецЕсли;
	Результат.ДанныеВыбора.Вставить("АдресВВидеГиперссылки", АдресВВидеГиперссылки);
	
	// Подавляем перенос строк в возвращаемом отдельно представлении.
	Результат.ДанныеВыбора.Представление = СокрЛП(СтрЗаменить(Результат.ДанныеВыбора.Представление, Символы.ПС, " "));
	Результат.ДанныеВыбора.Вставить("Вид", Контекст.ВидКонтактнойИнформации.Ссылка);
	Результат.ДанныеВыбора.Вставить("Тип", Контекст.ВидКонтактнойИнформации.Тип);
	
	Возврат Результат;
КонецФункции

&НаСервереБезКонтекста
Функция СписокОшибокЗаполнения(Контекст, ПредупреждатьОбОтсутствии, ТребуетсяОбновление = Ложь)
	XDTOИнформация = КонтактнаяИнформацияПоЗначениюРеквизитов(Контекст);
	
	Если Контекст.ВидКонтактнойИнформации.ПроверятьКорректность Тогда
		ПараметрыПроверки = Контекст.ВидКонтактнойИнформации;
	Иначе
		ПараметрыПроверки = Новый Структура("ПроверятьПоФИАС", Истина);
	КонецЕсли;
	
	// Получаем список значений: XPath - текст ошибки.
	Результат = Обработки.РасширенныйВводКонтактнойИнформации.ОшибкиЗаполненияАдресаXDTO(
		XDTOИнформация.Состав, ПараметрыПроверки);
		
	Если Результат.Количество() = 0 // Нет ошибок
		И ПредупреждатьОбОтсутствии // Но надо предупредить об их отсутствии.
		// Дополнительно проверяем на пустоту.
		И (Не УправлениеКонтактнойИнформациейСлужебный.XDTOКонтактнаяИнформацияЗаполнена(XDTOИнформация)) Тогда
		Результат.Добавить("/", НСтр("ru = 'Адрес пуст.'"));
	КонецЕсли;
	
	ОпределитьНеобходимостьОбновленияКлассификатора(Контекст, ТребуетсяОбновление);
	
	Возврат Результат;
КонецФункции

&НаСервереБезКонтекста
Процедура ОпределитьНеобходимостьОбновленияКлассификатора(Знач Контекст, ТребуетсяОбновление)
	
	// проверка источника
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.АдресныйКлассификатор") Тогда
		МодульАдресныйКлассификаторСлужебный = ОбщегоНазначения.ОбщийМодуль("АдресныйКлассификаторСлужебный");
		ИнформацияОбОбновление = МодульАдресныйКлассификаторСлужебный.ОписаниеПоследнейЗагрузки(Контекст.НаселенныйПунктДетально.Регион.Идентификатор);
		ТребуетсяОбновление = ИнформацияОбОбновление.НеобходимоОбновление;
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПерезаполнитьФормуВводаАдреса(Контекст, ЗначенияПолей, ТипКонтактнойИнформации, СтранаРоссия)

	РезультатыЧтения = "";
	XDTOКонтактная = УправлениеКонтактнойИнформациейСлужебный.КонтактнаяИнформацияИзXML(ЗначенияПолей, ТипКонтактнойИнформации, РезультатыЧтения);
	Если ПустаяСтрока(ЗначенияПолей) Тогда
		ПространствоИмен = УправлениеКонтактнойИнформациейКлиентСервер.ПространствоИмен();
		ПространствоИменРФ = РаботаСАдресами.ПространствоИмен();
		XDTOКонтактная.Состав.Страна = СтранаРоссия.Наименование;
		XDTOКонтактная.Состав.Состав = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИменРФ, "АдресРФ"));
	КонецЕсли;
	
	ЗначениеРеквизитовПоКонтактнойИнформации(Контекст, XDTOКонтактная);
	Возврат РезультатыЧтения;

КонецФункции

&НаСервереБезКонтекста
Функция ЗаполнитьСписокКонтактнойИнформации(ВидКонтактнойИнформации, КонтактнаяИнформацияОписаниеДополнительныхРеквизитов, Контекст)

	Отбор = Новый Структура("Вид", ВидКонтактнойИнформации);
	НайденныеСтроки = КонтактнаяИнформацияОписаниеДополнительныхРеквизитов.НайтиСтроки(Отбор);
	
	СписокКонтактнойИнформации = Новый Массив;
	Для каждого СтрокаКонтактнойИнформации Из НайденныеСтроки Цикл
		КонтактнаяИнформация = Новый Структура("Представление, ЗначенияПолей, ДействуетС, Комментарий");
		ЗаполнитьЗначенияСвойств(КонтактнаяИнформация, СтрокаКонтактнойИнформации);
		СписокКонтактнойИнформации.Добавить(КонтактнаяИнформация);
	КонецЦикла;
	
	Возврат СписокКонтактнойИнформации;
КонецФункции

&НаКлиенте
Процедура ПослеЗагрузкиАдресногоКлассификатора(Результат, ДополнительныеПараметры) Экспорт
	СообщениеОНеобходимостиОбновленияКлассификатора(НаселенныйПунктДетально.Регион.Идентификатор);
КонецПроцедуры

&НаСервере
Функция РезультатВыбораТолькоКомментария(КонтактнаяИнфо, Представление, Комментарий)
	
	Если ПустаяСтрока(КонтактнаяИнфо) Тогда
		НоваяКонтактная = УправлениеКонтактнойИнформациейСлужебный.АдресXMLВXDTO("");
		НоваяКонтактная.Комментарий = Комментарий;
		НоваяКонтактная = УправлениеКонтактнойИнформациейСлужебный.КонтактнаяИнформацияXDTOВXML(НоваяКонтактная);
		АдресВведенВСвободнойФорме = Ложь;
		
	ИначеЕсли УправлениеКонтактнойИнформациейКлиентСервер.ЭтоКонтактнаяИнформацияВXML(КонтактнаяИнфо) Тогда
		// Копия
		НоваяКонтактная = КонтактнаяИнфо;
		// Модифицируем значение "НоваяКонтактная".
		УправлениеКонтактнойИнформацией.УстановитьКомментарийКонтактнойИнформации(НоваяКонтактная, Комментарий);
		АдресВведенВСвободнойФорме = УправлениеКонтактнойИнформациейСлужебный.АдресВведенВСвободнойФорме(КонтактнаяИнфо);
		
	Иначе
		НоваяКонтактная = КонтактнаяИнфо;
		АдресВведенВСвободнойФорме = Ложь;
	КонецЕсли;
	
	Результат = Новый Структура("ДанныеВыбора, ОшибкиЗаполнения", Новый Структура, Новый СписокЗначений);
	Результат.ДанныеВыбора.Вставить("КонтактнаяИнформация", НоваяКонтактная);
	Результат.ДанныеВыбора.Вставить("Представление", Представление);
	Результат.ДанныеВыбора.Вставить("Комментарий", Комментарий);
	Результат.ДанныеВыбора.Вставить("ВведеноВСвободнойФорме", АдресВведенВСвободнойФорме);
	Возврат Результат;
КонецФункции

&НаСервере
Процедура СообщениеОНеобходимостиОбновленияКлассификатора(Идентификатор = Неопределено)
	
	Элементы.АдресныйКлассификаторУстарел.Видимость = Ложь;
	Если НЕ ЭтоВебСервис И  ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.АдресныйКлассификатор") Тогда
		МодульАдресныйКлассификаторСлужебный = ОбщегоНазначения.ОбщийМодуль("АдресныйКлассификаторСлужебный");
		ИнформацияОбОбновление = МодульАдресныйКлассификаторСлужебный.ОписаниеПоследнейЗагрузки(Идентификатор);
		Элементы.АдресныйКлассификаторУстарел.Видимость = ИнформацияОбОбновление.НеобходимоОбновление;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОбработкаИзмененияНаселенногоПунктаСервер(Контекст, ПереформироватьДетально = Истина)
	
	Если Не ЗначениеЗаполнено(Контекст.ИдентификаторНаселенногоПункта) Тогда
		// Данные вводились вручную, пробуем разбор введенной строки.
		Адрес = НаселенныеПунктыПоПредставлению(Контекст.НаселенныйПункт);
		ЗаполнитьКонтекстПоXDTOАдреса(Адрес, Контекст, УровниНаселенногоПункта());
		ПереформироватьДетально = Ложь;
	КонецЕсли;
	
	// Населенный пункт, разбитый по частям.
	Если ПереформироватьДетально Тогда
		СформироватьДетальныйНаселенныйПункт(Контекст);
	КонецЕсли;
	
	// Перепроверяем улицу, индекс и представление обновятся там.
	ОбработкаИзмененияУлицыСервер(Контекст);

КонецПроцедуры

&НаСервереБезКонтекста
Функция УровниНаселенногоПункта()
	Уровни = Новый Массив;
	Уровни.Добавить(1);
	Уровни.Добавить(2);
	Уровни.Добавить(3);
	Уровни.Добавить(4);
	Уровни.Добавить(5);
	Уровни.Добавить(6);

	Возврат Уровни;
КонецФункции

&НаСервереБезКонтекста
Процедура ОбработкаИзмененияУлицыСервер(Контекст)
	
	// Очистка записей по улице
	ОчиститьСведенияОбУлице(Контекст.НаселенныйПунктДетально);
	
	// Данные вводились вручную, пробуем разбор введенной строки.
	АнализКлассификатора = УлицыПоПредставлению(Контекст.ИдентификаторНаселенногоПункта, Контекст.Улица, Контекст.СкрыватьНеактуальныеАдреса);
	УровеньУлицы = 0;
	Если АнализКлассификатора <> Неопределено И АнализКлассификатора.Найти(0, "Уровень") = Неопределено Тогда
		ИдентификаторУлицы = Неопределено;
		Улица = АнализКлассификатора.Найти(7, "Уровень");
		Если Улица <> Неопределено Тогда
			Контекст.НаселенныйПунктДетально.Улица.Представление = Улица.Значение;
			Контекст.НаселенныйПунктДетально.Улица.Идентификатор = Улица.Идентификатор;
			ИдентификаторУлицы = Новый УникальныйИдентификатор(Улица.Идентификатор);
		КонецЕсли;
		
		Улица = АнализКлассификатора.Найти(90, "Уровень");
		Если Улица <> Неопределено Тогда
			Контекст.НаселенныйПунктДетально.ДополнительныйЭлемент.Представление = Улица.Значение;
			Контекст.НаселенныйПунктДетально.ДополнительныйЭлемент.Идентификатор = Улица.Идентификатор;
			ПутьXPath = РаботаСАдресамиКлиентСервер.XPathДополнительногоОбъектаАдресации(90, Контекст.НаселенныйПунктДетально.ДополнительныйЭлемент.Сокращение);
			Контекст.НаселенныйПунктДетально.ДополнительныйЭлемент.ПутьXPath = ПутьXPath;
			ИдентификаторУлицы = Новый УникальныйИдентификатор(Улица.Идентификатор);
			Улица = АнализКлассификатора.Найти(91, "Уровень");
			Если Улица <> Неопределено Тогда
				Контекст.НаселенныйПунктДетально.ПодчиненныйЭлемент.Представление = Улица.Значение;
				Контекст.НаселенныйПунктДетально.ПодчиненныйЭлемент.Идентификатор = Улица.Идентификатор;
				ПутьXPath = РаботаСАдресамиКлиентСервер.XPathДополнительногоОбъектаАдресации(91, Контекст.НаселенныйПунктДетально.ДополнительныйЭлемент.Сокращение);
				Контекст.НаселенныйПунктДетально.ПодчиненныйЭлемент.ПутьXPath = ПутьXPath;
				ИдентификаторУлицы = Новый УникальныйИдентификатор(Улица.Идентификатор);
			КонецЕсли;
		КонецЕсли;
		Контекст.ИдентификаторУлицы = ИдентификаторУлицы;
	Иначе
		Контекст.НаселенныйПунктДетально.Улица.Представление = Контекст.Улица;
	КонецЕсли;
	
	// Перепроверяем дом, индекс и представление обновятся там.
	УстановитьИндексИПредставление(Контекст);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОчиститьСведенияОбУлице(НаселенныйПунктДетально)
	
	НаселенныйПунктДетально.Улица.Представление = Неопределено;
	НаселенныйПунктДетально.Улица.Идентификатор = Неопределено;
	НаселенныйПунктДетально.Улица.Наименование = "";
	НаселенныйПунктДетально.ДополнительныйЭлемент.Представление = Неопределено;
	НаселенныйПунктДетально.ДополнительныйЭлемент.Идентификатор = Неопределено;
	НаселенныйПунктДетально.ПодчиненныйЭлемент.Представление = Неопределено;
	НаселенныйПунктДетально.ПодчиненныйЭлемент.Идентификатор = Неопределено;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаИзмененияСтраныКлиент()
	
	ЭтоНациональныйАдрес = (Страна = ОсновнаяСтрана);
	Элементы.ТипАдреса.ТекущаяСтраница = ?(ЭтоНациональныйАдрес, Элементы.НациональныйАдрес, Элементы.ИностранныйАдрес);
	
	// Проверять, вводить в свободной форме и искать по индексу можем только российские адреса.
	Элементы.ПроверитьЗаполнениеАдреса.Доступность              = ЭтоНациональныйАдрес;
	Элементы.ЗаполнитьПоПочтовомуИндексу.Доступность            = ЭтоНациональныйАдрес;
	Элементы.ЗаполнитьПоПочтовомуИндексуВсеДействия.Доступность = ЭтоНациональныйАдрес;
	
	ОпределениеРежимаВводаАдреса();
	
КонецПроцедуры

&НаКлиенте
Процедура ОпределениеРежимаВводаАдреса()
	
	Если Страна = ОсновнаяСтрана Тогда
		// Загружать можем только национальные адреса.
		Элементы.ВвестиАдресВСвободнойФорме.Доступность             = Истина;
		Элементы.ВвестиАдресВСвободнойФормеВсеДействия.Доступность  = Истина;
		Элементы.ГруппаИндекса.Видимость                            = Истина;
		Если ЕстьПравоЗагружатьКлассификатор Тогда
			КнопкаПанели = Элементы.Найти("ЗагрузитьКлассификатор");
			Если КнопкаПанели <> Неопределено Тогда
				Элементы.ЗагрузитьКлассификатор.Доступность         = Истина;
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если НЕ ЭтоСтранаУчастникЕАЭС(Страна) Тогда
			// Прочие страны
			Элементы.ВвестиАдресВСвободнойФорме.Доступность            = Ложь;
			Элементы.ВвестиАдресВСвободнойФормеВсеДействия.Доступность = Ложь;
			Элементы.СтруктураИностранныйАдреса.Видимость              = Ложь;
			Элементы.ГруппаИндекса.Видимость                           = Ложь;
			РазрешитьВводАдресаВСвободнойФорме                         = Истина;
		Иначе
			// Страны ЕАЭС
			Элементы.ВвестиАдресВСвободнойФорме.Доступность            = Истина;
			Элементы.ВвестиАдресВСвободнойФормеВсеДействия.Доступность = Истина;
			Если РазрешитьВводАдресаВСвободнойФорме Тогда
				Элементы.СтруктураИностранныйАдреса.Видимость = Ложь;
				Элементы.ГруппаИндекса.Видимость              = Ложь;
			Иначе
				Элементы.СтруктураИностранныйАдреса.Видимость = Истина;
				Элементы.ГруппаИндекса.Видимость              = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ВидКонтактнойИнформации.ВключатьСтрануВПредставление Тогда
		ПозицияСтрана = СтрНайти(ПредставлениеАдреса, ",");
		Если ПозицияСтрана > 0 Тогда
			ПредставлениеАдреса = ВРег(Страна) + Сред(ПредставлениеАдреса, ПозицияСтрана);
		Иначе
			ПредставлениеАдреса = ВРег(Страна);
		КонецЕсли;
	КонецЕсли;
	
	СостояниеВводаПредставленияАдреса(РазрешитьВводАдресаВСвободнойФорме);
	
КонецПроцедуры

&НаСервере
Процедура ОпределитьОтображениеЭлементовНаФорме(ЭтоТакси)
	
	Элементы.ЗагрузитьКлассификатор.Видимость            = ЕстьПравоЗагружатьКлассификатор;
	Элементы.ЗагрузитьКлассификаторВсеДействия.Видимость = ЕстьПравоЗагружатьКлассификатор;
	
	Если Не ЕстьКлассификатор Тогда
		Элементы.ЗаполнитьПоПочтовомуИндексу.Видимость            = Ложь;
		Элементы.ЗаполнитьПоПочтовомуИндексуВсеДействия.Видимость = Ложь;
		Элементы.Улица.КнопкаВыбора                               = Ложь;
		Элементы.Дом.КнопкаВыбора                                 = Ложь;
		Элементы.Строение.КнопкаВыбора                            = Ложь;
		Элементы.СообщениеОбОбращенииВФНС.Видимость               = Ложь;
		Элементы.НаселенныйПункт.ПодсказкаВвода                   = НСтр("ru='Введите название населенного пункта'");
		Элементы.Улица.ПодсказкаВвода                             = НСтр("ru='Введите название улицы'");
	КонецЕсли;
	
	// По умолчанию выводим представление.
	Элементы.АдресПредставлениеКомментарий.ТекущаяСтраница = Элементы.АдресСтраницаПредставление;
	
	Если ЭтоТакси Тогда
		Элементы.ФормаВсеДействия.Видимость = Ложь;
	Иначе
		Элементы.ВвестиАдресВСвободнойФорме.Видимость  = Ложь;
		Элементы.ЗаполнитьПоПочтовомуИндексу.Видимость = Ложь;
		Элементы.ФормаОчиститьАдрес.Видимость          = Ложь;
		Элементы.ЗагрузитьКлассификатор.Видимость      = Ложь;
		Элементы.ФормаВводАдресаПоказать.Видимость     = Ложь;
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ИзменитьФорму", "Видимость", Ложь);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УстановитьСписокВыбораЭлемента(ЭлементВид, ЭлементЗначение, Данные)
	ЭлементЗначение.КнопкаВыпадающегоСписка = Данные.МожноПодбиратьЗначения;
	
	СписокТипов = Данные.ВариантыТипа;
	ЭлементВид.КнопкаВыпадающегоСписка = СписокТипов.Количество() > 0;
	Если ЭлементВид.КнопкаВыпадающегоСписка Тогда
		ЭлементВид.СписокВыбора.ЗагрузитьЗначения(СписокТипов);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьИндексИПредставление(Контекст, XDTOКонтактная = Неопределено)
	
	Инфо = ?(XDTOКонтактная = Неопределено, КонтактнаяИнформацияПоЗначениюРеквизитов(Контекст), XDTOКонтактная);
	
	Если Контекст.Страна = Контекст.ОсновнаяСтрана Тогда
		УстановитьЗначениеИндекса(Контекст, Инфо);
	КонецЕсли;
	ЗаполнитьПредставлениеАдреса(Контекст, Инфо);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьЗначениеИндекса(Контекст, XDTOКонтактная = Неопределено)
	
	Инфо = ?(XDTOКонтактная = Неопределено, КонтактнаяИнформацияПоЗначениюРеквизитов(Контекст), XDTOКонтактная);
	
	Адрес = Инфо.Состав;
	
	Идентификатор = НайтиИдентификаторАдресаПоИерархии(Контекст.НаселенныйПунктДетально);
	ИндексИОКМТОПоКлассификатору = Обработки.РасширенныйВводКонтактнойИнформации.ОпределитьПочтовыйИндексИОКТМОАдреса(Адрес, Идентификатор);
	
	Если ЗначениеЗаполнено(ИндексИОКМТОПоКлассификатору.Индекс) Тогда
		Контекст.Индекс = Формат(ИндексИОКМТОПоКлассификатору.Индекс, "ЧЦ=6; ЧРГ=; ЧГ=0");
	КонецЕсли;
	Если ЗначениеЗаполнено(ИндексИОКМТОПоКлассификатору.ОКТМО) Тогда
		Контекст.ОКТМО = Обработки.РасширенныйВводКонтактнойИнформации.ОКТМО(ИндексИОКМТОПоКлассификатору.ОКТМО);
	КонецЕсли;
	Обработки.РасширенныйВводКонтактнойИнформации.ПочтовыйИндексАдреса(Адрес, Контекст.Индекс);
	XDTOКонтактная.Представление = УправлениеКонтактнойИнформациейСлужебный.ПредставлениеАдреса(Адрес, Контекст.ВидКонтактнойИнформации);
КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиИдентификаторАдресаПоИерархии(НаселенныйПунктДетально)
	
	Идентификатор = Неопределено;
	УровеньИдентификатора = 0;
	Для каждого УровеньАдреса Из НаселенныйПунктДетально Цикл
		Если ЗначениеЗаполнено(УровеньАдреса.Значение.Идентификатор) И УровеньИдентификатора < УровеньАдреса.Значение.Уровень Тогда
			Идентификатор = УровеньАдреса.Значение.Идентификатор;
			УровеньИдентификатора = УровеньАдреса.Значение.Уровень;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ?(ЗначениеЗаполнено(Идентификатор), Новый УникальныйИдентификатор(Идентификатор), Неопределено);
КонецФункции

&НаСервереБезКонтекста
Процедура ЗаполнитьПредставлениеАдреса(Контекст, XDTOКонтактная = Неопределено)
	
	// Код страны ставим всегда
	Если ТипЗнч(Контекст.Страна) = Тип("СправочникСсылка.СтраныМира") Тогда
		Контекст.КодСтраны = Контекст.Страна.Код
	Иначе
		Контекст.КодСтраны = "";
	КонецЕсли;
	
	Если Контекст.РазрешитьВводАдресаВСвободнойФорме И Контекст.ПредставлениеАдресаИзменено Тогда
		Возврат;
	КонецЕсли;
	
	Инфо = ?(XDTOКонтактная = Неопределено, КонтактнаяИнформацияПоЗначениюРеквизитов(Контекст), XDTOКонтактная);
	Контекст.ПредставлениеАдреса = Инфо.Представление;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗначениеРеквизитовПоКонтактнойИнформации(Контекст, РедактируемаяИнформация)
	
	ДанныеАдреса = РедактируемаяИнформация.Состав;
	
	// Общие реквизиты
	Контекст.ПредставлениеАдреса = РедактируемаяИнформация.Представление;
	Контекст.Комментарий         = РедактируемаяИнформация.Комментарий;
	
	// Копия комментария для анализа измененности.
	Контекст.КопияКомментария = Контекст.Комментарий;
	
	// Страна по наименованию
	НаименованиеСтраны = СокрЛП(ДанныеАдреса.Страна);
	Если ПустаяСтрока(НаименованиеСтраны) Тогда
		Контекст.Страна = Справочники.СтраныМира.ПустаяСсылка();
	Иначе
		СсылкаНаОсновнуюСтрану = РаботаСАдресами.ОсновнаяСтрана();
		Если ВРЕГ(НаименованиеСтраны) = ВРЕГ(СокрЛП(СсылкаНаОсновнуюСтрану.Наименование)) Тогда
			Контекст.Страна    = СсылкаНаОсновнуюСтрану;
			Контекст.КодСтраны = СсылкаНаОсновнуюСтрану.Код;
		Иначе
			ДанныеСтраны = Справочники.СтраныМира.ДанныеСтраныМира(, НаименованиеСтраны);
			Если ДанныеСтраны = Неопределено Тогда
				// Не нашли ни в справочнике, ни в классификаторе.
				Контекст.Страна    = Неопределено;
				Контекст.КодСтраны = Неопределено;
			Иначе
				Контекст.Страна    = ДанныеСтраны.Ссылка;
				Контекст.КодСтраны = ДанныеСтраны.Код;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ВычисленноеПредставление = УправлениеКонтактнойИнформациейСлужебный.СформироватьПредставлениеКонтактнойИнформации(
		РедактируемаяИнформация, Контекст.ВидКонтактнойИнформации);
		
	Если УправлениеКонтактнойИнформациейСлужебный.ЭтоНациональныйАдрес(ДанныеАдреса) Тогда
		Контекст.РазрешитьВводАдресаВСвободнойФорме = Не ПустаяСтрока(ДанныеАдреса.Состав.Адрес_по_документу);
		
		// Дополнительно проверяем случай, когда представление по документу равно вычисленному.
		Если Контекст.РазрешитьВводАдресаВСвободнойФорме Тогда
			Если ПредставленияАдресаОдинаковы(ВычисленноеПредставление, ДанныеАдреса.Состав.Адрес_по_документу, Истина) 
				И ПредставленияАдресаОдинаковы(РедактируемаяИнформация.Представление, ДанныеАдреса.Состав.Адрес_по_документу, Истина) Тогда
				Контекст.РазрешитьВводАдресаВСвободнойФорме = Ложь;
				ДанныеАдреса.Состав.Адрес_по_документу      = "";
			КонецЕсли;
		КонецЕсли;
		
		Если Контекст.РазрешитьВводАдресаВСвободнойФорме Тогда
			Контекст.ПредставлениеАдреса = ДанныеАдреса.Состав.Адрес_по_документу;
		КонецЕсли;
		
		Контекст.ОКТМО = Обработки.РасширенныйВводКонтактнойИнформации.ОКТМО(ДанныеАдреса.Состав.ОКТМО);
		ЗначениеРеквизитовПоКонтактнойИнформацииАдресРФ(ДанныеАдреса, Контекст);
	Иначе
		Адрес = Строка(ДанныеАдреса.Состав);
		Контекст.НаселенныйПунктДетально = РаботаСАдресамиКлиентСервер.СтруктураЧастейАдресаНаселенногоПункта("ФИАС");
		Если СтрЧислоВхождений(Адрес, ",") = 9 Тогда
			Адрес = Сред(Адрес, СтрНайти(Адрес, ",") + 1); // удаляем страну
		КонецЕсли;
		Если ЗначениеЗаполнено(Адрес) И СтрЧислоВхождений(Адрес, ",") = 8 Тогда
			ЗначениеРеквизитовПоКонтактнойИнформацииАдресИностранныйАдрес(Адрес, Контекст);
			Контекст.РазрешитьВводАдресаВСвободнойФорме = Ложь;
		Иначе
			Контекст.РазрешитьВводАдресаВСвободнойФорме = Истина;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВычисленноеПредставление) И ПустаяСтрока(Контекст.ПредставлениеАдреса) Тогда
			Контекст.ПредставлениеАдреса = ВычисленноеПредставление;
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗначениеРеквизитовПоКонтактнойИнформацииАдресИностранныйАдрес(Адрес, Контекст)
	
	Если НЕ СтрНачинаетсяС(ВРег(Адрес), ВРег(Контекст.Страна)) Тогда
		Адрес = ВРег(Контекст.Страна) + "," + Адрес;
	КонецЕсли;
	
	СтруктураАдрес = Обработки.РасширенныйВводКонтактнойИнформации.РазложитьАдресВСтруктуру(Адрес);
	
	Контекст.НаселенныйПунктДетально.Регион.Представление = СтруктураАдрес.Регион;
	Контекст.НаселенныйПунктДетально.Район.Представление = СтруктураАдрес.Район;
	Контекст.НаселенныйПунктДетально.Город.Представление = СтруктураАдрес.Город;
	Контекст.НаселенныйПунктДетально.НаселенныйПункт.Представление = СтруктураАдрес.НаселенныйПункт;
	
	НаселенныйПункт = "";
	НаселенныйПункт = ?(ЗначениеЗаполнено(СтруктураАдрес.НаселенныйПункт), СтруктураАдрес.НаселенныйПункт  + ", ","");
	НаселенныйПункт = НаселенныйПункт + ?(ЗначениеЗаполнено(СтруктураАдрес.Город), СтруктураАдрес.Город + ", ","");
	НаселенныйПункт = НаселенныйПункт + ?(ЗначениеЗаполнено(СтруктураАдрес.Район), СтруктураАдрес.Район + ", ","");
	НаселенныйПункт = НаселенныйПункт + ?(ЗначениеЗаполнено(СтруктураАдрес.Регион), СтруктураАдрес.Регион + ", ","");
	
	Если СтрДлина(НаселенныйПункт) > 2 И СтрЗаканчиваетсяНа(НаселенныйПункт, ", ") Тогда
		НаселенныйПункт = Лев(НаселенныйПункт, СтрДлина(НаселенныйПункт) - 2);
	КонецЕсли;
	
	Контекст.Индекс = СтруктураАдрес.Индекс;
	Контекст.НаселенныйПункт = НаселенныйПункт;
	Контекст.Улица = СтруктураАдрес.Улица;
	
	Если Не ПустаяСтрока(СтруктураАдрес.Дом) Тогда
		ЗданиеИНомер = НаименованиеЗданияИЕгоНомер(СтруктураАдрес.Дом);
		ВариантыДанныхДом = Обработки.РасширенныйВводКонтактнойИнформации.ВариантыДанныхДом();
		Контекст.Дом = ЗданиеИНомер.Номер;
		Для каждого ВариантДанныхДом Из ВариантыДанныхДом Цикл
			Если СтрСравнить(ВариантДанныхДом, ЗданиеИНомер.Здание) = 0 Тогда
				Контекст.ТипДома =  ЗданиеИНомер.Здание;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Не ПустаяСтрока(СтруктураАдрес.Корпус) Тогда
		ЗданиеИНомер = НаименованиеЗданияИЕгоНомер(СтруктураАдрес.Корпус);
		Контекст.ТипСтроения = ЗданиеИНомер.Здание;
		Контекст.Строение = ЗданиеИНомер.Номер;
	КонецЕсли;
	
	Если Не ПустаяСтрока(СтруктураАдрес.Квартира) Тогда
		ЗданиеИНомер = НаименованиеЗданияИЕгоНомер(СтруктураАдрес.Квартира);
		Контекст.ТипПомещения = ЗданиеИНомер.Здание;
		Контекст.Помещение = ЗданиеИНомер.Номер;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НаименованиеЗданияИЕгоНомер(Название)
	ТекстПоиска = СокрЛП(Название);
	
	Позиция = СтрНайти(ТекстПоиска, " ");
	
	Результат = Новый Структура("Здание, Номер");
	Если Позиция > 0 Тогда
		Результат.Здание = Лев(ТекстПоиска, Позиция - 1);
		Результат.Номер = СокрЛ(Сред(ТекстПоиска, Позиция + 1));
	Иначе
		Результат.Здание = ТекстПоиска;
		Результат.Номер = "";
	КонецЕсли;
	
	Возврат Результат;
КонецФункции


&НаСервереБезКонтекста
Процедура ЗначениеРеквизитовПоКонтактнойИнформацииАдресРФ(ДанныеАдреса, Контекст)
	
	// Индекс просто ставим
	Контекст.Индекс = Формат(Обработки.РасширенныйВводКонтактнойИнформации.ПочтовыйИндексАдреса(ДанныеАдреса), "ЧГ=");
	
	// Синтетический "Населенный пункт" получаем как представление.
	Контекст.НаселенныйПункт = Обработки.РасширенныйВводКонтактнойИнформации.ПредставлениеНаселенногоПункта(ДанныеАдреса);
	Контекст.Улица = Обработки.РасширенныйВводКонтактнойИнформации.ПредставлениеУлицы(ДанныеАдреса);
	
	СформироватьДетальныйНаселенныйПункт(Контекст, ДанныеАдреса);
	
	// Дом, строение, помещение
	ЗданияИПомещения = Обработки.РасширенныйВводКонтактнойИнформации.ЗданияИПомещенияАдреса(ДанныеАдреса);
	
	// Первые два здания выделяем отдельно, остальное в списке.
	ТаблицаДанных = ЗданияИПомещения.Здания;
	
	// Вид = 1 - признак дома, владения. Вид = 2, доп строение.
	СтрокаДома = ТаблицаДанных.Найти(1, "Вид");
	Если СтрокаДома <> Неопределено Тогда
		Контекст.ТипДома = СтрокаДома.Тип;
		Контекст.Дом     = СтрокаДома.Значение;
		ТаблицаДанных.Удалить(СтрокаДома);
	Иначе
		Контекст.Дом     = "";
	КонецЕсли;
	
	СтрокаДома = ТаблицаДанных.Найти(2, "Вид");
	Если СтрокаДома <> Неопределено Тогда
		Контекст.ТипСтроения = СтрокаДома.Тип;
		Контекст.Строение    = СтрокаДома.Значение;
		ТаблицаДанных.Удалить(СтрокаДома);
	Иначе
		Контекст.Строение    = "";
	КонецЕсли;
	
	НомерСтроки  = ТаблицаДанных.Количество();
	Если НомерСтроки > 0 Тогда
		Пока НомерСтроки > 0 Цикл
			НомерСтроки = НомерСтроки - 1;
			ЗаполнитьЗначенияСвойств(Контекст.ДополнительныеЗдания.Вставить(0), ТаблицаДанных[НомерСтроки]);
		КонецЦикла;
	Иначе
		Контекст.ДополнительныеЗдания.Очистить();
	КонецЕсли;
	
	// Первое помещение указываем отдельно, остальные в списке.
	ТаблицаДанных = ЗданияИПомещения.Помещения;
	НомерСтроки   = ТаблицаДанных.Количество();
	Если НомерСтроки > 0 Тогда
		Контекст.ТипПомещения = ТаблицаДанных[0].Тип;
		Контекст.Помещение    = ТаблицаДанных[0].Значение;
	Иначе
		Контекст.Помещение    = "";
	КонецЕсли;
	
	Если НомерСтроки > 1 Тогда
		Пока НомерСтроки > 1 Цикл
			НомерСтроки = НомерСтроки - 1;
			ЗаполнитьЗначенияСвойств(Контекст.ДополнительныеПомещения.Вставить(0), ТаблицаДанных[НомерСтроки]);
		КонецЦикла;
	Иначе
		Контекст.ДополнительныеПомещения.Очистить();
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция КонтактнаяИнформацияПоЗначениюРеквизитов(Контекст)
	ПространствоИмен = УправлениеКонтактнойИнформациейКлиентСервер.ПространствоИмен();
	
	Результат = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "КонтактнаяИнформация"));
	Результат.Комментарий = Контекст.Комментарий;
	
	Результат.Состав = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "Адрес"));
	Адрес = Результат.Состав;
	
	Адрес.Страна = Строка(Контекст.Страна);
	Если ВРег(Контекст.Страна) <> ВРег(Контекст.ОсновнаяСтрана.Наименование) Тогда
		Если Контекст.РазрешитьВводАдресаВСвободнойФорме Тогда
			
			Если Контекст.ВидКонтактнойИнформации.ВключатьСтрануВПредставление Тогда
				
				Если СтрНачинаетсяС(ВРЕГ(Контекст.ПредставлениеАдреса), ВРег(Контекст.Страна)) Тогда
					Позиция = СтрДлина(Контекст.Страна) + 1;
					Если Сред(Контекст.ПредставлениеАдреса, Позиция, 1) = "," Тогда
						Позиция = Позиция + 1;
					КонецЕсли;
					Адрес.Состав = СокрЛП(Сред(Контекст.ПредставлениеАдреса, Позиция));
				Иначе
					Адрес.Состав = СокрЛП(Контекст.ПредставлениеАдреса);
				КонецЕсли;
				
				Результат.Представление = Адрес.Страна  + ?(ЗначениеЗаполнено(Адрес.Состав), ", " + Адрес.Состав, "");;
			Иначе
				Адрес.Состав            = Контекст.ПредставлениеАдреса;
				Результат.Представление = Контекст.ПредставлениеАдреса;
			КонецЕсли;
			
		Иначе
			
			НаселенныйПункт = Строка(Контекст.НаселенныйПунктДетально.Регион.Представление) + "," + Строка(Контекст.НаселенныйПунктДетально.Район.Представление)
			+ "," + Строка(Контекст.НаселенныйПунктДетально.Город.Представление) + "," + Строка(Контекст.НаселенныйПунктДетально.НаселенныйПункт.Представление);
			Строка9Запятых = Контекст.Страна.Наименование + "," + Контекст.Индекс + "," + НаселенныйПункт + "," + Контекст.Улица + ",";
			Строка9Запятых = Строка9Запятых + ?(ЗначениеЗаполнено(Контекст.Дом), Контекст.ТипДома + " " +Контекст.Дом, "") + ",";
			Строка9Запятых = Строка9Запятых + ?(ЗначениеЗаполнено(Контекст.Строение), Контекст.ТипСтроения + " " + Контекст.Строение, "") + ",";
			Строка9Запятых = Строка9Запятых + ?(ЗначениеЗаполнено(Контекст.Помещение), Контекст.ТипПомещения + " " + Контекст.Помещение, "");
			Адрес.Состав = Строка9Запятых;
			Результат.Представление = УправлениеКонтактнойИнформациейСлужебный.ПредставлениеАдреса(Адрес, Контекст.ВидКонтактнойИнформации);
			
		КонецЕсли;
		
		Возврат Результат;
	КонецЕсли;
	
	Адрес.Состав = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(РаботаСАдресами.ПространствоИмен(), "АдресРФ"));
	АдресРФ = Адрес.Состав;
	ЗаполненныеЧастиАдреса = Новый СписокЗначений;
	Если Контекст.НаселенныйПунктДетально <> Неопределено Тогда
		Для Каждого КлючЗначение Из Контекст.НаселенныйПунктДетально Цикл
			ЧастьПункта = КлючЗначение.Значение;
			Если Не ПустаяСтрока(ЧастьПункта.Представление) Тогда
				НетДетальныхДанных = Ложь;
				ЗаполненныеЧастиАдреса.Добавить(ЧастьПункта.Уровень, ЧастьПункта.Представление);
				ПутьXPath = ЧастьПункта.ПутьXPath;
				Обработки.РасширенныйВводКонтактнойИнформации.УстановитьXDTOРеквизитОбъекта(АдресРФ, ПутьXPath, ЧастьПункта.Представление);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Здания и помещения
	ТаблицаЗданий = ?(ТипЗнч(Контекст.ДополнительныеЗдания) = Тип("ТаблицаЗначений"),
		Контекст.ДополнительныеЗдания.Скопировать(),
		Контекст.ДополнительныеЗдания.Выгрузить());
	
	Если Не ПустаяСтрока(Контекст.Дом) Тогда
		СтрокаЗдания = ТаблицаЗданий.Вставить(0);
		СтрокаЗдания.Тип      = Контекст.ТипДома;
		СтрокаЗдания.Значение = Контекст.Дом;
	КонецЕсли;
	
	Если Не ПустаяСтрока(Контекст.Строение) Тогда
		СтрокаЗдания = ТаблицаЗданий.Вставить(0);
		СтрокаЗдания.Тип      = Контекст.ТипСтроения;
		СтрокаЗдания.Значение = Контекст.Строение;
	КонецЕсли;
	
	ТаблицаПомещений = ?(ТипЗнч(Контекст.ДополнительныеПомещения) = Тип("ТаблицаЗначений"),
		Контекст.ДополнительныеПомещения.Скопировать(),
		Контекст.ДополнительныеПомещения.Выгрузить());
		
	Если Не ПустаяСтрока(Контекст.Помещение) Тогда
		СтрокаЗдания = ТаблицаЗданий.Вставить(0);
		СтрокаЗдания.Тип      = Контекст.ТипПомещения;
		СтрокаЗдания.Значение = Контекст.Помещение;
	КонецЕсли;
	
	Обработки.РасширенныйВводКонтактнойИнформации.ЗданияИПомещенияАдреса(АдресРФ, 
		Новый Структура("Здания, Помещения", ТаблицаЗданий, ТаблицаПомещений));
	
	// Индекс
	Обработки.РасширенныйВводКонтактнойИнформации.ПочтовыйИндексАдреса(АдресРФ, Контекст.Индекс);
	
	// ОКТМО
	ОписаниеЧисло = Новый ОписаниеТипов("Число");
	Обработки.РасширенныйВводКонтактнойИнформации.УстановитьXDTOРеквизитОбъекта(АдресРФ, "ОКТМО", ОписаниеЧисло.ПривестиЗначение(Контекст.ОКТМО));
	
	//
	Если НЕ ПустаяСтрока( Контекст.ДополнительныйЭлемент) Тогда 
		Обработки.РасширенныйВводКонтактнойИнформации.ДобавитьДополнительныеЭлементыАдреса(АдресРФ, Контекст.ДополнительныйЭлемент, 90);
	КонецЕсли;
	Если НЕ ПустаяСтрока(Контекст.ПодчиненныйЭлемент) Тогда 
		Обработки.РасширенныйВводКонтактнойИнформации.ДобавитьДополнительныеЭлементыАдреса(АдресРФ, Контекст.ПодчиненныйЭлемент, 91);
	КонецЕсли;
	
	// Представление и свободный ввод адреса.
	РасчетноеПредставление = УправлениеКонтактнойИнформациейСлужебный.ПредставлениеАдреса(Адрес, Контекст.ВидКонтактнойИнформации);
	ВведенноеПредставление = СокрЛП(Контекст.ПредставлениеАдреса);
	Если Контекст.РазрешитьВводАдресаВСвободнойФорме И Контекст.ПредставлениеАдресаИзменено Тогда
		Если ПредставленияАдресаОдинаковы(ВведенноеПредставление, РасчетноеПредставление) Тогда
			Результат.Представление = РасчетноеПредставление;
			АдресРФ.Сбросить("Адрес_по_документу");
		Иначе
			Результат.Представление    = ВведенноеПредставление;
			АдресРФ.Адрес_по_документу = ВведенноеПредставление;
		КонецЕсли;
	Иначе
		АдресРФ.Сбросить("Адрес_по_документу");
		Результат.Представление = РасчетноеПредставление;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

// Перезаполняет структуру "НаселенныйПунктДетально" по текущим данным адреса или по данным реквизитов формы.
//
&НаСервереБезКонтекста
Процедура СформироватьДетальныйНаселенныйПункт(Контекст, XDTOДанныеАдреса = Неопределено)
	
	Если XDTOДанныеАдреса = Неопределено И ЗначениеЗаполнено(Контекст.ИдентификаторНаселенногоПункта) Тогда
		// Перезаполняем
		Контекст.НаселенныйПунктДетально = Обработки.РасширенныйВводКонтактнойИнформации.СписокРеквизитовНаселенныйПункт(
			Контекст.ИдентификаторНаселенногоПункта,
			Контекст.ВидКонтактнойИнформации.ФорматАдреса);
		Возврат;
		
	ИначеЕсли ТипЗнч(XDTOДанныеАдреса) = Тип("Строка") Тогда
		// Попытка разбора
		АнализКлассификатора = НаселенныеПунктыПоПредставлению(XDTOДанныеАдреса);
		Если АнализКлассификатора.ДанныеВыбора.Количество()=1 Тогда
			Вариант = АнализКлассификатора.ДанныеВыбора[0].Значение.Значение;
			Контекст.НаселенныйПунктДетально = Обработки.РасширенныйВводКонтактнойИнформации.СписокРеквизитовНаселенныйПункт(
				Вариант.Код, Контекст.ВидКонтактнойИнформации.ФорматАдреса);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Контекст.НаселенныйПунктДетально) Тогда
		Контекст.НаселенныйПунктДетально = Обработки.РасширенныйВводКонтактнойИнформации.СписокРеквизитовНаселенныйПункт(,
			Контекст.ВидКонтактнойИнформации.ФорматАдреса);
	КонецЕсли;
	
	Если XDTOДанныеАдреса = Неопределено Тогда
		
		// Из реквизитов формы все идет в предопределенный детальный пункт.
		Для Каждого КлючЗначение Из Контекст.НаселенныйПунктДетально Цикл
			Значение = КлючЗначение.Значение;
			Если Значение.Предопределенный Тогда
				Значение.Представление = Контекст.НаселенныйПункт;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Возврат;
	КонецЕсли;
	
	ЗаполнитьКонтекстПоXDTOАдреса(XDTOДанныеАдреса, Контекст);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьКонтекстПоXDTOАдреса(XDTOДанныеАдреса, Контекст, Уровни = Неопределено)
	
	// Из переданного XDTO
	АдресРФ = Обработки.РасширенныйВводКонтактнойИнформации.НациональныйАдрес(XDTOДанныеАдреса);
	Если АдресРФ <> Неопределено Тогда
		
		Для Каждого КлючЗначение Из Контекст.НаселенныйПунктДетально Цикл
			Значение = КлючЗначение.Значение;
			
			Если Уровни = Неопределено ИЛИ Уровни.Найти(Значение.Уровень) <> Неопределено Тогда
				Если Значение.Уровень <> 90 Тогда
					Представление = Обработки.РасширенныйВводКонтактнойИнформации.ПолучитьXDTOРеквизитОбъекта(АдресРФ, Значение.ПутьXPath);
				Иначе
					ДополнительныйЭлементАдреса = Обработки.РасширенныйВводКонтактнойИнформации.НайтиДополнительныйЭлементАдреса(АдресРФ);
					Представление = ДополнительныйЭлементАдреса.Значение;
					Значение.ПутьXPath = ДополнительныйЭлементАдреса.XPath;
				КонецЕсли;
				
				Части = УправлениеКонтактнойИнформациейКлиентСервер.НаименованиеСокращение(Представление);
				Значение.Представление = Представление;
				Значение.Наименование  = Части.Наименование;
				Значение.Сокращение    = Части.Сокращение;
			КонецЕсли;
		КонецЦикла;
		
		Обработки.РасширенныйВводКонтактнойИнформации.ЗаполнитьИдентификаторыНаселенногоПункта(Контекст.НаселенныйПунктДетально);
		
		УровеньНаселенногоПункта = 0;
		УровеньУлицы = 0;
		Для каждого Элемент Из Контекст.НаселенныйПунктДетально Цикл
			Если Элемент.Значение.Уровень < 7
				И ЗначениеЗаполнено(Элемент.Значение.Идентификатор)
				И Элемент.Значение.Уровень > УровеньНаселенногоПункта Тогда
					Контекст.ИдентификаторНаселенногоПункта = Элемент.Значение.Идентификатор;
					УровеньНаселенногоПункта = Элемент.Значение.Уровень;
			КонецЕсли;
			Если Элемент.Значение.Уровень > 6
				И ЗначениеЗаполнено(Элемент.Значение.Идентификатор)
				И Элемент.Значение.Уровень > УровеньУлицы Тогда
					Контекст.ИдентификаторУлицы = Элемент.Значение.Идентификатор;
					УровеньУлицы = Элемент.Значение.Уровень;
			КонецЕсли;
		КонецЦикла;
	Иначе
		Для Каждого КлючЗначение Из Контекст.НаселенныйПунктДетально Цикл
			Значение = КлючЗначение.Значение;
			Если Значение.Уровень < 7 Тогда
				Значение.Представление = "";
				Значение.Наименование  = "";
				Значение.Сокращение    = "";
			КонецЕсли;
			Значение.Идентификатор = Неопределено;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УдалитьГруппуЭлементов(Группа)
	Пока Группа.ПодчиненныеЭлементы.Количество()>0 Цикл
		Элемент = Группа.ПодчиненныеЭлементы[0];
		Если ТипЗнч(Элемент)=Тип("ГруппаФормы") Тогда
			УдалитьГруппуЭлементов(Элемент);
		КонецЕсли;
		Элементы.Удалить(Элемент);
	КонецЦикла;
	Элементы.Удалить(Группа);
КонецПроцедуры

&НаСервере
Функция ОтобразитьДополнительныеЗданияИПомещения() 
	Возврат Новый Структура("ИмяЗдания, ИмяПомещения",
		ОтобразитьДополнительныеЗдания(),
		ОтобразитьДополнительныеПомещения());
КонецФункции

&НаСервере
Функция ОтобразитьДополнительныеЗдания()
	
	Удалять = Новый Массив;
	Пока Элементы.ГруппаСтроенияДополнительно.ПодчиненныеЭлементы.Количество() > 0 Цикл
		Группа = Элементы.ГруппаСтроенияДополнительно.ПодчиненныеЭлементы[0];
		Если ТипЗнч(Группа)=Тип("ГруппаФормы") Тогда
			Идентификатор = Сред(Группа.Имя, 1 + СтрДлина("ГруппаСтроение"));
			Если Не ПустаяСтрока(Идентификатор) Тогда
				Удалять.Добавить("ТипСтроения" + Идентификатор);
				Удалять.Добавить("Строение"    + Идентификатор);
				КомандаУдалить = Команды.Найти("УдалитьСтроение" + Идентификатор);
				Если КомандаУдалить <> Неопределено Тогда
					Команды.Удалить(КомандаУдалить);
				КонецЕсли;
			КонецЕсли;
			УдалитьГруппуЭлементов(Группа);
		Иначе
			Элементы.Удалить(Группа);
		КонецЕсли;
	КонецЦикла;
	ИзменитьРеквизиты(, Удалять);
	
	КоличествоЗданий = ДополнительныеЗдания.Количество() - 1;
	КоличествоТипов  = Элементы.ТипСтроения.СписокВыбора.Количество() - 2;
	
	Результат = Неопределено;
	Для НомерСтроки=0 По КоличествоЗданий Цикл
		Строка = ДополнительныеЗдания[НомерСтроки];
		Идентификатор = Формат(НомерСтроки, "ЧН=; ЧГ=");
		
		НоваяГруппа = Элементы.Добавить("ГруппаСтроение" + Идентификатор, Тип("ГруппаФормы"), Элементы.ГруппаСтроенияДополнительно);
		ЗаполнитьЗначенияСвойств(НоваяГруппа, Элементы.ГруппаСтроениеНачальное, , "ПутьКДаннымЗаголовка");
		
		НовыйТип = Элементы.Добавить("ТипСтроения" + Идентификатор, Тип("ПолеФормы"), НоваяГруппа);
		ЗаполнитьЗначенияСвойств(НовыйТип, Элементы.ТипСтроения, , "ПутьКДанным, СписокВыбора, ВыделенныйТекст, СвязьПоТипу");
		НовыйТип.СписокВыбора.ЗагрузитьЗначения(Элементы.ТипСтроения.СписокВыбора.ВыгрузитьЗначения());
		НовыйТип.УстановитьДействие("ПриИзменении", "Подключаемый_ТипСтроенияПриИзменении");
		
		НовыйЭлемент = Элементы.Добавить("Строение" + Идентификатор, Тип("ПолеФормы"), НоваяГруппа);
		ЗаполнитьЗначенияСвойств(НовыйЭлемент, Элементы.Строение, , "ПутьКДанным, СписокВыбора, ВыделенныйТекст, СвязьПоТипу");
		НовыйЭлемент.СписокВыбора.ЗагрузитьЗначения(Элементы.Строение.СписокВыбора.ВыгрузитьЗначения());
		НовыйЭлемент.УстановитьДействие("ПриИзменении", "Подключаемый_СтроениеПриИзменении");
		НовыйЭлемент.КнопкаВыбора = Ложь;
		
		Если НомерСтроки=КоличествоЗданий Тогда
			Результат = НовыйЭлемент.Имя;
		КонецЕсли;
		
		Добавлять = Новый Массив;
		Добавлять.Добавить(Новый РеквизитФормы(НовыйТип.Имя, Новый ОписаниеТипов("Строка")));
		Добавлять.Добавить(Новый РеквизитФормы(НовыйЭлемент.Имя, Новый ОписаниеТипов("Строка")));
		
		ИзменитьРеквизиты(Добавлять);
		ЭтотОбъект[НовыйТип.Имя]     = Строка.Тип;
		ЭтотОбъект[НовыйЭлемент.Имя] = Строка.Значение;
		
		НовыйТип.ПутьКДанным     = НовыйТип.Имя;
		НовыйЭлемент.ПутьКДанным = НовыйЭлемент.Имя;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

&НаСервере
Процедура ОтобразитьИнформациюОДатахДействияАдреса(ДействуетС)
	
	Если ВводНовогоАдреса Тогда
		ТекстИсторическийАдрес = НСтр("ru = ''");
		АдресНаДату = ДействуетС;
		Элементы.ГруппаИсторическийАдрес.Видимость = ЗначениеЗаполнено(ДействуетС);
	Иначе
		ДействуетДо = Неопределено;
		
		Отбор = Новый Структура("Вид", ВидКонтактнойИнформации.Ссылка);
		НайденныеСтроки = КонтактнаяИнформацияОписаниеДополнительныхРеквизитов.НайтиСтроки(Отбор);
		Если НайденныеСтроки.Количество() = 0 
			ИЛИ (НайденныеСтроки.Количество() = 1 И ПустаяСтрока(НайденныеСтроки[0].Представление)) Тогда
				АдресНаДату = Дата(1, 1, 1);
				Элементы.ГруппаИсторическийАдрес.Видимость = Ложь;
				Элементы.ИсторияИзменений.Видимость = Ложь;
		Иначе
			Результат = ОпределитьДатуДействия(ДействуетС, НайденныеСтроки);
			АдресНаДату = Результат.ДействуетС;
			АдресДействуетС = Результат.ДействуетС;
			
			Если НЕ ЗначениеЗаполнено(Результат.ДействуетС)
				И ПустаяСтрока(Результат.ТекущаяСтрока.Представление) Тогда
					Элементы.ГруппаИсторическийАдрес.Видимость = Ложь;
			ИначеЕсли ЗначениеЗаполнено(Результат.ДействуетПо) Тогда
				ТекстИсторическийАдрес = " " + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'действует по %1'"), Формат(Результат.ДействуетПо - 10, "ДЛФ=DD"));
			Иначе
				ТекстИсторическийАдрес = НСтр("ru = 'действует по настоящее время.'");
			КонецЕсли;
			ОтобразитьКоличествоЗаписейВИсторииИзменений();
		КонецЕсли;
	КонецЕсли;
	
	Элементы.ТекстПроДатуДействия.Заголовок = ТекстИсторическийАдрес;
	Элементы.АдресНаДату.ФорматРедактирования = ?(ЗначениеЗаполнено(АдресНаДату), "", "ДФ='""" + НСтр("ru='начало учета'") + """'");
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьКоличествоЗаписейВИсторииИзменений()
	
	Отбор = Новый Структура("Вид", ВидКонтактнойИнформации.Ссылка);
	НайденныеСтроки = КонтактнаяИнформацияОписаниеДополнительныхРеквизитов.НайтиСтроки(Отбор);
	Если НайденныеСтроки.Количество() > 1 Тогда
		Элементы.ИсторияИзмененийГиперссылка.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='История изменений (%1)'"), НайденныеСтроки.Количество());
		Элементы.ИсторияИзмененийГиперссылка.Видимость = Истина;
	ИначеЕсли НайденныеСтроки.Количество() = 1 И ПустаяСтрока(НайденныеСтроки[0].ЗначенияПолей) Тогда
		Элементы.ИсторияИзмененийГиперссылка.Видимость = Ложь;
	Иначе
		Элементы.ИсторияИзмененийГиперссылка.Заголовок = НСтр("ru='История изменений'");
		Элементы.ИсторияИзмененийГиперссылка.Видимость = Истина;
	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ОпределитьДатуДействия(ДействуетС, История)
	
	Результат = Новый Структура("ДействуетПо, ДействуетС, ТекущаяСтрока");
	Если История.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	ТекущаяСтрока        = Неопределено;
	ДействуетПо          = Неопределено;
	Минимум              = -1;
	МинимумСравнительный = Неопределено;
	
	Для каждого СтрокаИстория Из История Цикл
		Дельта = СтрокаИстория.ДействуетС - ДействуетС;
		Если Дельта <= 0 И (МинимумСравнительный = Неопределено ИЛИ Дельта > МинимумСравнительный) Тогда
			ТекущаяСтрока        = СтрокаИстория;
			МинимумСравнительный = Дельта;
		КонецЕсли;

		Если Минимум = -1 Тогда
			Минимум       = Дельта + 1;
			ТекущаяСтрока = СтрокаИстория;
		КонецЕсли;
		Если Дельта > 0 И МодульЧисла(Дельта) < МодульЧисла(Минимум) Тогда
			ДействуетПо = СтрокаИстория.ДействуетС;
			Минимум     = МодульЧисла(Дельта);
		КонецЕсли;
	КонецЦикла;
	
	Результат.ДействуетПо   = ДействуетПо;
	Результат.ДействуетС    = ТекущаяСтрока.ДействуетС;
	Результат.ТекущаяСтрока = ТекущаяСтрока;
	
	Возврат Результат;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция МодульЧисла(Число)
	Возврат Макс(Число, -Число);
КонецФункции

&НаКлиенте
Процедура Подключаемый_ТипСтроенияПриИзменении(Элемент)
	НомерСтроки = Сред(Элемент.Имя, 1 + СтрДлина("ТипСтроения"));
	СтрокаЗданий = ДополнительныеЗдания.Получить(НомерСтроки);
	СтрокаЗданий.Тип = ЭтотОбъект[Элемент.Имя];
	СтрокаЗданий.ПутьXPath = РаботаСАдресамиКлиентСервер.XPathНомераДополнительногоОбъектаАдресации(СтрокаЗданий.Тип);
	
	ОбновитьИндексИПредставление();
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СтроениеПриИзменении(Элемент)
	НомерСтроки = Сред(Элемент.Имя, 1 + СтрДлина("Строение"));
	СтрокаЗданий = ДополнительныеЗдания.Получить(НомерСтроки);
	СтрокаЗданий.Значение = ЭтотОбъект[Элемент.Имя];
	
	ОбновитьИндексИПредставление();
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Функция ОтобразитьДополнительныеПомещения(ФокусНаТип = Ложь)
	
	Удалять = Новый Массив;
	Пока Элементы.ГруппаПомещенияДополнительно.ПодчиненныеЭлементы.Количество() > 0 Цикл
		Группа = Элементы.ГруппаПомещенияДополнительно.ПодчиненныеЭлементы[0];
		Если ТипЗнч(Группа)=Тип("ГруппаФормы") Тогда
			Идентификатор = Сред(Группа.Имя, 1 + СтрДлина("ГруппаПомещение"));
			Если Не ПустаяСтрока(Идентификатор) Тогда
				Удалять.Добавить("ТипПомещения" + Идентификатор);
				Удалять.Добавить("Помещение"    + Идентификатор);
				КомандаУдалить = Команды.Найти("УдалитьПомещение" + Идентификатор);
				Если КомандаУдалить<>Неопределено Тогда
					Команды.Удалить(КомандаУдалить);
				КонецЕсли;
			КонецЕсли;
			УдалитьГруппуЭлементов(Группа);
		Иначе
			Элементы.Удалить(Группа);
		КонецЕсли;
	КонецЦикла;
	ИзменитьРеквизиты(,Удалять);
	
	КоличествоПомещений = ДополнительныеПомещения.Количество() - 1;
	КоличествоТипов     = Элементы.ТипПомещения.СписокВыбора.Количество() - 2;
	
	Результат = Неопределено;
	Для НомерСтроки = 0 По КоличествоПомещений Цикл
		Строка = ДополнительныеПомещения[НомерСтроки];
		Идентификатор = Формат(НомерСтроки, "ЧН=; ЧГ=");
		
		НоваяГруппа = Элементы.Добавить("ГруппаПомещение" + Идентификатор, Тип("ГруппаФормы"), Элементы.ГруппаПомещенияДополнительно);
		ЗаполнитьЗначенияСвойств(НоваяГруппа, Элементы.ГруппаПомещениеНачальное, , "ПутьКДаннымЗаголовка");
		
		НовыйТип = Элементы.Добавить("ТипПомещения" + Идентификатор, Тип("ПолеФормы"), НоваяГруппа);
		ЗаполнитьЗначенияСвойств(НовыйТип, Элементы.ТипПомещения, , "ПутьКДанным, СписокВыбора, ВыделенныйТекст, СвязьПоТипу");
		НовыйТип.СписокВыбора.ЗагрузитьЗначения(Элементы.ТипПомещения.СписокВыбора.ВыгрузитьЗначения());
		НовыйТип.УстановитьДействие("ПриИзменении", "Подключаемый_ТипПомещенияПриИзменении");
		
		НовыйЭлемент = Элементы.Добавить("Помещение" + Идентификатор, Тип("ПолеФормы"), НоваяГруппа);
		ЗаполнитьЗначенияСвойств(НовыйЭлемент, Элементы.Помещение, , "ПутьКДанным, СписокВыбора, ВыделенныйТекст, СвязьПоТипу");
		НовыйЭлемент.СписокВыбора.ЗагрузитьЗначения(Элементы.Помещение.СписокВыбора.ВыгрузитьЗначения());
		НовыйЭлемент.УстановитьДействие("ПриИзменении", "Подключаемый_ПомещениеПриИзменении");
		
		Если НомерСтроки = КоличествоПомещений Тогда
			Если ФокусНаТип Тогда
				Результат = НовыйТип.Имя;
			Иначе
				Результат = НовыйЭлемент.Имя;
			КонецЕсли;
		КонецЕсли;
		
		Добавлять = Новый Массив;
		Добавлять.Добавить(Новый РеквизитФормы(НовыйТип.Имя, Новый ОписаниеТипов("Строка")));
		Добавлять.Добавить(Новый РеквизитФормы(НовыйЭлемент.Имя, Новый ОписаниеТипов("Строка")));
		
		ИзменитьРеквизиты(Добавлять);
		ЭтотОбъект[НовыйТип.Имя]     = Строка.Тип;
		ЭтотОбъект[НовыйЭлемент.Имя] = Строка.Значение;
		
		НовыйТип.ПутьКДанным     = НовыйТип.Имя;
		НовыйЭлемент.ПутьКДанным = НовыйЭлемент.Имя;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура Подключаемый_ТипПомещенияПриИзменении(Элемент)
	НомерСтроки = Сред(Элемент.Имя, 1 + СтрДлина("ТипПомещения"));
	СтрокаПомещений = ДополнительныеПомещения.Получить(НомерСтроки);
	СтрокаПомещений.Тип = ЭтотОбъект[Элемент.Имя];
	СтрокаПомещений.ПутьXPath = РаботаСАдресамиКлиентСервер.XPathНомераДополнительногоОбъектаАдресации(СтрокаПомещений.Тип);
	
	Контекст = КонтекстФормыКлиент();
	ЗаполнитьПредставлениеАдреса(Контекст);
	КонтекстФормыКлиент(Контекст);
	
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПомещениеПриИзменении(Элемент)
	НомерСтроки = Сред(Элемент.Имя, 1 + СтрДлина("Помещение"));
	СтрокаЗданий = ДополнительныеПомещения.Получить(НомерСтроки);
	СтрокаЗданий.Значение = ЭтотОбъект[Элемент.Имя];
	
	Контекст = КонтекстФормыКлиент();
	ЗаполнитьПредставлениеАдреса(Контекст);
	КонтекстФормыКлиент(Контекст);
	
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте 
Процедура СообщитьОбОшибкахЗаполнения(СписокОшибок, ПредупреждатьОбОтсутствии, ТребуетсяОбновление = Ложь)
	
	ОчиститьСообщения();
	
	КоличествоОшибок = СписокОшибок.Количество();
	Если КоличествоОшибок = 0 И ПредупреждатьОбОтсутствии Тогда
		// Нет ошибок
		Если НЕ ЭтоВебСервис И ТребуетсяОбновление Тогда
			Если ЕстьПравоЗагружатьКлассификатор Тогда
				СообщениеОПроверкеАдреса = НСтр("ru='Адрес введен корректно, но адресные сведения, загруженные в программу, давно не обновлялись.
				|Рекомендуется выполнить обновление и повторить проверку адреса заново.'");
				ВопросОбОбновлениеАдресногоКлассификатора(СообщениеОПроверкеАдреса);
			Иначе
				СообщениеОПроверкеАдреса = НСтр("ru='Адрес введен корректно, но адресные сведения, загруженные в программу,
				|давно не обновлялись. Рекомендуется выполнить обновление и повторить проверку адреса заново.
				|Для обновления адресного классификатора необходимо обратиться к администратору.'");
				ПоказатьПредупреждение(, СообщениеОПроверкеАдреса);
			КонецЕсли;
		Иначе
			ПоказатьПредупреждение(, НСтр("ru='Адрес введен корректно.'"));
		КонецЕсли;
		
		Элементы.ГруппаОписаниеНедоступностиСервера.ТекущаяСтраница = Элементы.СервисДоступен;
		Возврат;
	КонецЕсли;
	
	Если КоличествоОшибок = 1 Тогда
		МестоОшибки = СписокОшибок[0].Значение;
		Если СписокОшибок[0].Пометка = Ложь И НЕ ЭтоВебСервис Тогда
			ТекстВопроса = НСтр("ru = 'Адрес не может быть проверен.'") + Символы.ПС + СписокОшибок[0].Представление + Символы.ПС + НСтр("ru = 'Загрузить классификатор для проверки адреса?'");
			ПредложениеЗагрузкиКлассификатора(ТекстВопроса);
			Возврат;
		ИначеЕсли ПустаяСтрока(МестоОшибки) Или МестоОшибки = "/" Тогда
			// Одна ошибка на весь адрес, не привязанная к полю.
			ПоказатьПредупреждение(, СписокОшибок[0].Представление);
			Элементы.ГруппаОписаниеНедоступностиСервера.ТекущаяСтраница = Элементы.СообщениеОбОшибке;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Элементы.ГруппаОписаниеНедоступностиСервера.ТекущаяСтраница = Элементы.СообщениеОбОшибке;
	// Сообщаем список с привязкой к полям.
	Для Каждого Элемент Из СписокОшибок Цикл
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			Элемент.Представление,,,ПутьКДаннымФормыПоПутиXPath(Элемент.Значение));
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура ПослеВопросОбОбновление(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = "ОбновитьКлассификатор" Тогда
		Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.АдресныйКлассификатор") Тогда
			ОповещениеОЗакрытие = Новый ОписаниеОповещения("ПослеЗагрузкиАдресногоКлассификатора", ЭтотОбъект);
			МодульАдресныйКлассификаторКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("АдресныйКлассификаторКлиент");
			МодульАдресныйКлассификаторКлиент.ПоказатьФормуЗагрузкиАдресногоКлассификатора(ОповещениеОЗакрытие);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ПутьКДаннымФормыПоПутиXPath(ПутьXPath) 
	
	Если ПутьXPath = "СубъектРФ" Тогда
		Возврат "НаселенныйПункт";
		
	ИначеЕсли ПутьXPath = "Округ" Тогда
		Возврат "НаселенныйПункт";
		
	ИначеЕсли ПутьXPath = РаботаСАдресамиКлиентСервер.XPathРайона() Тогда
		Возврат "НаселенныйПункт";
		
	ИначеЕсли ПутьXPath = "Город" Тогда
		Возврат "НаселенныйПункт";
		
	ИначеЕсли ПутьXPath = "ВнутригРайон" Тогда
		Возврат "НаселенныйПункт";
		
	ИначеЕсли ПутьXPath = "НаселПункт" Тогда
		Возврат "НаселенныйПункт";
		
	ИначеЕсли ПутьXPath = "Улица" Тогда
		Возврат "Улица";
		
	ИначеЕсли ПутьXPath = РаботаСАдресамиКлиентСервер.XPathПочтовогоИндекса() Тогда
		Возврат "Индекс";
		
	КонецЕсли;
	
	// Дополнительно добавленные строения и помещения.
	Фильтр = Новый Структура("ПутьXPath", ПутьXPath);
	
	Строки = ДополнительныеЗдания.НайтиСтроки(Фильтр);
	Если Строки.Количество() > 0 Тогда
		// Первое непустое
		Для Каждого СтрокаЗдания Из Строки Цикл
			ИмяРеквизита = "Строение" + Формат(ДополнительныеЗдания.Индекс(СтрокаЗдания), "ЧН=; ЧГ=");
			Если ЗначениеЗаполнено(ЭтотОбъект[ИмяРеквизита]) Тогда
				Возврат ИмяРеквизита;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Строки = ДополнительныеПомещения.НайтиСтроки(Фильтр); 
	Если Строки.Количество() > 0 Тогда
		// Первое непустое
		Для Каждого СтрокаПомещения Из Строки Цикл
			ИмяРеквизита = "Помещение" + Формат(ДополнительныеПомещения.Индекс(СтрокаПомещения), "ЧН=; ЧГ=");
			Если ЗначениеЗаполнено(ЭтотОбъект[ИмяРеквизита]) Тогда
				Возврат ИмяРеквизита;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Варианты дома
	Для Каждого ЭлементСписка Из Элементы.ТипДома.СписокВыбора Цикл
		Если ПутьXPath = РаботаСАдресамиКлиентСервер.XPathНомераДополнительногоОбъектаАдресации(ЭлементСписка.Значение) Тогда
			Возврат "Дом";
		КонецЕсли;
	КонецЦикла;
	
	// Варианты корпуса
	Для Каждого ЭлементСписка Из Элементы.ТипСтроения.СписокВыбора Цикл
		Если ПутьXPath = РаботаСАдресамиКлиентСервер.XPathНомераДополнительногоОбъектаАдресации(ЭлементСписка.Значение) Тогда
			Возврат "Строение";
		КонецЕсли;
	КонецЦикла;
	
	// Варианты помещения
	Для Каждого ЭлементСписка Из Элементы.ТипПомещения.СписокВыбора Цикл
		Если ПутьXPath = РаботаСАдресамиКлиентСервер.XPathНомераДополнительногоОбъектаАдресации(ЭлементСписка.Значение) Тогда
			Возврат "Помещение";
		КонецЕсли;
	КонецЦикла;
		
	// Не нашли
	Возврат "";
КонецФункции

&НаКлиенте
Процедура ОчиститьАдресКлиент()
	
	Контекст = КонтекстФормыКлиент();
	ОчиститьАдресСервер(Контекст);
	КонтекстФормыКлиент(Контекст);
	
	Модифицированность = Истина;
КонецПроцедуры

&НаСервере
Процедура ОчиститьАдресСервер(Контекст)
	
	Контекст.ПредставлениеАдреса            = "";
	Контекст.Комментарий                    = "";
	Контекст.Индекс                         = Неопределено;
	Контекст.ОКТМО                          = Неопределено;
	Контекст.ИдентификаторНаселенногоПункта = Неопределено;
	Контекст.НаселенныйПункт                = "";
	Контекст.НаселенныйПунктДетально        = Обработки.РасширенныйВводКонтактнойИнформации.СписокРеквизитовНаселенныйПункт(Неопределено, "ФИАС");
	
	Контекст.ИдентификаторУлицы = Неопределено;
	Контекст.Улица              = "";
	
	Контекст.ТипДома      = УправлениеКонтактнойИнформациейКлиентСервер.ПервыйИлиПустой(Элементы.ТипДома.СписокВыбора);
	Контекст.ТипСтроения  = УправлениеКонтактнойИнформациейКлиентСервер.ПервыйИлиПустой(Элементы.ТипСтроения.СписокВыбора);
	Контекст.ТипПомещения = УправлениеКонтактнойИнформациейКлиентСервер.ПервыйИлиПустой(Элементы.ТипПомещения.СписокВыбора);
	
	Контекст.Дом          = "";
	Контекст.Строение     = "";
	Контекст.Помещение    = "";
	
	Контекст.ДополнительныеЗдания.Очистить();
	Контекст.ДополнительныеПомещения.Очистить();
	
	XDTOКонтактнаяИнфо = КонтактнаяИнформацияПоЗначениюРеквизитов(Контекст);
	СформироватьДетальныйНаселенныйПункт(Контекст, XDTOКонтактнаяИнфо.Состав);
	ЗаполнитьПредставлениеАдреса(Контекст, XDTOКонтактнаяИнфо);
	
	// Очищаем непосредственно на форме, в контексте уже очищено.
	Контекст.ДополнительныеЗдания.Очистить();
	Контекст.ДополнительныеПомещения.Очистить();
	ОтобразитьДополнительныеЗданияИПомещения();
КонецПроцедуры

&НаКлиенте
Функция НеиспользованныеЭлементыДополнительнойТаблицы(ТаблицаДанных, ЭлементИсточник, ЗначениеМаркера)
	Использованные = Новый Соответствие;
	Использованные.Вставить(ЭтотОбъект[ЭлементИсточник.Имя], Истина);
	Для Каждого Строка Из ТаблицаДанных Цикл
		Использованные.Вставить(Строка.Тип, Истина);
	КонецЦикла;
	
	Результат = Новый СписокЗначений;
	Для Каждого ЭлементСписка Из ЭлементИсточник.СписокВыбора Цикл
		Если Использованные[ЭлементСписка.Значение]=Неопределено Тогда
			Результат.Добавить(ЗначениеМаркера, ЭлементСписка.Значение, ЭлементСписка.Пометка, ЭлементСписка.Картинка);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Функция МожноДобавлятьДополнительныеОбъекты()
	ВариантыСтроения  = НеиспользованныеЭлементыДополнительнойТаблицы(ДополнительныеЗдания, Элементы.ТипСтроения, 1);
	ВариантыПомещения = НеиспользованныеЭлементыДополнительнойТаблицы(ДополнительныеПомещения, Элементы.ТипПомещения, 2);
	Возврат ВариантыСтроения.Количество() + ВариантыПомещения.Количество() > 0
КонецФункции

&НаСервере
Процедура УстановитьКлючИспользованияФормы()
	КлючСохраненияПоложенияОкна = Строка(Страна);
	
	Количество = 0;
	Для Каждого Строка Из ДополнительныеЗдания Цикл
		Если Не ПустаяСтрока(Строка.Значение) Тогда
			Количество = Количество + 1;
		КонецЕсли;
	КонецЦикла;
	КлючСохраненияПоложенияОкна = КлючСохраненияПоложенияОкна + "/" + Формат(Количество, "ЧН=; ЧГ=");
	
	Количество = 0;
	Для Каждого Строка Из ДополнительныеПомещения Цикл
		Если Не ПустаяСтрока(Строка.Значение) Тогда
			Количество = Количество + 1;
		КонецЕсли;
	КонецЦикла;
	
	КлючСохраненияПоложенияОкна = КлючСохраненияПоложенияОкна + "/" + Формат(Количество, "ЧН=; ЧГ=");
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////////////////////////

// Преобразовываем Реквизиты формы <-> Структура
//
&НаКлиенте
Функция КонтекстФормыКлиент(НовыеДанные = Неопределено)
	
	СписокРеквизитов = "
		|ВидКонтактнойИнформации,
		|Страна, КодСтраны, ОсновнаяСтрана, СкрыватьНеактуальныеАдреса,
		|Индекс, ПредставлениеАдреса, Комментарий, КопияКомментария,
		|НаселенныйПункт, ИдентификаторНаселенногоПункта, НаселенныйПунктДетально,
		|Улица, ИдентификаторУлицы, ОКТМО, ИдентификаторУлицы,
		|ТипДома, Дом, ТипСтроения, Строение, ТипПомещения, Помещение,
		|РазрешитьВводАдресаВСвободнойФорме, ПредставлениеАдресаИзменено,
		|Модифицированность, ЕстьКлассификатор, ДополнительныйЭлемент, ПодчиненныйЭлемент, ЭтоВебСервис, АдресНаДату
		|";
		
	СписокКоллекций = "ДополнительныеЗдания, ДополнительныеПомещения";
	
	Если НовыеДанные = Неопределено Тогда
		// Читаем
		Результат = Новый Структура(СписокРеквизитов + "," + СписокКоллекций);
		ЗаполнитьЗначенияСвойств(Результат, ЭтотОбъект, СписокРеквизитов + "," + СписокКоллекций);
		Возврат Результат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, НовыеДанные, СписокРеквизитов, СписокКоллекций);
	ЗаполнитьЗначенияКоллекций(ЭтотОбъект, НовыеДанные, СписокКоллекций);
	
	Возврат НовыеДанные;
КонецФункции

&НаКлиенте
Процедура ЗаполнитьЗначенияКоллекций(Приемник, Источник, СписокСвойств)
	Для Каждого КлючЗначение Из Новый Структура(СписокСвойств) Цикл
		ИмяСвойства = КлючЗначение.Ключ;
		СвойствоПриемник = Приемник[ИмяСвойства];
		СвойствоПриемник.Очистить();
		Для Каждого Значение Из Источник[ИмяСвойства] Цикл
			ЗаполнитьЗначенияСвойств(СвойствоПриемник.Добавить(), Значение);
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

// Разрешение элементов для ввода в свободном формате.
//
// Параметры:
//    - Режим                    - Булево - Истина - можно редактировать представление адреса, Ложь - нельзя.
//    - ФормироватьПредставление - Булево - необязательный флаг. По умолчанию установлен.
//
&НаКлиенте
Процедура СостояниеВводаПредставленияАдреса(РедактироватьПредставлениеАдреса, ФормироватьПредставление = Истина)
	
	Если Страна = ОсновнаяСтрана Тогда
		Элемент = Элементы.ПредставлениеАдреса;
		// Проверять адрес, введенный вручную нельзя.
		Элементы.ПроверитьЗаполнениеАдреса.Доступность = Не РедактироватьПредставлениеАдреса;
	Иначе
		Элемент = Элементы.ПредставлениеИностранногоАдреса;
	КонецЕсли;
	Элемент.ТолькоПросмотр = Не РедактироватьПредставлениеАдреса;
	Если РедактироватьПредставлениеАдреса Тогда
		Элемент.ЦветФона = АвтоЦвет;
	Иначе
		Элемент.ЦветФона = ЦветФонаФормы;
		
		Если ФормироватьПредставление Тогда
			Контекст = КонтекстФормыКлиент();
			ЗаполнитьПредставлениеАдреса(Контекст);
			КонтекстФормыКлиент(Контекст);
		КонецЕсли;
	КонецЕсли;
	Элементы.Индекс.Доступность = НЕ РедактироватьПредставлениеАдреса;
	Элементы.ОКТМО.Доступность = НЕ РедактироватьПредставлениеАдреса;
	
	// Остальные поля ввода
	СостояниеГруппыВвода(Элементы.ТипАдреса, Не РедактироватьПредставлениеАдреса);
	
	// Пометка режима
	Элементы.ВвестиАдресВСвободнойФорме.Пометка            = РедактироватьПредставлениеАдреса;
	Элементы.ВвестиАдресВСвободнойФормеВсеДействия.Пометка = РедактироватьПредставлениеАдреса;
	Элементы.ПредставлениеАдресаКонтекстноеМенюВвестиАдресВСвободнойФорме.Пометка = РедактироватьПредставлениеАдреса;
	
	// Страна - по прежнему управляющее поле.
	Если Элементы.Страна.Доступность Тогда
		Элементы.Страна.ЦветФона = ЦветФонаУправляющегоПоля;
	КонецЕсли;
	
	// Переключаем заголовок представления и текущий элемент ввода для индикации режима.
	Если РедактироватьПредставлениеАдреса Тогда
		Элементы.ПредставлениеАдреса.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Верх;
		Элементы.ПредставлениеАдреса.Заголовок          = НСтр("ru='Адрес в свободной форме'");
		Элементы.СтранаАдреса.Отображение        = ОтображениеОбычнойГруппы.Нет;
		Если Страна <> ОсновнаяСтрана Тогда
			ТекущийЭлемент = Элементы.ПредставлениеИностранногоАдреса;
		Иначе
			ТекущийЭлемент = Элементы.ПредставлениеАдреса;
		КонецЕсли;
	Иначе 
		Элементы.ПредставлениеАдреса.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		Элементы.ПредставлениеАдреса.Заголовок          = "";
		Элементы.СтранаАдреса.Отображение        = ОтображениеОбычнойГруппы.ОбычноеВыделение;
		Если Страна = ОсновнаяСтрана Тогда
			ТекущийЭлемент = Элементы.НаселенныйПункт;
		Иначе
			ТекущийЭлемент = Элементы.НаселенныйПунктИностранныйАдреса;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Установка доступности элементов в группе.
//
// Параметры:
//    - Группа - ГруппаФормы - Контейнер для элементов.
//    - Режим  - Булево      - Флаг разрешения элементов. Истина - разрешены, Ложь - нет.
//
&НаКлиенте
Процедура СостояниеГруппыВвода(Группа, Режим)
	
	Для Каждого Элемент Из Группа.ПодчиненныеЭлементы Цикл
		ТипЭлемента = ТипЗнч(Элемент);
		Если ТипЭлемента = Тип("ГруппаФормы") Тогда
			Если Элемент <> Элементы.ИностранныйАдрес Тогда
				СостояниеГруппыВвода(Элемент, Режим);
			КонецЕсли;
			
		ИначеЕсли ТипЭлемента = Тип("КнопкаФормы") Тогда
			Если Элемент = Элементы.ДобавитьОбъект Тогда
				Элемент.Доступность = Режим И МожноДобавлятьДополнительныеОбъекты();
			Иначе
				Элемент.Доступность = Режим;
			КонецЕсли;
			
		ИначеЕсли ТипЭлемента = Тип("ПолеФормы") И Элемент.Вид = ВидПоляФормы.ПолеВвода Тогда
			Если Элемент <> Элементы.ПредставлениеАдреса Тогда
				Элемент.ТолькоПросмотр = Не Режим;
				Элемент.ЦветФона = ?(Режим, АвтоЦвет, ЦветФонаФормы);
			КонецЕсли;
			
		Иначе 
			Элемент.Доступность = Режим;
			
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ПроверитьДоступностьКлассификатора()
	
	Если Не ЕстьКлассификатор Тогда
		Возврат;
	КонецЕсли;
	
	Задание = ПроверитьДоступностьКлассификатораВФоне(УникальныйИдентификатор);
	
	НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
	
	Обработчик = Новый ОписаниеОповещения("ПослеПроверкиДоступностиКлассификатораВФоне", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(Задание, Обработчик, НастройкиОжидания);
КонецПроцедуры

&НаКлиенте
Процедура ПослеПроверкиДоступностиКлассификатораВФоне(Задание, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Задание) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если Задание.Статус = "Выполнено" Тогда
		ДоступностьКлассификатора = ПолучитьИзВременногоХранилища(Задание.АдресРезультата);
		Если ДоступностьКлассификатора <> Неопределено Тогда
			Элементы.ГруппаОписаниеНедоступностиСервера.ТекущаяСтраница = ?(ДоступностьКлассификатора.Отказ,
			Элементы.СервисНедоступен, Элементы.СервисДоступен);
			Если ДоступностьКлассификатора.Отказ Тогда
				ТекстСообщенияСервиса = НСтр("ru = 'Автоподбор и проверка адреса недоступны:'") + Символы.ПС 
				+ ДоступностьКлассификатора.КраткоеПредставлениеОшибки;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли Задание.Статус = "Ошибка" Тогда
		Элементы.ГруппаОписаниеНедоступностиСервера.ТекущаяСтраница = Элементы.СервисНедоступен;
		ТекстСообщенияСервиса = НСтр("ru = 'Автоподбор и проверка адреса недоступны:'") + Символы.ПС 
		+ Задание.КраткоеПредставлениеОшибки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АвторизацияНаСайтеПоддержкиПользователейЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ОбновитьПовторноИспользуемыеЗначения();
		ПроверитьДоступностьКлассификатора();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНаселенныйПунктИностранногоАдреса()
	
	НаселенныйПунктМассив = СтрРазделить(НаселенныйПункт, ",", Ложь);
	КоличествоЭлементовВНаселенныйПункт = 0;
	СоответствиеУровнейНаселенногоПункта = Новый Соответствие;
	Для каждого НаселенныйПунктДетальноЭлемент Из НаселенныйПунктДетально Цикл
		Если НЕ (НаселенныйПунктДетальноЭлемент.Значение.Уровень = 2 ИЛИ НаселенныйПунктДетальноЭлемент.Значение.Уровень = 5
			ИЛИ НаселенныйПунктДетальноЭлемент.Значение.Уровень > 6) Тогда
			Если ЗначениеЗаполнено(НаселенныйПунктДетальноЭлемент.Значение.Представление) Тогда
				КоличествоЭлементовВНаселенныйПункт = КоличествоЭлементовВНаселенныйПункт + 1;
			КонецЕсли;
		КонецЕсли;
		СоответствиеУровнейНаселенногоПункта.Вставить(НаселенныйПунктДетальноЭлемент.Значение.Уровень, НаселенныйПунктДетальноЭлемент);
	КонецЦикла;
	
	ПозицияВМассиве = НаселенныйПунктМассив.Количество() - 1;
	
	Если ПозицияВМассиве >=0 Тогда
		Если НаселенныйПунктМассив.Количество() = КоличествоЭлементовВНаселенныйПункт Тогда
			Для Уровень = 1 По 6 Цикл
				Если НЕ (Уровень = 2 ИЛИ Уровень = 5) Тогда
					Элемент = СоответствиеУровнейНаселенногоПункта.Получить(Уровень);
					Если Элемент <> Неопределено И ЗначениеЗаполнено(Элемент.Значение.Представление) Тогда
						Элемент.Значение.Представление = СокрЛП(НаселенныйПунктМассив.Получить(ПозицияВМассиве));
						ПозицияВМассиве = ПозицияВМассиве - 1;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		Иначе
			Для Уровень = 1 По 6 Цикл
				Если НЕ (Уровень = 2 ИЛИ Уровень = 5) Тогда
					Элемент = СоответствиеУровнейНаселенногоПункта.Получить(Уровень);
					Если Элемент <> Неопределено Тогда
						Если ПозицияВМассиве >= 0 Тогда 
							Элемент.Значение.Представление = СокрЛП(НаселенныйПунктМассив.Получить(ПозицияВМассиве));
							ПозицияВМассиве = ПозицияВМассиве - 1;
						Иначе
							Элемент.Значение.Представление = "";
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	Иначе
		Для Уровень = 1 По 6 Цикл
			Элемент = СоответствиеУровнейНаселенногоПункта.Получить(Уровень);
			Элемент.Значение.Представление = "";
		КонецЦикла;
	КонецЕсли;
	
	ОбновитьПредставлениеАдреса();

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПредставлениеАдреса()
	Контекст = КонтекстФормыКлиент();
	ЗаполнитьПредставлениеАдреса(Контекст);
	КонтекстФормыКлиент(Контекст);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоляДомовИСтроений(Знач ВыбранноеЗначение)
	
	Модифицированность = Истина;
	НеобходимоПерерисоватьПоляДомов = Ложь;
	
	Если ДополнительныеЗдания.Количество() > 0 Тогда
		ДополнительныеЗдания.Очистить();
		НеобходимоПерерисоватьПоляДомов = Истина;
	КонецЕсли;

	Если ВыбранноеЗначение.Свойство("Дом") Тогда
		ИнформацияОДоме = ВыбранноеЗначение.Дом;
		Дом             = ИнформацияОДоме.НомерВладения;
		ТипДома         =  ИнформацияОДоме.ВидВладения;
	Иначе 
		ИнформацияОДоме = Неопределено;
	КонецЕсли;
	
	ИнформацияОКорпусе  = Неопределено;
	ИнформацияОСтроение = Неопределено;
	ВыбранноеЗначение.Свойство("Корпус", ИнформацияОКорпусе);
	ВыбранноеЗначение.Свойство("Строение", ИнформацияОСтроение);
	
	СтроениеПустое = Истина;
	Элементы.Дом.ОбновитьТекстРедактирования();
	
	Если ИнформацияОКорпусе <> Неопределено Тогда
		Строение       = ИнформацияОКорпусе.НомерКорпуса;
		ТипСтроения    = ИнформацияОКорпусе.ВидКорпуса;
		ТекущийЭлемент = Элементы.Строение;
		СтроениеПустое = Ложь;
	КонецЕсли;
	
	Если ИнформацияОСтроение <> Неопределено Тогда
		Если ИнформацияОКорпусе <> Неопределено Тогда
			ДополнительныеЗдания.Очистить();
			Строка = ДополнительныеЗдания.Добавить();
		
			Строка.Тип       = ПерваяБукваЗаглавная(ИнформацияОСтроение.ВидСтроения);
			Строка.ПутьXPath = РаботаСАдресамиКлиентСервер.XPathНомераДополнительногоОбъектаАдресации(Строка.Тип);
			Строка.Значение  = ИнформацияОСтроение.НомерСтроения;
			НеобходимоПерерисоватьПоляДомов = Истина;
		Иначе
			Строение       = ИнформацияОСтроение.НомерСтроения;
			ТипСтроения    = ПерваяБукваЗаглавная(ИнформацияОСтроение.ВидСтроения);
			ТекущийЭлемент = Элементы.Строение;
		КонецЕсли;
		СтроениеПустое = Ложь;
	КонецЕсли;
	
	Если СтроениеПустое Тогда
		Строение = "";
	КонецЕсли;
	Элементы.Строение.КнопкаВыбора = Не СтроениеПустое;
	
	Если НеобходимоПерерисоватьПоляДомов Тогда
		ПодключитьОбработчикОжидания("ОтобразитьДополнительныеЗданияКлиент", 0.1, Истина);
		КоличествоВариантов = НеиспользованныеЭлементыДополнительнойТаблицы(ДополнительныеЗдания, Элементы.ТипСтроения, 1);
		Элементы.ДобавитьОбъект.Доступность = КоличествоВариантов.Количество() > 1;
	КонецЕсли;
	
	ОбновитьИндексИПредставление();

КонецПроцедуры

&НаКлиенте
Функция ПерваяБукваЗаглавная(Строка)
	Возврат ВРег(Лев(Строка, 1)) + Сред(НРег(Строка), 2); // Использовать ТРег нельзя.
КонецФункции

&НаКлиенте
Процедура ОтобразитьДополнительныеЗданияКлиент()
	ИмяТекущего = ОтобразитьДополнительныеЗдания();
	
	Если ИмяТекущего <> Неопределено Тогда
		ТекущийЭлемент = Элементы[ИмяТекущего];
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИндексИПредставление()
	Контекст = КонтекстФормыКлиент();
	УстановитьИндексИПредставление(Контекст);
	КонтекстФормыКлиент(Контекст);
КонецПроцедуры

// Представление разрешено менять только при вводе в режиме ввода в свободной форме.
// Поэтому необходимо привести остальные поля к измененному представлению.
// Страну не трогаем, режим свободного ввода возможен только в России.
//
&НаСервере
Процедура ПредставлениеАдресаПриИзмененииСервер()
	
	Если Страна = ОсновнаяСтрана Тогда
		// Пытаемся разобрать заново
		XDTOКонтактная = УправлениеКонтактнойИнформациейСлужебный.КонтактнаяИнформацияXDTOПоПредставлению(ПредставлениеАдреса, ВидКонтактнойИнформации);
		XDTOКонтактная.Представление = ПредставлениеАдреса;
		XDTOКонтактная.Комментарий   = Комментарий;
		
		// И проставляем в реквизиты, кроме страны и представления.
		ТекущееПредставление = ПредставлениеАдреса;
		ТекущаяСтрана        = Страна;
		
		ОчиститьАдресСервер(ЭтотОбъект);
		
		// Возможно будет отключен свободного ввода.
		ЗначениеРеквизитовПоКонтактнойИнформации(ЭтотОбъект, XDTOКонтактная);
		
		ПредставлениеАдреса = ТекущееПредставление;
		Страна              = ТекущаяСтрана;
		ОтобразитьДополнительныеЗданияИПомещения();
	КонецЕсли;
	
	// Включаем режим свободного ввода принудительно.
	РазрешитьВводАдресаВСвободнойФорме = Истина;
	Модифицированность = Истина;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПредопределенныеВариантыАдреса(Параметры)
	
	Если Параметры.Свойство("Индекс") Тогда
		Индекс = Параметры.Индекс;
	КонецЕсли;
	
	// Возможные варианты дома, строения, квартиры.
	ВариантыДанныхДом = Обработки.РасширенныйВводКонтактнойИнформации.ВариантыДанныхДом();
	УстановитьСписокВыбораЭлемента(Элементы.ТипДома, Элементы.Дом, ВариантыДанныхДом);
	УстановитьСписокВыбораЭлемента(Элементы.ТипДомаИностранныйАдреса, Элементы.ДомИностранныйАдреса, ВариантыДанныхДом);
	
	ВариантыДанныхСтроение = Обработки.РасширенныйВводКонтактнойИнформации.ВариантыДанныхСтроение();
	УстановитьСписокВыбораЭлемента(Элементы.ТипСтроения, Элементы.Строение, ВариантыДанныхСтроение);
	УстановитьСписокВыбораЭлемента(Элементы.ТипСтроенияИностранныйАдреса, Элементы.СтроениеИностранныйАдреса, ВариантыДанныхСтроение);
	
	ВариантыДанныхПомещение = Обработки.РасширенныйВводКонтактнойИнформации.ВариантыДанныхПомещение();
	УстановитьСписокВыбораЭлемента(Элементы.ТипПомещения, Элементы.Помещение, ВариантыДанныхПомещение);
	УстановитьСписокВыбораЭлемента(Элементы.ТипПомещенияИностранныйАдреса, Элементы.ПомещениеИностранныйАдреса, ВариантыДанныхПомещение);
	Элементы.ТипПомещения.СписокВыбора.НайтиПоЗначению("").Значение = НаименованиеПриДобавлениеПроизвольногоПомещения();
	Элементы.ТипПомещенияИностранныйАдреса.СписокВыбора.НайтиПоЗначению("").Значение = НаименованиеПриДобавлениеПроизвольногоПомещения();
	
	// Возможно пустые значения, чтобы не смущали.
	Если ПустаяСтрока(Дом) И ПустаяСтрока(ТипДома) Тогда
		ТипДома = УправлениеКонтактнойИнформациейКлиентСервер.ПервыйИлиПустой(Элементы.ТипДома);
	КонецЕсли;
	Если ПустаяСтрока(Строение) И ПустаяСтрока(ТипСтроения) Тогда
		ТипСтроения = УправлениеКонтактнойИнформациейКлиентСервер.ПервыйИлиПустой(Элементы.ТипСтроения);
	КонецЕсли;
	
	Если Параметры.Свойство("ТипПомещения")
		И ВариантыДанныхПомещение.ВариантыТипа.Найти(Параметры.ТипПомещения) <> Неопределено Тогда
			ТипПомещения = Параметры.ТипПомещения;
	КонецЕсли;

	Если ПустаяСтрока(Помещение) И ПустаяСтрока(ТипПомещения) Тогда
		ТипПомещения = УправлениеКонтактнойИнформациейКлиентСервер.ПервыйИлиПустой(Элементы.ТипПомещения);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Функция НедоступностьСервиса()
	
	Возврат Элементы.ГруппаОписаниеНедоступностиСервера.ТекущаяСтраница = Элементы.СервисНедоступен;
	
КонецФункции

&НаСервере
Процедура УстановитьВозможностьПодбораИндексаИНомеровДомов(Знач ЭтоВебСервис)
	
	ПодборПоИндексуДоступен = Истина;
	
	Если ЭтоВебСервис Тогда
		Элементы.Дом.КнопкаВыбора = Ложь;
		Элементы.Строение.КнопкаВыбора = Ложь;
	КонецЕсли;
	
	Если ЭтоВебСервис ИЛИ НЕ ЕстьПравоЗагружатьКлассификатор Тогда
		ПодборПоИндексуДоступен = Ложь;
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.АдресныйКлассификатор") Тогда
		МодульАдресныйКлассификаторСлужебный = ОбщегоНазначения.ОбщийМодуль("АдресныйКлассификаторСлужебный");
		Сведения = МодульАдресныйКлассификаторСлужебный.КраткиеСведенияОЗагрузкеСубъектовРФ();
		
		Если Сведения.КоличествоРегионов <= Сведения.КоличествоЗагруженныхРегионов Тогда
			ПодборПоИндексуДоступен = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Сравниваем два представления на эквивалентность.
&НаСервереБезКонтекста
Функция ПредставленияАдресаОдинаковы(Знач Представление1, Знач Представление2, Знач ИгнорироватьЗнакНомера=Ложь)
	Возврат ХешПредставления(Представление1, ИгнорироватьЗнакНомера)=ХешПредставления(Представление2, ИгнорироватьЗнакНомера);
КонецФункции

&НаСервереБезКонтекста
Функция ХешПредставления(Знач Представление, Знач ИгнорироватьЗнакНомера=Ложь)
	Результат = СтрЗаменить(Представление, Символы.ПС, "");
	Результат = СтрЗаменить(Результат, " ", "");
	Если ИгнорироватьЗнакНомера Тогда
		Результат = СтрЗаменить(Результат, "№", "");
	КонецЕсли;
	Возврат ВРег(Результат);
КонецФункции

&НаСервереБезКонтекста
Функция НаличиеИндексаВКлассификаторе(ПочтовыйИндекс)
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.АдресныйКлассификатор") Тогда
		МодульАдресныйКлассификаторСлужебный = ОбщегоНазначения.ОбщийМодуль("АдресныйКлассификаторСлужебный");
		Если МодульАдресныйКлассификаторСлужебный.КодСубъектаРФПоПочтовомуИндексуВКлассификаторе(ПочтовыйИндекс) <> Неопределено Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ложь;
КонецФункции

&НаСервереБезКонтекста
Функция СведенияОРегионе(НаименованиеСубъектаРФ)
	
	МодульАдресныйКлассификаторСлужебный = ОбщегоНазначения.ОбщийМодуль("АдресныйКлассификаторСлужебный");
	СведенияОРегионе = МодульАдресныйКлассификаторСлужебный.СведенияОРегионе(НаименованиеСубъектаРФ);
	
	Возврат СведенияОРегионе;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПроверитьДоступностьКлассификатораВФоне(Знач УникальныйИдентификатор)
	ИмяМетода = "Обработки.РасширенныйВводКонтактнойИнформации.ПроверитьДоступностьКлассификатора";
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Проверка доступности сервиса адресного классификатора'");
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(ИмяМетода, Новый Структура, НастройкиЗапуска);
КонецФункции

&НаСервереБезКонтекста
Функция НаселенныйПунктДетальноПоИдентификатору(ИдентификаторНаселенногоПункта, ФорматАдреса)
	Возврат Обработки.РасширенныйВводКонтактнойИнформации.СписокРеквизитовНаселенныйПункт(ИдентификаторНаселенногоПункта, ФорматАдреса);
КонецФункции

&НаСервереБезКонтекста
Функция СписокАвтоподбораНаселенногоПункта(Текст, ДополнительныеПараметры)
	
	Возврат Обработки.РасширенныйВводКонтактнойИнформации.СписокАвтоподбораНаселенногоПункта(Текст, ДополнительныеПараметры);
	
КонецФункции

&НаСервереБезКонтекста
Функция ЕстьСписокДомов(Идентификатор)

	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.АдресныйКлассификатор") Тогда
		МодульАдресныйКлассификаторСлужебный = ОбщегоНазначения.ОбщийМодуль("АдресныйКлассификаторСлужебный");
		ЕстьСписокДомов = МодульАдресныйКлассификаторСлужебный.ЕстьСписокДомов(Идентификатор);
		Возврат ЕстьСписокДомов;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаСервереБезКонтекста
Функция СписокАвтоподбораУлицы(ИдентификаторНаселенногоПункта, Текст, ДополнительныеПараметры)
	
	Возврат Обработки.РасширенныйВводКонтактнойИнформации.СписокАвтоподбораУлицы(ИдентификаторНаселенногоПункта, Текст, ДополнительныеПараметры);
	
КонецФункции

&НаСервереБезКонтекста
Функция СписокАвтоподбораВариантовДомов(Идентификатор, Текст)
	Возврат Обработки.РасширенныйВводКонтактнойИнформации.СписокАвтоподбораНомераДомов(Идентификатор, Текст);
КонецФункции

&НаСервереБезКонтекста
Функция НаселенныеПунктыПоПредставлению(Текст)
	Возврат Обработки.РасширенныйВводКонтактнойИнформации.НаселенныеПунктыПоПредставлению(Текст);
КонецФункции

&НаСервереБезКонтекста
Функция УлицыПоПредставлению(Родитель, Текст, СкрыватьНеактуальныеАдреса = Ложь, ВыбиратьСтрок = 50)
	
	Возврат Обработки.РасширенныйВводКонтактнойИнформации.УлицыПоПредставлению(Родитель, Текст, СкрыватьНеактуальныеАдреса, ВыбиратьСтрок);
	
КонецФункции

// УправлениеКонтактнойИнформациейКлиент

// Обработчик события НачалоВыбора для улицы.
//
//  Параметры:
//      Владелец                       - Произвольный - вызывающий элемент формы.
//      ИдентификаторНаселенногоПункта - УникальныйИдентификатор - ограничение по населенному пункту.
//      ТекущееЗначение                - УникальныйИдентификатор, Строка - текущее значение - или код классификатора,
//                                       или текст.
//      ДополнительныеПараметры        - Структура - дополнительная структура параметров.
//
&НаКлиенте
Процедура НачалоВыбораУлицы(Владелец, ИдентификаторНаселенногоПункта, ТекущееПредставлениеУлицы, ДополнительныеПараметры)
	
	Если Не ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.АдресныйКлассификатор") Тогда
		Возврат;
	КонецЕсли;
	
	СкрыватьНеактуальныеАдреса = Неопределено;
	ДополнительныеПараметры.Свойство("СкрыватьНеактуальныеАдреса", СкрыватьНеактуальныеАдреса);
	Если СкрыватьНеактуальныеАдреса = Неопределено Тогда
		СкрыватьНеактуальныеАдреса = Ложь;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Родитель", ИдентификаторНаселенногоПункта);
	ПараметрыФормы.Вставить("ПредставлениеУлицы", ТекущееПредставлениеУлицы);
	ПараметрыФормы.Вставить("Уровень",  7);
	ПараметрыФормы.Вставить("СкрыватьНеактуальныеАдреса", СкрыватьНеактуальныеАдреса);
	ПараметрыФормы.Вставить("НаселенныйПунктДетально", ДополнительныеПараметры.НаселенныйПунктДетально);
	
	ОткрытьФорму("Обработка.РасширенныйВводКонтактнойИнформации.Форма.ВыборУлицы", ПараметрыФормы, Владелец, 
		,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

// Предлагает загрузить адресный классификатор.
//
//  Параметры:
//      Текст  - Строка        - Дополнительный текст предложения.
//      Регион - Число, Строка - Код или название региона для загрузки.
//
&НаКлиенте
Процедура ПредложениеЗагрузкиКлассификатора(Знач Текст = "", Знач Регион = Неопределено)
	
	Если Не ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.АдресныйКлассификатор") Тогда
		Возврат;
	КонецЕсли;
	
	НеЗагружатьАдресныйКлассификатор = ПараметрыПриложения.Получить("АдресныйКлассификатор.НеЗагружатьКлассификатор");
	Если НеЗагружатьАдресныйКлассификатор = Неопределено ИЛИ НеЗагружатьАдресныйКлассификатор = Ложь Тогда
		ТипПараметраРегиона = ТипЗнч(Регион);
		ПараметрыФормы = Новый Структура;
		ПараметрыЗагрузки = Новый Структура;
		ПараметрыФормы.Вставить("ТекстПредупреждения", Текст);
		
		Если ТипПараметраРегиона = Тип("Число") Тогда
			ПараметрыЗагрузки.Вставить("КодРегионаДляЗагрузки", Регион);
			
		ИначеЕсли ТипПараметраРегиона = Тип("Строка") Тогда
			ПараметрыЗагрузки.Вставить("НазваниеРегиона", Регион);
			ПараметрыФормы.Вставить("НазваниеРегиона", Регион);
		КонецЕсли;
		
		Оповещение = Новый ОписаниеОповещения("ПредложениеЗагрузкиКлассификатораЗавершение", ЭтотОбъект, ПараметрыЗагрузки);
		ОткрытьФорму("Обработка.РасширенныйВводКонтактнойИнформации.Форма.ЗагрузкаАдресногоКлассификатора", ПараметрыФормы, ЭтотОбъект,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредложениеЗагрузкиКлассификатораЗавершение(Знач РезультатВопроса, Знач ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ЗагрузитьАдресныйКлассификатор(ДополнительныеПараметры);
КонецПроцедуры

// Загружает адресный классификатор.
//
&НаКлиенте
Процедура ЗагрузитьАдресныйКлассификатор(Знач ДополнительныеПараметры = Неопределено)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.АдресныйКлассификатор") Тогда
		МодульАдресныйКлассификаторКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("АдресныйКлассификаторКлиент");
		МодульАдресныйКлассификаторКлиент.ЗагрузитьАдресныйКлассификатор(ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

// Страны ЕАЭС поддерживающие структурированный ввод.
// 
&НаСервереБезКонтекста
Функция ЭтоСтранаУчастникЕАЭС(Страна)
	Возврат УправлениеКонтактнойИнформацией.ЭтоСтранаУчастникЕАЭС(Страна);
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НаименованиеПриДобавлениеПроизвольногоПомещения()
	Возврат НСтр("ru='Другое'");
КонецФункции

#КонецОбласти