#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Параметры.Объект) = Тип("ДанныеФормыСтруктура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.Объект);
		ПроверятьПоКлассификатору = ?(Параметры.Объект.ПроверятьПоФИАС, 0, 1);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОтобразитьДоступностьВариантовПроверкиПоКлассификатору();
	ИзменитьОтображениеПриИзмененииТолькоНациональный();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура АдресТолькоНациональныйПриИзменении(Элемент)
	
	ИзменитьРеквизитыПриИзмененииТолькоНациональный();
	ИзменитьОтображениеПриИзмененииТолькоНациональный();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверятьКорректностьАдресаПриИзменении(Элемент)
	ОтобразитьДоступностьВариантовПроверкиПоКлассификатору();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	Результат = ОписаниеНастроекАдреса();
	ЗаполнитьЗначенияСвойств(Результат, ЭтотОбъект);
	Результат.ПроверятьПоФИАС = ?(ПроверятьПоКлассификатору = 0, Истина, Ложь);
	Закрыть(Результат);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ОписаниеНастроекАдреса()
	Результат = Новый Структура();
	Результат.Вставить("ТолькоНациональныйАдрес");
	Результат.Вставить("ПроверятьКорректность");
	Результат.Вставить("ПроверятьПоФИАС");
	Результат.Вставить("СкрыватьНеактуальныеАдреса");
	Результат.Вставить("УказыватьОКТМО");
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура ОтобразитьДоступностьВариантовПроверкиПоКлассификатору()
	Если ПроверятьКорректность Тогда
		Элементы.ПроверятьПоКлассификатору.Доступность = Истина;
	Иначе
		Элементы.ПроверятьПоКлассификатору.Доступность = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьОтображениеПриИзмененииТолькоНациональный()
	
	Элементы.ПроверятьПоКлассификатору.Доступность   = ПроверятьКорректность;
	Элементы.ПроверятьКорректностьАдреса.Доступность = ТолькоНациональныйАдрес;
	Элементы.СкрыватьНеактуальныеАдреса.Доступность  = ТолькоНациональныйАдрес;
	Элементы.УказыватьОКТМОВручную.Доступность       = ТолькоНациональныйАдрес;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьРеквизитыПриИзмененииТолькоНациональный()
	
	Если Не ТолькоНациональныйАдрес Тогда
		ПроверятьКорректность      = Ложь;
		СкрыватьНеактуальныеАдреса = Ложь;
		УказыватьОКТМО = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти