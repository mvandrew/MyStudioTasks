
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВариантыОтчетов

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - УправляемаяФорма, Неопределено - Форма отчета или форма настроек отчета.
//       Неопределено когда вызов без контекста.
//   КлючВарианта - Строка, Неопределено - Имя предопределенного
//       или уникальный идентификатор пользовательского варианта отчета.
//       Неопределено когда вызов без контекста.
//   Настройки - Структура - см. возвращаемое значение
//       ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию().
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.РазрешеноВыбиратьИНастраиватьВариантыБезСохранения = Истина;
	Настройки.СкрытьКомандыРассылки                              = Истина;
	Настройки.ПараметрыРасположенияЭлементовУправления           = ОпределитьРасположениеЭлементовУправления();
	
	Настройки.События.ПриСозданииНаСервере               = Истина;
	Настройки.События.ПередЗагрузкойНастроекВКомпоновщик = Истина;
	Настройки.События.ПередЗагрузкойВариантаНаСервере    = Истина;
	Настройки.События.ПриОпределенииПараметровВыбора     = Истина;
	
	Настройки.ФормироватьСразу = Истина;
	
КонецПроцедуры

// Вызывается перед загрузкой новых настроек. Используется для изменения схемы компоновки.
//   Например, если схема отчета зависит от ключа варианта или параметров отчета.
//   Чтобы изменения схемы вступили в силу следует вызывать метод ОтчетыСервер.ПодключитьСхему().
//
// Параметры:
//   Контекст - Произвольный - 
//       Параметры контекста, в котором используется отчет.
//       Используется для передачи в параметрах метода ОтчетыСервер.ПодключитьСхему().
//   КлючСхемы - Строка -
//       Идентификатор текущей схемы компоновщика настроек.
//       По умолчанию не заполнен (это означает что компоновщик инициализирован на основании основной схемы).
//       Используется для оптимизации, чтобы переинициализировать компоновщик как можно реже.
//       Может не использоваться если переинициализация выполняется безусловно.
//   КлючВарианта - Строка, Неопределено -
//       Имя предопределенного или уникальный идентификатор пользовательского варианта отчета.
//       Неопределено когда вызов для варианта расшифровки или без контекста.
//   НовыеНастройкиКД - НастройкиКомпоновкиДанных, Неопределено -
//       Настройки варианта отчета, которые будут загружены в компоновщик настроек после его инициализации.
//       Неопределено когда настройки варианта не надо загружать (уже загружены ранее).
//   НовыеПользовательскиеНастройкиКД - ПользовательскиеНастройкиКомпоновкиДанных, Неопределено -
//       Пользовательские настройки, которые будут загружены в компоновщик настроек после его инициализации.
//       Неопределено когда пользовательские настройки не надо загружать (уже загружены ранее).
//
// Варианты вызова:
//   Если КлючСхемы <> "1" Тогда
//   	КлючСхемы = "1";
//   	СхемаКД = ПолучитьОбщийМакет("МояОбщаяСхемаКомпоновки");
//   	ОтчетыСервер.ПодключитьСхему(ЭтотОбъект, Контекст, СхемаКД, КлючСхемы);
//   КонецЕсли; - компоновщик отчета инициализируется на основании схемы из общих макетов.
//   Если ТипЗнч(НовыеПользовательскиеНастройкиКД) = Тип("ПользовательскиеНастройкиКомпоновкиДанных") Тогда
//   	ПолноеИмяОбъектаМетаданных = "";
//   	Для Каждого ЭлементКД Из НовыеПользовательскиеНастройкиКД.Элементы Цикл
//   		Если ТипЗнч(ЭлементКД) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
//   			ИмяПараметра = Строка(ЭлементКД.Параметр);
//   			Если ИмяПараметра = "ОбъектМетаданных" Тогда
//   				ПолноеИмяОбъектаМетаданных = ЭлементКД.Значение;
//   			КонецЕсли;
//   		КонецЕсли;
//   	КонецЦикла;
//   	Если КлючСхемы <> ПолноеИмяОбъектаМетаданных Тогда
//   		КлючСхемы = ПолноеИмяОбъектаМетаданных;
//   		СхемаКД = Новый СхемаКомпоновкиДанных;
//   		ОтчетыСервер.ПодключитьСхему(ЭтотОбъект, Контекст, СхемаКД, КлючСхемы);
//   	КонецЕсли;
//   КонецЕсли; - схема зависит от значения параметра, выведенного в пользовательские настройки отчета.
//
Процедура ПередЗагрузкойНастроекВКомпоновщик(Контекст, КлючСхемы, КлючВарианта, НовыеНастройкиКД,
	НовыеПользовательскиеНастройкиКД) Экспорт
	
	Если ТипЗнч(НовыеНастройкиКД) = Тип("НастройкиКомпоновкиДанных") Тогда
		НастройкиКД = НовыеНастройкиКД;
	Иначе
		НастройкиКД = КомпоновщикНастроек.Настройки;
	КонецЕсли;
	
	ПараметрыОтчета = ПараметрыОтчета(НастройкиКД, НовыеПользовательскиеНастройкиКД);
	
	Если ПараметрыОтчета.ДокументВладелец = Неопределено Тогда
		ВызватьИсключение НСтр("ru = 'Отчет о движениях документа не предназначен для рассылки.'");
	КонецЕсли;
	
	Если КлючСхемы <> КлючВарианта Тогда
		
		КлючСхемы = КлючВарианта;
		
		Если КлючВарианта = "Основной" Тогда
			ЗаполнитьСхемуКомпоновкиДанных_Горизонталь(ПараметрыОтчета.ДокументВладелец,
				СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
		ИначеЕсли КлючВарианта = "Дополнительный" Тогда
			ЗаполнитьСхемуКомпоновкиДанных_Вертикаль(ПараметрыОтчета.ДокументВладелец,
				СхемаКомпоновкиДанных.НастройкиПоУмолчанию, Истина);
		КонецЕсли;
		
		ОтчетыСервер.ПодключитьСхему(ЭтотОбъект, Контекст, СхемаКомпоновкиДанных, КлючСхемы);
		
		НастройкиКДПоУмолчаниюТиповойСхемы(СхемаКомпоновкиДанных, ПараметрыОтчета, НовыеНастройкиКД, КлючВарианта);
		
	КонецЕсли;
	
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   Форма - УправляемаяФорма - форма отчета.
//   Отказ - Булево - передается из параметров стандартного обработчика ПриСозданииНаСервере "как есть".
//   СтандартнаяОбработка - Булево - передается из параметров стандартного обработчика ПриСозданииНаСервере "как есть".
//
Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	Если Форма.Параметры.ПредставлениеВарианта = "Расшифровка" Тогда
		ВызватьИсключение НСтр("ru = 'Выбранное действие в данном отчете не доступно.'");
	КонецЕсли;
	
	Если Форма.Параметры.Свойство("ДокументВладелец") Тогда
		СтруктураПараметровДанных = Новый Структура("ДокументВладелец", Форма.Параметры.ДокументВладелец);
		УстановитьПараметрыДанных(КомпоновщикНастроек.Настройки, СтруктураПараметровДанных);
	ИначеЕсли Не Форма.Параметры.Свойство("ПараметрКоманды") Тогда
		ВызватьИсключение НСтр("ru = 'Для начала работы с отчетом необходимо воспользоваться соответствующей командой в форме интересующего документа.'");
	КонецЕсли;
	ДокументВладелец = Форма.Параметры.ПараметрКоманды;
	Если Не ЗначениеЗаполнено(ДокументВладелец) Тогда
		ВызватьИсключение НСтр("ru = 'Для начала работы с отчетом необходимо воспользоваться соответствующей командой в форме интересующего документа.'");
	КонецЕсли;
	
	СтруктураПараметровДанных = Новый Структура("ДокументВладелец", ДокументВладелец);
	УстановитьПараметрыДанных(КомпоновщикНастроек.Настройки, СтруктураПараметровДанных);
	Форма.КлючНазначенияИспользования = ДокументВладелец.Метаданные().ПолноеИмя();
	
КонецПроцедуры

// Вызывается в форме отчета перед выводом настройки (см. ОтчетыПереопределяемый.ПриОпределенииПараметровВыбора).
//
// Параметры:
//   Форма - УправляемаяФорма, Неопределено - форма отчета.
//   СвойстваНастройки - Структура - Описание настройки отчета, которая будет выведена в форме отчета.
//       * ОписаниеТипов - ОписаниеТипов - тип настройки.
//       * ЗначенияДляВыбора - СписокЗначений - объекты, которые будут предложены пользователю в списке выбора.
//                             Дополняет список объектов, уже выбранных пользователем ранее.
//       * ЗапросЗначенийВыбора - Запрос - возвращает объекты, которыми необходимо дополнить ЗначенияДляВыбора.
//                                Первой колонкой (с 0м индексом) должен выбираться объект,
//                                который следует добавить в ЗначенияДляВыбора.Значение.
//                                Для отключения автозаполнения в свойство ЗапросЗначенийВыбора.Текст
//                                следует записать пустую строку.
//       * ОграничиватьВыборУказаннымиЗначениями - Булево - когда Истина, то выбор пользователя будет
//                                                 ограничен значениями, указанными в 
//                                                 ЗначенияДляВыбора (его конечным состоянием).
//
Процедура ПриОпределенииПараметровВыбора(Форма, СвойстваНастройки) Экспорт
	
	ПолеИмяРегистра = Новый ПолеКомпоновкиДанных("ПараметрыДанных.СписокРегистров");
	Если СвойстваНастройки.ПолеКД = ПолеИмяРегистра Тогда
		
		Если КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Свойство("ГлобальныеНастройки") Тогда
			
			СвойстваНастройки.ОграничиватьВыборУказаннымиЗначениями = Истина;
			
			ГлобальныеНастройки = ПерехватитьГлобальныеНастройки();
			ЗначенияДляВыбора   = Новый СписокЗначений;
			
			Для Каждого ГлобальнаяНастройка Из ГлобальныеНастройки Цикл
				
				ТекущееЗначениеДляВыбора = "Регистр" + ГлобальнаяНастройка.ВидРегистра + "_" + ГлобальнаяНастройка.ИмяРегистра;
				
				ТекущееЗначениеДляВыбораПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Регистр %1 %2'"),
					НРег(ГлобальнаяНастройка.ЛокализацияВидРегистра), ГлобальнаяНастройка.ПредставлениеРегистра);
				
				ЗначенияДляВыбора.Добавить(ТекущееЗначениеДляВыбора, ТекущееЗначениеДляВыбораПредставление, Истина);
				
			КонецЦикла;
			
			СвойстваНастройки.ЗначенияДляВыбора = ЗначенияДляВыбора;
			СвойстваНастройки.Значение          = ЗначенияДляВыбора;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
// Более подробное описание можно найти в синтакс-помощнике, а именно, в разделе расширений
// управляемой формы для отчета.
//
// Параметры:
//   Форма - УправляемаяФорма - форма отчета.
//   НовыеНастройкиКД - НастройкиКомпоновкиДанных - настройки для загрузки в компоновщик настроек.
//
Процедура ПередЗагрузкойВариантаНаСервере(Форма, НовыеНастройкиКД) Экспорт
	
	ПараметрСКД         = Новый ПараметрКомпоновкиДанных("ДокументВладелец");
	ДокументПараметрСКД = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти(ПараметрСКД);
	
	Если ДокументПараметрСКД <> Неопределено Тогда
		ДокументПараметр = ДокументПараметрСКД.Значение;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДокументПараметр) Тогда
		
		УстановитьПараметрыДанных(НовыеНастройкиКД, Новый Структура("ДокументВладелец", ДокументПараметр));
		
		Если Форма.КлючТекущегоВарианта = Форма.Отчет.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.КлючВарианта Тогда
			
			СкопироватьНастройкиКомпоновкиДанных(НовыеНастройкиКД, КомпоновщикНастроек.Настройки);
			
			Если Форма.КлючТекущегоВарианта = "Основной" Тогда
				ЗаполнитьСхемуКомпоновкиДанных_Горизонталь(ДокументПараметр, СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
			ИначеЕсли Форма.КлючТекущегоВарианта = "Дополнительный" Тогда
				ЗаполнитьСхемуКомпоновкиДанных_Вертикаль(ДокументПараметр, СхемаКомпоновкиДанных.НастройкиПоУмолчанию, Истина);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВариантыОтчетов

#КонецОбласти

#КонецОбласти

#Область ПрограммноеФормированиеСКД

#Область ПодготовкаДанных

Функция ПодготовкаДанных(ДвиженияДокумента, ТаблицаРегистров, ДополнительнаяНумерация = Ложь)
	
	ОсновнаяИнформация = Новый Массив;
	
	Для Каждого СтрокаТаблицыРегистров Из ТаблицаРегистров Цикл
	
		ИмяРегистра            = СокрЛП(СтрокаТаблицыРегистров.ИмяРегистра);
		ВидРегистра            = СокрЛП(СтрокаТаблицыРегистров.ВидРегистра);
		ЛокализацияВидРегистра = СокрЛП(СтрокаТаблицыРегистров.ЛокализацияВидРегистра);
		ПредставлениеРегистра  = СокрЛП(СтрокаТаблицыРегистров.ПредставлениеРегистра);
	
		Для Каждого ДвижениеДокумента Из ДвиженияДокумента Цикл
	
			Если ВРег(СокрЛП(ДвижениеДокумента.Имя)) = ВРег(ИмяРегистра) Тогда
	
				Если ВидРегистра = "Накопления" Или ВидРегистра = "Сведений" Тогда
	
					ПодготовкаДанныхПоРегистрамНакопленияСведений(ДвижениеДокумента, ОсновнаяИнформация, ИмяРегистра, ВидРегистра,
						ЛокализацияВидРегистра, ПредставлениеРегистра, ДополнительнаяНумерация);
	
				ИначеЕсли ВидРегистра = "Бухгалтерии" Тогда
	
					ПодготовкаДанныхПоРегистрамБухгалтерии(ДвижениеДокумента, ОсновнаяИнформация, ИмяРегистра, ВидРегистра,
						ЛокализацияВидРегистра, ПредставлениеРегистра);
	
				ИначеЕсли ВидРегистра = "Расчета" Тогда
	
					ПодготовкаДанныхПоРегистрамРасчета(ДвижениеДокумента, ОсновнаяИнформация, ИмяРегистра, ВидРегистра,
						ЛокализацияВидРегистра, ПредставлениеРегистра, ДополнительнаяНумерация);
	
				КонецЕсли;
	
			КонецЕсли;
	
		КонецЦикла;
	
	КонецЦикла;
	
	Возврат ОсновнаяИнформация;
	
КонецФункции

Процедура ПодготовкаДанныхПоРегистрамНакопленияСведений(ДвижениеДокумента, ОсновнаяИнформация, ИмяРегистра,
	ВидРегистра, ЛокализацияВидРегистра, ПредставлениеРегистра, ДополнительнаяНумерация = Ложь)
	
	ИнформацияПоРегистру = Новый Структура;
	ИнформацияПоРегистру.Вставить("Измерения",              Новый Структура);
	ИнформацияПоРегистру.Вставить("Ресурсы",                Новый Структура);
	ИнформацияПоРегистру.Вставить("Реквизиты",              Новый Структура);
	ИнформацияПоРегистру.Вставить("СтандартныеРеквизиты",   Новый Структура);
	ИнформацияПоРегистру.Вставить("ИмяРегистра",            ИмяРегистра);
	ИнформацияПоРегистру.Вставить("ВидРегистра",            ВидРегистра);
	ИнформацияПоРегистру.Вставить("ЛокализацияВидРегистра", ЛокализацияВидРегистра);
	ИнформацияПоРегистру.Вставить("ПредставлениеРегистра",  ПредставлениеРегистра);
	Если ДополнительнаяНумерация Тогда
		ИнформацияПоРегистру.Вставить("Нумератор", Новый Структура);
	КонецЕсли;
	
	ИнформацияПоРегистру.СтандартныеРеквизиты = СформироватьСтруктуруПолей(ДвижениеДокумента.СтандартныеРеквизиты,
		"НомерСтроки, Регистратор");
	ИнформацияПоРегистру.Измерения            = СформироватьСтруктуруПолей(ДвижениеДокумента.Измерения);
	ИнформацияПоРегистру.Ресурсы              = СформироватьСтруктуруПолей(ДвижениеДокумента.Ресурсы);
	ИнформацияПоРегистру.Реквизиты            = СформироватьСтруктуруПолей(ДвижениеДокумента.Реквизиты);
	
	ПоляВыбора = "";
	ДобавитьПоля(ПоляВыбора, ИнформацияПоРегистру.СтандартныеРеквизиты);
	ДобавитьПоля(ПоляВыбора, ИнформацияПоРегистру.Измерения);
	ДобавитьПоля(ПоляВыбора, ИнформацияПоРегистру.Ресурсы);
	ДобавитьПоля(ПоляВыбора, ИнформацияПоРегистру.Реквизиты);
	
	Если ДополнительнаяНумерация Тогда
	
		МаксимумПолей = Макс(ИнформацияПоРегистру.СтандартныеРеквизиты.Количество(),
			ИнформацияПоРегистру.Измерения.Количество(),
			ИнформацияПоРегистру.Ресурсы.Количество(),
			ИнформацияПоРегистру.Реквизиты.Количество());
			
			ИнформацияПоРегистру.Нумератор = СформироватьНумератор(МаксимумПолей);
			
			ДобавитьНумератор(ПоляВыбора, ИнформацияПоРегистру.Нумератор);
	
	КонецЕсли;
	
	Если СокрЛП(Прав(ПоляВыбора, 2)) = "," Тогда
		ПоляВыбора = Лев(ПоляВыбора, СтрДлина(ПоляВыбора) - 2);
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	1 КАК КоличествоЗаписейРегистра,
	|	""&ИмяРегистра"" КАК ИмяРегистра,
	|	&Поля
	|ИЗ
	|	&ТекущаяТаблица КАК ТекущаяТаблица
	|ГДЕ
	|	&УсловиеЗапроса
	|{ГДЕ
	|	(&УсловиеКомпоновки)}";
	
	ПолноеИмяРегистра = "Регистр" + ВидРегистра + "_" + ИмяРегистра;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяРегистра", ПолноеИмяРегистра);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Поля", ПоляВыбора);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекущаяТаблица", ДвижениеДокумента.ПолноеИмя());
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеЗапроса", "Регистратор = &ДокументВладелец");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеКомпоновки", """" + ПолноеИмяРегистра + """ В (&СписокРегистров)");
	
	ИнформацияДляФормированияНабораДанных = Новый Структура;
	ИнформацияДляФормированияНабораДанных.Вставить("ТекстЗапроса",         ТекстЗапроса);
	ИнформацияДляФормированияНабораДанных.Вставить("ИнформацияПоРегистру", ИнформацияПоРегистру);
	
	ОсновнаяИнформация.Добавить(ИнформацияДляФормированияНабораДанных);
	
КонецПроцедуры

Процедура ПодготовкаДанныхПоРегистрамБухгалтерии(ДвижениеДокумента, ОсновнаяИнформация, ИмяРегистра, ВидРегистра, ЛокализацияВидРегистра, ПредставлениеРегистра)
	
	ИнформацияПоРегистру = Новый Структура;
	ИнформацияПоРегистру.Вставить("СтандартныеРеквизиты",   Новый Структура);
	ИнформацияПоРегистру.Вставить("Измерения",              Новый Структура);
	ИнформацияПоРегистру.Вставить("Ресурсы",                Новый Структура);
	ИнформацияПоРегистру.Вставить("РесурсыДт",              Новый Структура);
	ИнформацияПоРегистру.Вставить("РесурсыКт",              Новый Структура);
	ИнформацияПоРегистру.Вставить("Субконто",               Новый Структура);
	ИнформацияПоРегистру.Вставить("СубконтоДт",             Новый Структура);
	ИнформацияПоРегистру.Вставить("СубконтоКт",             Новый Структура);
	ИнформацияПоРегистру.Вставить("ИзмеренияДт",            Новый Структура);
	ИнформацияПоРегистру.Вставить("ИзмеренияКт",            Новый Структура);
	ИнформацияПоРегистру.Вставить("Реквизиты",              Новый Структура);
	ИнформацияПоРегистру.Вставить("ИмяРегистра",            ИмяРегистра);
	ИнформацияПоРегистру.Вставить("ВидРегистра",            ВидРегистра);
	ИнформацияПоРегистру.Вставить("ЛокализацияВидРегистра", ЛокализацияВидРегистра);
	ИнформацияПоРегистру.Вставить("ПредставлениеРегистра",  ПредставлениеРегистра);
	
	МаксимальноеКоличествоСубконто = ДвижениеДокумента.ПланСчетов.МаксКоличествоСубконто;
	КорреспонденцияОбъекта         = ДвижениеДокумента.Корреспонденция;
	
	ЛокализацияДебет    = НСтр("ru = 'Дт'");
	ЛокализацияКредит   = НСтр("ru = 'Кт'");
	ЛокализацияСубконто = НСтр("ru = 'Субконто'");
	ЛокализацияСчет     = НСтр("ru = 'Счет'");
	
	СтандартныеРеквизитыИсключения = "НомерСтроки, Регистратор, Счет";
	Для ИндексСубконто = 1 По МаксимальноеКоличествоСубконто Цикл
		ИндексСубконтоСтрокой =  Формат(ИндексСубконто, "ЧГ=0");
		СтандартныеРеквизитыИсключения = СтандартныеРеквизитыИсключения 
			+ ", " + "ВидСубконто" + ИндексСубконтоСтрокой + ", Субконто" + ИндексСубконтоСтрокой;
	КонецЦикла;
	
	ИнформацияПоРегистру.СтандартныеРеквизиты = СформироватьСтруктуруПолей(ДвижениеДокумента.СтандартныеРеквизиты,
		СтандартныеРеквизитыИсключения);
	ИнформацияПоРегистру.Реквизиты            = СформироватьСтруктуруПолей(ДвижениеДокумента.Реквизиты,
		СтандартныеРеквизитыИсключения);
	
	Ресурсы = ДвижениеДокумента.Ресурсы;
	Для Каждого Ресурс Из Ресурсы Цикл
	
		Если Ресурс.Балансовый Или Не КорреспонденцияОбъекта Тогда
	
			ИнформацияПоРегистру.Ресурсы.Вставить(Ресурс.Имя, Ресурс.Представление());
	
		Иначе
	
			ИнформацияПоРегистру.РесурсыДт.Вставить(Ресурс.Имя + "Дт", Ресурс.Представление() + " " + ЛокализацияДебет);
			ИнформацияПоРегистру.РесурсыКт.Вставить(Ресурс.Имя + "Кт", Ресурс.Представление() + " " + ЛокализацияКредит);
	
		КонецЕсли;
	
	КонецЦикла;
	
	Для ИндексСубконто = 1 По МаксимальноеКоличествоСубконто Цикл
	
		Если КорреспонденцияОбъекта Тогда
	
			ИндексСтрокой = Формат(ИндексСубконто, "ЧГ=0");
			
			ИнформацияПоРегистру.СубконтоДт.Вставить("СубконтоДт" + ИндексСтрокой,
				ЛокализацияСубконто + " " + ЛокализацияДебет + " " + ИндексСтрокой);
			ИнформацияПоРегистру.СубконтоКт.Вставить("СубконтоКт" + ИндексСтрокой,
				ЛокализацияСубконто + " " + ЛокализацияКредит + " " + ИндексСтрокой);
	
		Иначе
	
			ИнформацияПоРегистру.Субконто.Вставить("Субконто" + ИндексСтрокой, ЛокализацияСубконто + " " + ИндексСтрокой);
	
		КонецЕсли;
		
	КонецЦикла;
	
	Если КорреспонденцияОбъекта Тогда
		
		ИнформацияПоРегистру.ИзмеренияДт.Вставить("СчетДт", ЛокализацияСчет + " " +ЛокализацияДебет);
		ИнформацияПоРегистру.ИзмеренияКт.Вставить("СчетКт", ЛокализацияСчет + " " + ЛокализацияКредит);
	
	Иначе
	
		ТекстИзмерения = ТекстИзмерения + ?(ЗначениеЗаполнено(ТекстИзмерения), ", ", "") + "Счет";
	
		ИнформацияПоРегистру.Измерения.Вставить("Счет", ЛокализацияСчет);
	
	КонецЕсли;
	
	Измерения = ДвижениеДокумента.Измерения;
	Для Каждого Измерение Из Измерения Цикл
	
		Если Измерение.Балансовый Или Не КорреспонденцияОбъекта Тогда
	
			ИнформацияПоРегистру.Измерения.Вставить(Измерение.Имя, Измерение.Представление());
	
		Иначе
	
			ИнформацияПоРегистру.ИзмеренияДт.Вставить(Измерение.Имя + "Дт", Измерение.Представление() + " " + ЛокализацияДебет);
			ИнформацияПоРегистру.ИзмеренияКт.Вставить(Измерение.Имя + "Кт", Измерение.Представление() + " " + ЛокализацияКредит);
	
		КонецЕсли;
	
	КонецЦикла;
	
	ПоляВыбора = "";
	ДобавитьПоля(ПоляВыбора, ИнформацияПоРегистру.СтандартныеРеквизиты);
	ДобавитьПоля(ПоляВыбора, ИнформацияПоРегистру.Реквизиты);
	ДобавитьПоля(ПоляВыбора, ИнформацияПоРегистру.Измерения);
	ДобавитьПоля(ПоляВыбора, ИнформацияПоРегистру.ИзмеренияДт);
	ДобавитьПоля(ПоляВыбора, ИнформацияПоРегистру.ИзмеренияКт);
	ДобавитьПоля(ПоляВыбора, ИнформацияПоРегистру.Ресурсы);
	ДобавитьПоля(ПоляВыбора, ИнформацияПоРегистру.РесурсыДт);
	ДобавитьПоля(ПоляВыбора, ИнформацияПоРегистру.РесурсыКт);
	ДобавитьПоля(ПоляВыбора, ИнформацияПоРегистру.Субконто);
	ДобавитьПоля(ПоляВыбора, ИнформацияПоРегистру.СубконтоДт);
	ДобавитьПоля(ПоляВыбора, ИнформацияПоРегистру.СубконтоКт);
	
	Если СокрЛП(Прав(ПоляВыбора, 2)) = "," Тогда
		ПоляВыбора = Лев(ПоляВыбора, СтрДлина(ПоляВыбора) - 2);
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	1 КАК КоличествоЗаписейРегистра,
	|	""&ИмяРегистра"" КАК ИмяРегистра,
	|	&Поля
	|ИЗ
	|	&ТекущаяТаблица КАК ТекущаяТаблица
	|ГДЕ
	|	&УсловиеЗапроса
	|{ГДЕ
	|	(&УсловиеКомпоновки)}";
	
	ПолноеИмяРегистра = "Регистр" + ВидРегистра + "_" + ИмяРегистра;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяРегистра", ПолноеИмяРегистра);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Поля", ПоляВыбора);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекущаяТаблица", "РегистрБухгалтерии." + ИмяРегистра + ".ДвиженияССубконто(, , Регистратор = &ДокументВладелец)");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеЗапроса", "Регистратор = &ДокументВладелец");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеКомпоновки", """" + ПолноеИмяРегистра + """ В (&СписокРегистров)");
	
	ИнформацияДляФормированияНабораДанных = Новый Структура;
	ИнформацияДляФормированияНабораДанных.Вставить("ТекстЗапроса",         ТекстЗапроса);
	ИнформацияДляФормированияНабораДанных.Вставить("ИнформацияПоРегистру", ИнформацияПоРегистру);
	
	ОсновнаяИнформация.Добавить(ИнформацияДляФормированияНабораДанных);
	
КонецПроцедуры

Процедура ПодготовкаДанныхПоРегистрамРасчета(ДвижениеДокумента, ОсновнаяИнформация, ИмяРегистра, ВидРегистра, ЛокализацияВидРегистра, ПредставлениеРегистра, ДополнительнаяНумерация = Ложь)
	
	ИнформацияПоРегистру = Новый Структура;
	ИнформацияПоРегистру.Вставить("Измерения",              Новый Структура);
	ИнформацияПоРегистру.Вставить("Ресурсы",                Новый Структура);
	ИнформацияПоРегистру.Вставить("Реквизиты",              Новый Структура);
	ИнформацияПоРегистру.Вставить("СтандартныеРеквизиты",   Новый Структура);
	ИнформацияПоРегистру.Вставить("ИмяРегистра",            ИмяРегистра);
	ИнформацияПоРегистру.Вставить("ВидРегистра",            ВидРегистра);
	ИнформацияПоРегистру.Вставить("ЛокализацияВидРегистра", ЛокализацияВидРегистра);
	ИнформацияПоРегистру.Вставить("ПредставлениеРегистра",  ПредставлениеРегистра);
	Если ДополнительнаяНумерация Тогда
		ИнформацияПоРегистру.Вставить("Нумератор", Новый Структура);
	КонецЕсли;
	
	Для Каждого Реквизит Из ДвижениеДокумента.СтандартныеРеквизиты Цикл
		ИнформацияПоРегистру.СтандартныеРеквизиты.Вставить(Реквизит.Имя, Реквизит.Представление());
	КонецЦикла;
	
	ИнформацияПоРегистру.Измерения                          = СформироватьСтруктуруПолей(ДвижениеДокумента.Измерения);
	ИнформацияПоРегистру.Ресурсы                            = СформироватьСтруктуруПолей(ДвижениеДокумента.Ресурсы);
	ИнформацияПоРегистру.Реквизиты                          = СформироватьСтруктуруПолей(ДвижениеДокумента.Реквизиты);
	
	ПоляВыбора = "";
	ДобавитьПоля(ПоляВыбора, ИнформацияПоРегистру.СтандартныеРеквизиты);
	ДобавитьПоля(ПоляВыбора, ИнформацияПоРегистру.Измерения);
	ДобавитьПоля(ПоляВыбора, ИнформацияПоРегистру.Ресурсы);
	ДобавитьПоля(ПоляВыбора, ИнформацияПоРегистру.Реквизиты);
	
	Если ДополнительнаяНумерация Тогда
		
		МаксимумПолей = Макс(ИнформацияПоРегистру.СтандартныеРеквизиты.Количество(),
			ИнформацияПоРегистру.Измерения.Количество(),
			ИнформацияПоРегистру.Ресурсы.Количество(),
			ИнформацияПоРегистру.Реквизиты.Количество());
		
		ИнформацияПоРегистру.Нумератор = СформироватьНумератор(МаксимумПолей);
		ДобавитьНумератор(ПоляВыбора, ИнформацияПоРегистру.Нумератор);
		
	КонецЕсли;
	
	Если СокрЛП(Прав(ПоляВыбора, 2)) = "," Тогда
		ПоляВыбора = Лев(ПоляВыбора, СтрДлина(ПоляВыбора) - 2);
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	1 КАК КоличествоЗаписейРегистра,
	|	""&ИмяРегистра"" КАК ИмяРегистра,
	|	&Поля
	|ИЗ
	|	&ТекущаяТаблица КАК ТекущаяТаблица
	|ГДЕ
	|	&УсловиеЗапроса
	|{ГДЕ
	|	(&УсловиеКомпоновки)}";
	
	ПолноеИмяРегистра = "Регистр" + ВидРегистра + "_" + ИмяРегистра;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяРегистра", ПолноеИмяРегистра);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Поля", ПоляВыбора);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекущаяТаблица", ДвижениеДокумента.ПолноеИмя());
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеЗапроса", "Регистратор = &ДокументВладелец");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеКомпоновки", """" + ПолноеИмяРегистра + """ В (&СписокРегистров)");
	
	ИнформацияДляФормированияНабораДанных = Новый Структура;
	ИнформацияДляФормированияНабораДанных.Вставить("ТекстЗапроса",         ТекстЗапроса);
	ИнформацияДляФормированияНабораДанных.Вставить("ИнформацияПоРегистру", ИнформацияПоРегистру);
	
	ОсновнаяИнформация.Добавить(ИнформацияДляФормированияНабораДанных);
	
КонецПроцедуры

Процедура ДобавитьНумератор(ТекстНумератора, СтруктураНумератора)
	
	Для Каждого Нумератор Из СтруктураНумератора Цикл
		ТекстНумератора = ТекстНумератора + ?(ЗначениеЗаполнено(ТекстНумератора), ", ", "") + Нумератор.Значение + " Как "
			+ Нумератор.Ключ;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбщееФормированиеНаборовДанных

#Область РегистрыБухгалтерии

Процедура НастройкаСтруктурыРегистровБухгалтерии(НастройкиКД, НаборДанныхКД, ЭлементДанных)
	
	ВидРегистра            = "Бухгалтерии";
	ЛокализацияВидРегистра = НСтр("ru = 'Бухгалтерии'");
	ИмяРегистра            = ЭлементДанных.ИмяРегистра;
	ПредставлениеРегистра  = ЭлементДанных.ПредставлениеРегистра;
	
	СтруктураСтандартныхРеквизитов = ЭлементДанных.СтандартныеРеквизиты;
	СтруктураИзмерений             = ЭлементДанных.Измерения;
	СтруктураИзмеренийДт           = ЭлементДанных.ИзмеренияДт;
	СтруктураИзмеренийКт           = ЭлементДанных.ИзмеренияКт;
	СтруктураРесурсов              = ЭлементДанных.Ресурсы;
	СтруктураРесурсовДт            = ЭлементДанных.РесурсыДт;
	СтруктураРесурсовКт            = ЭлементДанных.РесурсыКт;
	СтруктураСубконто              = ЭлементДанных.Субконто;
	СтруктураСубконтоДт            = ЭлементДанных.СубконтоДт;
	СтруктураСубконтоКт            = ЭлементДанных.СубконтоКт;
	СтруктураРеквизитов            = ЭлементДанных.Реквизиты;
	
	ПользовательскийЗаголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Регистр %1 %2'"),
		НРег(ЛокализацияВидРегистра), ПредставлениеРегистра);
	
	ГруппировкаДетальныеЗаписи = ДобавитьЭлементСтруктуры(НастройкиКД,
		"Регистр" + ВидРегистра + "_" + ИмяРегистра, ПользовательскийЗаголовок);
	
	Если СтруктураСтандартныхРеквизитов.Количество() > 0 Тогда
		
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "СтандартныеРеквизиты");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Вертикально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Истина);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", "");
		
		РазместитьГруппуПолейСКД(СтруктураСтандартныхРеквизитов, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы);
	
	КонецЕсли;
	
	Если СтруктураИзмерений.Количество() > 0 Тогда
	
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "Измерения");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  Неопределено);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Истина);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", "");
		
		РазместитьГруппуПолейСКД(СтруктураИзмерений, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы);
	
	КонецЕсли;
	
	Если СтруктураИзмеренийДт.Количество() > 0 Тогда
	
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "ИзмеренияДт");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Вертикально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Истина);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", "");
		
		РазместитьГруппуПолейСКД(СтруктураИзмеренийДт, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы);
	
	КонецЕсли;
	
	Если СтруктураСубконто.Количество() > 0 Тогда
	
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "Субконто");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Вертикально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Истина);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", "");
		
		РазместитьГруппуПолейСКД(СтруктураСубконто, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы);
	
	КонецЕсли;
	
	Если СтруктураСубконтоДт.Количество() > 0 Тогда
	
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "СубконтоДт");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Вертикально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Истина);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", "");
		
		РазместитьГруппуПолейСКД(СтруктураСубконтоДт, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы);
	
	КонецЕсли;
	
	Если СтруктураРесурсовДт.Количество() > 0 Тогда
	
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "РесурсыДт");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Вертикально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Истина);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", "");
		
		РазместитьГруппуПолейСКД(СтруктураРесурсовДт, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы);
	
	КонецЕсли;
	
	Если СтруктураИзмеренийКт.Количество() > 0 Тогда
	
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "ИзмеренияКт");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Вертикально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Истина);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", "");
		
		РазместитьГруппуПолейСКД(СтруктураИзмеренийКт, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы);
	
	КонецЕсли;
	
	Если СтруктураСубконтоКт.Количество() > 0 Тогда
	
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "СубконтоКт");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Вертикально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Истина);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", "");
		
		РазместитьГруппуПолейСКД(СтруктураСубконтоКт, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы);
	
	КонецЕсли;
	
	Если СтруктураРесурсовКт.Количество() > 0 Тогда
	
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "РесурсыКт");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Вертикально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Истина);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", "");
		
		РазместитьГруппуПолейСКД(СтруктураРесурсовКт, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы);
	
	КонецЕсли;
	
	Если СтруктураРесурсов.Количество() > 0 Тогда
	
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "Ресурсы");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  Неопределено);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Истина);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", "");
		
		РазместитьГруппуПолейСКД(СтруктураРесурсов, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы);
	
	КонецЕсли;
	
	Если СтруктураРеквизитов.Количество() > 0 Тогда
	
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "Реквизиты");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  Неопределено);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Истина);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", "");
		
		РазместитьГруппуПолейСКД(СтруктураРеквизитов, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы);
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ФормированиеНаборовДанных_Горизонталь

#Область Общее

Функция ЗаполнитьСхемуКомпоновкиДанных_Горизонталь(ДокументОтбор, НастройкиКД)
	
	МетаданныеДокумента  = ДокументОтбор.Метаданные();
	ДвиженияДокумента    = МетаданныеДокумента.Движения;
	
	МассивИспользуемыхРегистров = МассивИспользуемыхРегистров(ДокументОтбор, ДвиженияДокумента);
	ЗафиксироватьГлобальныеНастройки(ДокументОтбор, МассивИспользуемыхРегистров);
	
	Если МассивИспользуемыхРегистров.Количество() = 0 Тогда
		Возврат СхемаКомпоновкиДанных;
	Иначе
		ТаблицаРегистров     = ТаблицаИспользуемыхРегистров(МассивИспользуемыхРегистров);
		ПодготовленныеДанные = ПодготовкаДанных(ДвиженияДокумента, ТаблицаРегистров);
	
		СтруктураПараметровДанных = Новый Структура("СписокРегистров", СформироватьСписокРегистров(ТаблицаРегистров));
		УстановитьПараметрыДанных(НастройкиКД, СтруктураПараметровДанных);
	
		Возврат ФормированиеСхемыКД_Горизонталь(ПодготовленныеДанные, НастройкиКД);
	КонецЕсли;
	
КонецФункции

Функция ФормированиеСхемыКД_Горизонталь(ПодготовленныеДанные, НастройкиКД)
	
	СхемаКД            = СхемаКомпоновкиДанных;
	ГлавноеОбъединение = СхемаКД.НаборыДанных["Main"];
	
	ГлобальныеНастройки = ПерехватитьГлобальныеНастройки();
	
	ИндексНабора = 0;
	Для Каждого ЭлементДанных Из ПодготовленныеДанные Цикл
	
		ИндексНабора         = ИндексНабора + 1;
		ИнформацияПоРегистру = ЭлементДанных.ИнформацияПоРегистру;
		
		НаборыДанных   = ГлавноеОбъединение.Элементы;
		ИмяНабора      = "ЗапросПоРегистру" + ИнформацияПоРегистру.ВидРегистра + "_" + ИнформацияПоРегистру.ИмяРегистра;
		НайденныйНабор = НаборыДанных.Найти(ИмяНабора);
	
		Если НайденныйНабор = Неопределено Тогда
	
			НайденныйНабор                = НаборыДанных.Добавить(Тип("НаборДанныхЗапросСхемыКомпоновкиДанных"));
			НайденныйНабор.Имя            = ИмяНабора;
			НайденныйНабор.ИсточникДанных = "ИсточникДанных1";
	
			НайденныйНабор.Запрос = ЭлементДанных.ТекстЗапроса;
	
			ДоопределитьМакетСКД(СхемаКД, ИндексНабора, ИнформацияПоРегистру.ВидРегистра,
				ИнформацияПоРегистру.ЛокализацияВидРегистра, ИнформацияПоРегистру.ИмяРегистра, ГлобальныеНастройки);
	
		КонецЕсли;
	
		НастройкаСтруктурыНабора_Горизонталь(НастройкиКД, ЭлементДанных.ИнформацияПоРегистру, НайденныйНабор);
		
	КонецЦикла;
	
	Возврат СхемаКД;
	
КонецФункции

Процедура НастройкаСтруктурыНабора_Горизонталь(НастройкиКД, ЭлементДанных, НаборДанныхКД)
	
	Если ЭлементДанных.ВидРегистра = "Накопления" Или ЭлементДанных.ВидРегистра = "Сведений" Тогда
	
		НастройкаСтруктурыРегистровНакопленияИСведений_Горизонталь(НастройкиКД, НаборДанныхКД, ЭлементДанных);
	
	ИначеЕсли ЭлементДанных.ВидРегистра = "Бухгалтерии" Тогда
	
		НастройкаСтруктурыРегистровБухгалтерии(НастройкиКД, НаборДанныхКД, ЭлементДанных);
	
	ИначеЕсли ЭлементДанных.ВидРегистра = "Расчета" Тогда
	
		НастройкаСтруктурыРегистровРасчета_Горизонталь(НастройкиКД, НаборДанныхКД, ЭлементДанных);
	
	КонецЕсли;
	
	ОформитьОтчет(НастройкиКД);
	
КонецПроцедуры

#КонецОбласти

#Область РегистрыНакопленияИСведений

Процедура НастройкаСтруктурыРегистровНакопленияИСведений_Горизонталь(НастройкиКД, НаборДанныхКД, ЭлементДанных)
	
	ВидРегистра            = ЭлементДанных.ВидРегистра;
	ЛокализацияВидРегистра = ЭлементДанных.ЛокализацияВидРегистра;
	ИмяРегистра            = ЭлементДанных.ИмяРегистра;
	ПредставлениеРегистра  = ЭлементДанных.ПредставлениеРегистра;
	
	СтруктураСтандартныхРеквизитов = ЭлементДанных.СтандартныеРеквизиты;
	СтруктураИзмерений             = ЭлементДанных.Измерения;
	СтруктураРесурсов              = ЭлементДанных.Ресурсы;
	СтруктураРеквизитов            = ЭлементДанных.Реквизиты;
	
	ПользовательскийЗаголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Регистр %1 %2'"),
		НРег(ЛокализацияВидРегистра), ПредставлениеРегистра);
	
	ГруппировкаДетальныеЗаписи = 
		ДобавитьЭлементСтруктуры(НастройкиКД, "Регистр" + ВидРегистра + "_" + ИмяРегистра, ПользовательскийЗаголовок);
	
	Если СтруктураСтандартныхРеквизитов.Количество() > 0 Тогда
		
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "СтандартныеРеквизиты");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  Неопределено);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Ложь);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", НСтр("ru = 'Стандартные реквизиты'"));
		
		РазместитьГруппуПолейСКД(СтруктураСтандартныхРеквизитов, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы);
	
	КонецЕсли;
	
	Если СтруктураИзмерений.Количество() > 0 Тогда
		
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "Измерения");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  Неопределено);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Ложь);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", НСтр("ru = 'Измерения'"));
		
		РазместитьГруппуПолейСКД(СтруктураИзмерений, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы);
	
	КонецЕсли;
	
	Если СтруктураРесурсов.Количество() > 0 Тогда
		
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "Ресурсы");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  Неопределено);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Ложь);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", НСтр("ru = 'Ресурсы'"));
		
		РазместитьГруппуПолейСКД(СтруктураРесурсов, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы);
	
	КонецЕсли;
	
	Если СтруктураРеквизитов.Количество() > 0 Тогда
		
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "Реквизиты");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  Неопределено);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Ложь);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", НСтр("ru = 'Реквизиты'"));
		
		РазместитьГруппуПолейСКД(СтруктураРеквизитов, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы);
	
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область РегистрыРасчета

Процедура НастройкаСтруктурыРегистровРасчета_Горизонталь(НастройкиКД, НаборДанныхКД, ЭлементДанных)
	
	ВидРегистра            = "Расчета";
	ЛокализацияВидРегистра = НСтр("ru = 'Расчета'");
	ИмяРегистра            = ЭлементДанных.ИмяРегистра;
	ПредставлениеРегистра  = ЭлементДанных.ПредставлениеРегистра;
	
	СтруктураСтандартныхРеквизитов = ЭлементДанных.СтандартныеРеквизиты;
	СтруктураИзмерений             = ЭлементДанных.Измерения;
	СтруктураРесурсов              = ЭлементДанных.Ресурсы;
	СтруктураРеквизитов            = ЭлементДанных.Реквизиты;
	
	ПользовательскийЗаголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Регистр %1 %2'"),
		НРег(ЛокализацияВидРегистра), ПредставлениеРегистра);
	
	ГруппировкаДетальныеЗаписи = ДобавитьЭлементСтруктуры(НастройкиКД,
		"Регистр" + ВидРегистра + "_" + ИмяРегистра, ПользовательскийЗаголовок);
	
	Если СтруктураСтандартныхРеквизитов.Количество() > 0 Тогда
		
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "СтандартныеРеквизиты");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Горизонтально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Ложь);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", "");
		
		РазместитьГруппуПолейСКД(СтруктураСтандартныхРеквизитов, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы);
	
	КонецЕсли;
	
	Если СтруктураИзмерений.Количество() > 0 Тогда
		
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "Измерения");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  Неопределено);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Ложь);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", НСтр("ru = 'Измерения'"));
		
		РазместитьГруппуПолейСКД(СтруктураИзмерений, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы);
		
	КонецЕсли;
	
	Если СтруктураРесурсов.Количество() > 0 Тогда
		
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "Ресурсы");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  Неопределено);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Ложь);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", НСтр("ru = 'Ресурсы'"));
		
		РазместитьГруппуПолейСКД(СтруктураРесурсов, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы);
		
	КонецЕсли;
	
	Если СтруктураРеквизитов.Количество() > 0 Тогда
		
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "Реквизиты");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  Неопределено);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Ложь);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", НСтр("ru = 'Реквизиты'"));
		
		РазместитьГруппуПолейСКД(СтруктураРеквизитов, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ФормированиеНаборовДанных_Вертикаль

#Область Общее

Функция ЗаполнитьСхемуКомпоновкиДанных_Вертикаль(ДокументОтбор, НастройкиКД, ДополнительнаяНумерация = Ложь)
	
	МетаданныеДокумента  = ДокументОтбор.Метаданные();
	ДвиженияДокумента    = МетаданныеДокумента.Движения;
	
	МассивИспользуемыхРегистров      = МассивИспользуемыхРегистров(ДокументОтбор, ДвиженияДокумента);
	ЗафиксироватьГлобальныеНастройки(ДокументОтбор, МассивИспользуемыхРегистров);
	
	Если МассивИспользуемыхРегистров.Количество() = 0 Тогда
		Возврат СхемаКомпоновкиДанных;
	Иначе
		ТаблицаРегистров     = ТаблицаИспользуемыхРегистров(МассивИспользуемыхРегистров);
		ПодготовленныеДанные = ПодготовкаДанных(ДвиженияДокумента, ТаблицаРегистров, ДополнительнаяНумерация);
		
		СтруктураПараметровДанных = Новый Структура("СписокРегистров", СформироватьСписокРегистров(ТаблицаРегистров));
		УстановитьПараметрыДанных(НастройкиКД, СтруктураПараметровДанных);
	
		Возврат ФормированиеСхемыКД_Вертикаль(ПодготовленныеДанные, НастройкиКД);
	КонецЕсли;
	
КонецФункции

Функция ФормированиеСхемыКД_Вертикаль(ПодготовленныеДанные, НастройкиКД)
	
	СхемаКД            = СхемаКомпоновкиДанных;
	ГлавноеОбъединение = СхемаКД.НаборыДанных["Main"];
	
	ГлобальныеНастройки = ПерехватитьГлобальныеНастройки();
	
	ИндексНабора = 0;
	Для Каждого ЭлементДанных Из ПодготовленныеДанные Цикл
	
		ИндексНабора         = ИндексНабора + 1;
		ИнформацияПоРегистру = ЭлементДанных.ИнформацияПоРегистру;
	
		НаборыДанных   = ГлавноеОбъединение.Элементы;
		ИмяНабора      = "ЗапросПоРегистру" + ИнформацияПоРегистру.ВидРегистра + "_" + ИнформацияПоРегистру.ИмяРегистра;
		НайденныйНабор = НаборыДанных.Найти(ИмяНабора);
	
		Если НайденныйНабор = Неопределено Тогда
	
			НайденныйНабор                = НаборыДанных.Добавить(Тип("НаборДанныхЗапросСхемыКомпоновкиДанных"));
			НайденныйНабор.Имя            = ИмяНабора;
			НайденныйНабор.ИсточникДанных = "ИсточникДанных1";
	
			НайденныйНабор.Запрос = ЭлементДанных.ТекстЗапроса;
	
			ДоопределитьМакетСКД(СхемаКД, ИндексНабора, ИнформацияПоРегистру.ВидРегистра,
				ИнформацияПоРегистру.ЛокализацияВидРегистра, ИнформацияПоРегистру.ИмяРегистра, ГлобальныеНастройки);
	
		КонецЕсли;
	
		НастройкаСтруктурыНабора_Вертикаль(НастройкиКД, ЭлементДанных.ИнформацияПоРегистру, НайденныйНабор);
	
	КонецЦикла;
	
	Возврат СхемаКД;
	
КонецФункции

Процедура НастройкаСтруктурыНабора_Вертикаль(НастройкиКД, ЭлементДанных, НаборДанныхКД)
	
	Если ЭлементДанных.ВидРегистра = "Накопления" Или ЭлементДанных.ВидРегистра = "Сведений" Тогда
	
		НастройкаСтруктурыРегистровНакопленияИСведений_Вертикаль(НастройкиКД, НаборДанныхКД, ЭлементДанных);
	
	ИначеЕсли ЭлементДанных.ВидРегистра = "Бухгалтерии" Тогда
	
		НастройкаСтруктурыРегистровБухгалтерии(НастройкиКД, НаборДанныхКД, ЭлементДанных);
	
	ИначеЕсли ЭлементДанных.ВидРегистра = "Расчета" Тогда
	
		НастройкаСтруктурыРегистровРасчета_Вертикаль(НастройкиКД, НаборДанныхКД, ЭлементДанных);
	
	КонецЕсли;
	
	ОформитьОтчет(НастройкиКД);
	
КонецПроцедуры

#КонецОбласти

#Область РегистрыНакопленияИСведений

Процедура НастройкаСтруктурыРегистровНакопленияИСведений_Вертикаль(НастройкиКД, НаборДанныхКД, ЭлементДанных)
	
	ВидРегистра            = ЭлементДанных.ВидРегистра;
	ЛокализацияВидРегистра = ЭлементДанных.ЛокализацияВидРегистра;
	ИмяРегистра            = ЭлементДанных.ИмяРегистра;
	ПредставлениеРегистра  = ЭлементДанных.ПредставлениеРегистра;
	
	СтруктураСтандартныхРеквизитов = ЭлементДанных.СтандартныеРеквизиты;
	СтруктураИзмерений             = ЭлементДанных.Измерения;
	СтруктураРесурсов              = ЭлементДанных.Ресурсы;
	СтруктураРеквизитов            = ЭлементДанных.Реквизиты;
	СтруктураНумератора            = ЭлементДанных.Нумератор; 
	
	ПользовательскийЗаголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Регистр %1 %2'"),
		НРег(ЛокализацияВидРегистра), ПредставлениеРегистра);
	
	ГруппировкаДетальныеЗаписи = ДобавитьЭлементСтруктуры(НастройкиКД, "Регистр" + ВидРегистра + "_" + ИмяРегистра,
		ПользовательскийЗаголовок);
	
	Если СтруктураНумератора.Количество() > 0 Тогда
	
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "Нумератор");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Вертикально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Ложь);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", "№ГруппаЭлементаВыводаСКДТекущегоДокумента");
		
		РазместитьГруппуПолейСКД(СтруктураНумератора, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы);
	
	КонецЕсли;
	
	Если СтруктураСтандартныхРеквизитов.Количество() > 0 Тогда
	
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "СтандартныеРеквизиты");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Вертикально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Ложь);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", НСтр("ru = 'Стандартные реквизиты'"));
		
		РазместитьГруппуПолейСКД(СтруктураСтандартныхРеквизитов, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы);
	
	КонецЕсли;
	
	Если СтруктураИзмерений.Количество() > 0 Тогда
	
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "Измерения");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Вертикально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Ложь);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", НСтр("ru = 'Измерения'"));
		
		РазместитьГруппуПолейСКД(СтруктураИзмерений, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы);
	
	КонецЕсли;
	
	Если СтруктураРесурсов.Количество() > 0 Тогда
	
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "Ресурсы");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Вертикально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Ложь);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", НСтр("ru = 'Ресурсы'"));
		
		РазместитьГруппуПолейСКД(СтруктураРесурсов, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы);
	
	КонецЕсли;
	
	Если СтруктураРеквизитов.Количество() > 0 Тогда
	
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "Реквизиты");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Вертикально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Ложь);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", НСтр("ru = 'Реквизиты'"));
		
		РазместитьГруппуПолейСКД(СтруктураРеквизитов, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы);
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РегистрыРасчета

Процедура НастройкаСтруктурыРегистровРасчета_Вертикаль(НастройкиКД, НаборДанныхКД, ЭлементДанных)
	
	ВидРегистра            = "Расчета";
	ЛокализацияВидРегистра = НСтр("ru = 'Расчета'");
	ИмяРегистра            = ЭлементДанных.ИмяРегистра;
	ПредставлениеРегистра  = ЭлементДанных.ПредставлениеРегистра;
	
	СтруктураСтандартныхРеквизитов = ЭлементДанных.СтандартныеРеквизиты;
	СтруктураИзмерений             = ЭлементДанных.Измерения;
	СтруктураРесурсов              = ЭлементДанных.Ресурсы;
	СтруктураРеквизитов            = ЭлементДанных.Реквизиты;
	СтруктураНумератора            = ЭлементДанных.Нумератор;
	
	ПользовательскийЗаголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Регистр %1 %2'"),
		НРег(ЛокализацияВидРегистра), ПредставлениеРегистра);
	
	ГруппировкаДетальныеЗаписи = ДобавитьЭлементСтруктуры(НастройкиКД, "Регистр" + ВидРегистра + "_" + ИмяРегистра,
		ПользовательскийЗаголовок);
	
	Если СтруктураНумератора.Количество() > 0 Тогда
		
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "Нумератор");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Вертикально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Ложь);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", "№ГруппаЭлементаВыводаСКДТекущегоДокумента");
		
		РазместитьГруппуПолейСКД(СтруктураНумератора, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы);
		
	КонецЕсли;
	
	Если СтруктураСтандартныхРеквизитов.Количество() > 0 Тогда
		
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "СтандартныеРеквизиты");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Вертикально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Ложь);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", НСтр("ru = 'Стандартные реквизиты'"));
		
		РазместитьГруппуПолейСКД(СтруктураСтандартныхРеквизитов, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы);
		
	КонецЕсли;
	
	Если СтруктураИзмерений.Количество() > 0 Тогда
		
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "Измерения");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Вертикально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Ложь);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", НСтр("ru = 'Измерения'"));
		
		РазместитьГруппуПолейСКД(СтруктураИзмерений, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы);
		
	КонецЕсли;
	
	Если СтруктураРесурсов.Количество() > 0 Тогда
		
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "Ресурсы");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Вертикально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Ложь);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", НСтр("ru = 'Ресурсы'"));
		
		РазместитьГруппуПолейСКД(СтруктураРесурсов, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы);
		
	КонецЕсли;
	
	Если СтруктураРеквизитов.Количество() > 0 Тогда
		
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "Реквизиты");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Вертикально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Ложь);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", НСтр("ru = 'Реквизиты'"));
		
		РазместитьГруппуПолейСКД(СтруктураРеквизитов, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ФормированиеСтруктурыОтчета

#Область ПоляНабораДанных

Функция ДобавитьПолеНабораДанных(НаборДанных, Поле, Заголовок, ПутьКДанным = Неопределено)
	
	Если ПутьКДанным = Неопределено Тогда
		ПутьКДанным = Поле;
	КонецЕсли;
	
	ПолеНабораДанных             = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
	ПолеНабораДанных.Поле        = Поле;
	ПолеНабораДанных.Заголовок   = Заголовок;
	ПолеНабораДанных.ПутьКДанным = ПутьКДанным;
	Возврат ПолеНабораДанных;
	
КонецФункции

#КонецОбласти

#Область ВыбранныеПоляНабораДанных

Функция ДобавитьВыбранноеПоле(Куда, ИмяИлиПолеКД, Заголовок = "") Экспорт
	
	Если ТипЗнч(Куда) = Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
		ВыбранныеПоляКД = Куда.Настройки.Выбор;
	ИначеЕсли ТипЗнч(Куда) = Тип("НастройкиКомпоновкиДанных") Или ТипЗнч(Куда) = Тип("ГруппировкаКомпоновкиДанных") Тогда
		ВыбранныеПоляКД = Куда.Выбор;
	Иначе
		ВыбранныеПоляКД = Куда;
	КонецЕсли;
	
	Если ТипЗнч(ИмяИлиПолеКД) = Тип("Строка") Тогда
		ПолеКД = Новый ПолеКомпоновкиДанных(ИмяИлиПолеКД);
	Иначе
		ПолеКД = ИмяИлиПолеКД;
	КонецЕсли;
	
	ВыбранноеПолеКД      = ВыбранныеПоляКД.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
	ВыбранноеПолеКД.Поле = ПолеКД;
	
	Если Заголовок <> "" Тогда
		ВыбранноеПолеКД.Заголовок = Заголовок;
	КонецЕсли;
	
	Возврат ВыбранноеПолеКД;
	
КонецФункции

Функция ДобавитьВыбранноеПолеГруппа(Куда, ИмяИлиПолеКД, Заголовок = "", Расположение = Неопределено)
	
	Если ТипЗнч(Куда) = Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
		ВыбранныеПоляКД = Куда.Настройки.Выбор;
	ИначеЕсли ТипЗнч(Куда) = Тип("НастройкиКомпоновкиДанных") Или ТипЗнч(Куда) = Тип("ГруппировкаКомпоновкиДанных") Тогда
		ВыбранныеПоляКД = Куда.Выбор;
	Иначе
		ВыбранныеПоляКД = Куда;
	КонецЕсли;
	
	Если ТипЗнч(ИмяИлиПолеКД) = Тип("Строка") Тогда
		ПолеКД = Новый ПолеКомпоновкиДанных(ИмяИлиПолеКД);
	Иначе
		ПолеКД = ИмяИлиПолеКД;
	КонецЕсли;
	
	ВыбранноеПолеКД      = ВыбранныеПоляКД.Элементы.Добавить(Тип("ГруппаВыбранныхПолейКомпоновкиДанных"));
	ВыбранноеПолеКД.Поле = ПолеКД;
	
	Если Расположение <> Неопределено Тогда
		ВыбранноеПолеКД.Расположение = Расположение;
	КонецЕсли;	
	
	Если Заголовок <> "" Тогда
		ВыбранноеПолеКД.Заголовок = Заголовок;
	КонецЕсли;
	
	Возврат ВыбранноеПолеКД;
	
КонецФункции

#КонецОбласти

#Область Ресурс

Процедура ДобавитьРесурсРасчетаКоличестваЗаписей(СхемаКД)
	
	ПолеКоличестваЗаписей = СхемаКД.ПоляИтога.Найти("КоличествоЗаписейРегистра");
	Если ПолеКоличестваЗаписей = Неопределено Тогда
		ПолеИтога = СхемаКД.ПоляИтога.Добавить();
		ПолеИтога.Группировки.Добавить("ИмяРегистра");
		ПолеИтога.ПутьКДанным = "КоличествоЗаписейРегистра";
		ПолеИтога.Выражение   = "Сумма(КоличествоЗаписейРегистра)";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Процедура ДополнительнаяОбщаяНастройкаГруппировки(Группировка, СтруктураНастроек)

	Для Каждого ЭлементНастройки Из СтруктураНастроек Цикл
	
		Настройка = Группировка.ПараметрыВывода.Элементы.Найти(ЭлементНастройки.Ключ);
		Если Настройка <> Неопределено Тогда
			УстановитьПараметрВывода(Группировка, ЭлементНастройки.Ключ, ЭлементНастройки.Значение);
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

Функция ДобавитьЭлементСтруктуры(НастройкиКД, ИмяГруппировки, ПользовательскийЗаголовок)
	
	ГруппировкаПоРегистру = НастройкиКД.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	
	ГруппировкаПоРегистру.Имя                                    = ИмяГруппировки;
	ГруппировкаПоРегистру.ПредставлениеПользовательскойНастройки = ПользовательскийЗаголовок;
	ГруппировкаПоРегистру.Использование                          = Истина;
	
	ГруппировкаПоРегистру.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
	ГруппировкаПоРегистру.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
	
	ОформитьГруппировкуПоРегистру(ГруппировкаПоРегистру);
	
	ПолеГруппировкиРегистр = ГруппировкаПоРегистру.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
	
	ПолеГруппировкиРегистр.Использование  = Истина;
	ПолеГруппировкиРегистр.Поле           = Новый ПолеКомпоновкиДанных("ИмяРегистра");
	ПолеГруппировкиРегистр.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.Элементы;
	ПолеГруппировкиРегистр.ТипДополнения  = ТипДополненияПериодаКомпоновкиДанных.БезДополнения;
	
	ДетальныеЗаписи = ГруппировкаПоРегистру.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	ДетальныеЗаписи.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
	ДетальныеЗаписи.Использование = Истина;
	
	Возврат ДетальныеЗаписи;
	
КонецФункции

Процедура РазместитьГруппуПолейСКД(СтруктураПолей, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы)
	
	Если ПараметрыГруппы.ВыбраннаяГруппаПуста Тогда
		ЛокальноеИмяГруппы     = Новый ПолеКомпоновкиДанных("");
		ЛокальноеПредставлениеГруппы = "";
	Иначе
		ЛокальноеИмяГруппы           = ПараметрыГруппы.ИмяГруппы;
		ЛокальноеПредставлениеГруппы = ПараметрыГруппы.ПредставлениеВыбраннойГруппы;
	КонецЕсли;
	
	Группа = ДобавитьВыбранноеПолеГруппа(ГруппировкаДетальныеЗаписи, ЛокальноеИмяГруппы, ЛокальноеПредставлениеГруппы,
		ПараметрыГруппы.РасположениеВыбраннойГруппы);
	
	Для Каждого ЭлементСтруктуры Из СтруктураПолей Цикл
	
		Имя           = ЭлементСтруктуры.Ключ;
		Представление = ЭлементСтруктуры.Значение;
	
		ДобавитьПолеНабораДанных(НаборДанныхКД, Имя, Представление);
		ДобавитьВыбранноеПоле(Группа, Имя, Представление);
	
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьПоля(ПоляВыбора, СтруктураПолей)
	
	Для Каждого Поле Из СтруктураПолей Цикл
		ПоляВыбора = ПоляВыбора + ?(ЗначениеЗаполнено(ПоляВыбора), ", ", "") + Поле.Ключ;
	КонецЦикла;
	
КонецПроцедуры

Функция СформироватьСтруктуруПолей(МетаданныеПоля, Исключения = Неопределено)
	
	МассивИсключаемыхПолей = Новый Массив;
	Если Исключения <> Неопределено Тогда
		МассивИсключаемыхПолей = СтрРазделить(Исключения, ", ");
	КонецЕсли;
	
	СтруктураПолей = Новый Структура;
	Для Каждого ЕдиницаМетаданных Из МетаданныеПоля Цикл
	
		Если МассивИсключаемыхПолей.Найти(ЕдиницаМетаданных.Имя) = Неопределено Тогда
			СтруктураПолей.Вставить(ЕдиницаМетаданных.Имя, ЕдиницаМетаданных.Представление());
		КонецЕсли;
	
	КонецЦикла;
	
	Возврат СтруктураПолей;
	
КонецФункции

Функция СформироватьНумератор(ГраницаНумератора)
	
	СтруктураНумератора = Новый Структура;
	
	Для Индекс = 1 По ГраницаНумератора Цикл
		ИндексСтрокой = Формат(Индекс, "ЧГ=0");
		СтруктураНумератора.Вставить("ГруппаЭлементаВыводаСКДТекущегоДокумента" + ИндексСтрокой, ИндексСтрокой);
	КонецЦикла;
	
	Возврат СтруктураНумератора;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область Прочее

Процедура НастройкиКДПоУмолчаниюТиповойСхемы(СхемаКомпоновкиДанных, ПараметрыОтчета, НовыеНастройкиКД, КлючВарианта)
	
	СписокРегистровПараметр = СхемаКомпоновкиДанных.Параметры.Найти("СписокРегистров");
	СписокРегистров         = Новый СписокЗначений;
	
	Если КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Свойство("ГлобальныеНастройки") Тогда
		
		ГлобальныеНастройки = КомпоновщикНастроек.Настройки.ДополнительныеСвойства.ГлобальныеНастройки;
		Для Каждого ГлобальнаяНастройка Из ГлобальныеНастройки Цикл
			
			ТекущееЗначениеДляВыбора = "Регистр" + ГлобальнаяНастройка.ВидРегистра + "_" + ГлобальнаяНастройка.ИмяРегистра;
			
			ТекущееЗначениеДляВыбораПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Регистр %1 %2'"),
			НРег(ГлобальнаяНастройка.ЛокализацияВидРегистра), ГлобальнаяНастройка.ПредставлениеРегистра);
			
			СписокРегистров.Добавить(ТекущееЗначениеДляВыбора, ТекущееЗначениеДляВыбораПредставление, Истина);
			
		КонецЦикла;
		
	КонецЕсли;
	
	СписокРегистровПараметр.УстановитьДоступныеЗначения(СписокРегистров);
	
	Если НовыеНастройкиКД <> Неопределено Тогда
		НовыеНастройкиКД.ДополнительныеСвойства.Вставить("ГлобальныеНастройки",
			КомпоновщикНастроек.Настройки.ДополнительныеСвойства.ГлобальныеНастройки);
	КонецЕсли;
	
	КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	
	НастройкиКД = КомпоновщикНастроек.Настройки;
	
	Если КлючВарианта = "Основной" Тогда
		ЗаполнитьСхемуКомпоновкиДанных_Горизонталь(ПараметрыОтчета.ДокументВладелец, НовыеНастройкиКД);
	ИначеЕсли КлючВарианта = "Дополнительный" Тогда
		ЗаполнитьСхемуКомпоновкиДанных_Вертикаль(ПараметрыОтчета.ДокументВладелец, НовыеНастройкиКД, Истина);
	КонецЕсли;
	
	ПараметрОтчета          = НастройкиКД.ПараметрыДанных.Элементы.Найти("ДокументВладелец");
	ПараметрОтчета.Значение = ПараметрыОтчета.ДокументВладелец;
	
	ДобавитьРесурсРасчетаКоличестваЗаписей(СхемаКомпоновкиДанных);
	
КонецПроцедуры

Функция МассивИспользуемыхРегистров(Регистратор, Движения)
	
	Результат = Новый Массив;
	Для Каждого ОбъектМетаданныхРегистр Из Движения Цикл
		Результат.Добавить(ОбъектМетаданныхРегистр);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗафиксироватьГлобальныеНастройки(Регистратор, МассивИспользуемыхРегистров)
	
	ГлобальныеНастройки             = Новый Массив;
	ПредварительныйРасчетКоличества = ПредварительныйРасчетКоличества(Регистратор, МассивИспользуемыхРегистров);
	
	Для Каждого ИспользуемыйРегистр Из МассивИспользуемыхРегистров Цикл
		
		ИмяРегистра           = ИспользуемыйРегистр.Имя;
		ПредставлениеРегистра = ИспользуемыйРегистр.Представление();
		
		Если ОбщегоНазначения.ЭтоРегистрНакопления(ИспользуемыйРегистр) Тогда
			ВидРегистра            = "Накопления";
			ЛокализацияВидРегистра = НСтр("ru = 'Накопления'");
		ИначеЕсли ОбщегоНазначения.ЭтоРегистрБухгалтерии(ИспользуемыйРегистр) Тогда
			ВидРегистра            = "Бухгалтерии";
			ЛокализацияВидРегистра = НСтр("ru = 'Бухгалтерии'");
		ИначеЕсли ОбщегоНазначения.ЭтоРегистрСведений(ИспользуемыйРегистр) Тогда
			ВидРегистра            = "Сведений";
			ЛокализацияВидРегистра = НСтр("ru = 'Сведений'");
		ИначеЕсли ОбщегоНазначения.ЭтоРегистрРасчета(ИспользуемыйРегистр) Тогда
			ВидРегистра            = "Расчета";
			ЛокализацияВидРегистра = НСтр("ru = 'Расчета'");
		КонецЕсли;
		
		СтруктураНастроек = Новый Структура;
		СтруктураНастроек.Вставить("ИмяРегистра",            ИмяРегистра);
		СтруктураНастроек.Вставить("ПредставлениеРегистра" , ПредставлениеРегистра);
		СтруктураНастроек.Вставить("ВидРегистра",            ВидРегистра);
		СтруктураНастроек.Вставить("ЛокализацияВидРегистра", ЛокализацияВидРегистра);
		СтруктураНастроек.Вставить("КоличествоЗаписей",      ПредварительныйРасчетКоличества[СтрЗаменить(ИспользуемыйРегистр.ПолноеИмя(), ".", "_")]);
		СтруктураНастроек.Вставить("Использование",          Ложь);
		ГлобальныеНастройки.Добавить(СтруктураНастроек);
		
	КонецЦикла;
	
	КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить("ГлобальныеНастройки", ГлобальныеНастройки);
	
КонецПроцедуры

Функция ПредварительныйРасчетКоличества(Регистратор, МассивИспользуемыхРегистров)
	
	РассчитанноеКоличество = Новый Соответствие;
	
	ТекстЗапроса = "";
	Для Каждого ИспользуемыйРегистр Из МассивИспользуемыхРегистров Цикл
		
		ПолноеИмя          = ИспользуемыйРегистр.ПолноеИмя();
		ТекстЗапросаШаблон =
		"ВЫБРАТЬ
		|	""&ПолноеИмяРегистра"" КАК ПолноеИмяРегистра,
		|	КОЛИЧЕСТВО(*) КАК Количество
		|ИЗ
		|	&ТекущаяТаблица КАК ТекущаяТаблица
		|ГДЕ
		|	&Условие";
		
		ТекстЗапросаШаблон = СтрЗаменить(ТекстЗапросаШаблон, "&ПолноеИмяРегистра", СтрЗаменить(ПолноеИмя, ".", "_"));
		ТекстЗапросаШаблон = СтрЗаменить(ТекстЗапросаШаблон, "&Условие", "Регистратор = &ДокументВладелец");
		ТекстЗапросаШаблон = СтрЗаменить(ТекстЗапросаШаблон, "&ТекущаяТаблица", ПолноеИмя);
		
		ТекстЗапроса = ТекстЗапроса + ?(ЗначениеЗаполнено(ТекстЗапроса), " ОБЪЕДИНИТЬ ВСЕ ", "") + ТекстЗапросаШаблон;
		
	КонецЦикла;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ДокументВладелец", Регистратор);
	Результат = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаРезультата Из Результат Цикл
		РассчитанноеКоличество.Вставить(СтрокаРезультата.ПолноеИмяРегистра, СтрокаРезультата.Количество);
	КонецЦикла;
	
	Возврат РассчитанноеКоличество;
	
КонецФункции

Функция ТаблицаИспользуемыхРегистров(МассивИспользуемыхРегистров)
	
	ТаблицаРегистров        = Новый ТаблицаЗначений;
	КолонкиТаблицыРегистров = ТаблицаРегистров.Колонки;
	КолонкиТаблицыРегистров.Добавить("ИмяРегистра",                   Новый ОписаниеТипов("Строка"));
	КолонкиТаблицыРегистров.Добавить("ПредставлениеРегистра",         Новый ОписаниеТипов("Строка"));
	КолонкиТаблицыРегистров.Добавить("ВидРегистра",                   Новый ОписаниеТипов("Строка"));
	КолонкиТаблицыРегистров.Добавить("ЛокализацияВидРегистра",        Новый ОписаниеТипов("Строка"));
	КолонкиТаблицыРегистров.Добавить("ВидРегистраНакопления",         Новый ОписаниеТипов("Строка"));
	КолонкиТаблицыРегистров.Добавить("ПериодичностьРегистраСведений", Новый ОписаниеТипов("Строка"));
	КолонкиТаблицыРегистров.Добавить("РежимЗаписиРегистраСведений",   Новый ОписаниеТипов("Строка"));
	КолонкиТаблицыРегистров.Добавить("ПериодичностьРегистраРасчета",  Новый ОписаниеТипов("Строка"));
	КолонкиТаблицыРегистров.Добавить("ВидРегистраДляУпорядочивания",  Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(1, 0, ДопустимыйЗнак.Неотрицательный)));
	
	Для Каждого ИспользуемыйРегистр Из МассивИспользуемыхРегистров Цикл
	
		СтрокаТаблицыРегистров             = ТаблицаРегистров.Добавить();
		СтрокаТаблицыРегистров.ИмяРегистра = ИспользуемыйРегистр.Имя;
		
		Если ОбщегоНазначения.ЭтоРегистрНакопления(ИспользуемыйРегистр) Тогда
			
			СтрокаТаблицыРегистров.ПредставлениеРегистра        = ИспользуемыйРегистр.Представление();
			СтрокаТаблицыРегистров.ВидРегистра                  = "Накопления";
			СтрокаТаблицыРегистров.ЛокализацияВидРегистра       = НСтр("ru = 'Накопления'");
			СтрокаТаблицыРегистров.ВидРегистраНакопления        = Строка(ИспользуемыйРегистр.ВидРегистра);
			СтрокаТаблицыРегистров.ВидРегистраДляУпорядочивания = 1;
			
		ИначеЕсли ОбщегоНазначения.ЭтоРегистрБухгалтерии(ИспользуемыйРегистр) Тогда
			
			СтрокаТаблицыРегистров.ПредставлениеРегистра        = ИспользуемыйРегистр.Представление();
			СтрокаТаблицыРегистров.ВидРегистра                  = "Бухгалтерии";
			СтрокаТаблицыРегистров.ЛокализацияВидРегистра       = НСтр("ru = 'Бухгалтерии'");
			СтрокаТаблицыРегистров.ВидРегистраДляУпорядочивания = 3;
			
		ИначеЕсли ОбщегоНазначения.ЭтоРегистрСведений(ИспользуемыйРегистр) Тогда
			
			СтрокаТаблицыРегистров.ПредставлениеРегистра         = ИспользуемыйРегистр.Представление();
			СтрокаТаблицыРегистров.ВидРегистра                   = "Сведений";
			СтрокаТаблицыРегистров.ЛокализацияВидРегистра        = НСтр("ru = 'Сведений'");
			СтрокаТаблицыРегистров.ПериодичностьРегистраСведений = ИспользуемыйРегистр.ПериодичностьРегистраСведений;
			СтрокаТаблицыРегистров.РежимЗаписиРегистраСведений   = ИспользуемыйРегистр.РежимЗаписи;
			СтрокаТаблицыРегистров.ВидРегистраДляУпорядочивания  = 2;
			
		ИначеЕсли ОбщегоНазначения.ЭтоРегистрРасчета(ИспользуемыйРегистр) Тогда
			
			СтрокаТаблицыРегистров.ПредставлениеРегистра        = ИспользуемыйРегистр.Представление();
			СтрокаТаблицыРегистров.ВидРегистра                  = "Расчета";
			СтрокаТаблицыРегистров.ЛокализацияВидРегистра       = НСтр("ru = 'Расчета'");
			СтрокаТаблицыРегистров.ПериодичностьРегистраРасчета = ИспользуемыйРегистр.Периодичность;
			СтрокаТаблицыРегистров.ВидРегистраДляУпорядочивания = 4;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ТаблицаРегистров.Сортировать("ВидРегистраДляУпорядочивания, ПредставлениеРегистра");
	
	Возврат ТаблицаРегистров;
	
КонецФункции

Процедура ДоопределитьМакетСКД(СхемаКД, ИндексНабора, ВидРегистра, ЛокализацияВидРегистра, ИмяРегистра,
	ГлобальныеНастройки)
	
	СтруктураПоиска = Новый Структура("ВидРегистра, ИмяРегистра", ВидРегистра, ИмяРегистра);
	СтрокиГлобальныхНастроек = ГлобальныеНастройки.НайтиСтроки(СтруктураПоиска);
	
	Если СтрокиГлобальныхНастроек.Количество() > 0 Тогда
		
		СтрокаГлобальныхНастроек = СтрокиГлобальныхНастроек.Получить(0);
		
		ЗаголовокГруппировки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Регистр %1 ""%2""'"),
		НРег(ЛокализацияВидРегистра), СтрокаГлобальныхНастроек.ПредставлениеРегистра);
		
		Макет       = СхемаКД.Макеты.Добавить();
		Макет.Имя   = "Макет" + Формат(ИндексНабора + 1, "ЧГ=0");
		Макет.Макет = Новый МакетОбластиКомпоновкиДанных;
		
		Параметр           = Макет.Параметры.Добавить(Тип("ПараметрОбластиВыражениеКомпоновкиДанных"));
		Параметр.Имя       = "КоличествоЗаписей";
		Параметр.Выражение = "КоличествоЗаписейРегистра";
		
		МакетГруппировки                = СхемаКД.МакетыГруппировок.Добавить();
		МакетГруппировки.ИмяГруппировки = "Регистр" + ВидРегистра + "_" + ИмяРегистра;
		МакетГруппировки.ТипМакета      = ТипМакетаОбластиКомпоновкиДанных.Заголовок;
		МакетГруппировки.Макет          = "Макет" + Формат(ИндексНабора + 1, "ЧГ=0");
		
		МакетОбласти = Макет.Макет;
		СтрокаМакета = МакетОбласти.Добавить(Тип("СтрокаТаблицыОбластиКомпоновкиДанных"));
		Ячейка       = СтрокаМакета.Ячейки.Добавить();
		
		ОформлениеЯчейки = Ячейка.Оформление.Элементы;
		
		Шрифт = ОформлениеЯчейки.Найти("Шрифт");
		
		Если Шрифт <> Неопределено Тогда
			Шрифт.Значение      = Новый Шрифт("Arial", 14);
			Шрифт.Использование = Истина;
		КонецЕсли;
		
		Размещение = ОформлениеЯчейки.Найти("Размещение");
		
		Если Размещение <> Неопределено Тогда
			Размещение.Значение      = ТипРазмещенияТекстаКомпоновкиДанных.Переносить;
			Размещение.Использование = Истина;
		КонецЕсли;
		
		ОбластьКД          = Ячейка.Элементы.Добавить(Тип("ПолеОбластиКомпоновкиДанных"));
		ОбластьКД.Значение = ЗаголовокГруппировки + " (";
		
		ОбластьКД          = Ячейка.Элементы.Добавить(Тип("ПолеОбластиКомпоновкиДанных"));
		ОбластьКД.Значение = Новый ПараметрКомпоновкиДанных(Параметр.Имя);
		
		ОбластьКД          = Ячейка.Элементы.Добавить(Тип("ПолеОбластиКомпоновкиДанных"));
		ОбластьКД.Значение = ")";
		
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьСписокРегистров(ТаблицаРегистров)
	
	СписокРегистров = Новый СписокЗначений;
	
	Для Каждого СтрокаТаблицыРегистров Из ТаблицаРегистров Цикл
		
		ЗаголовокГруппировки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Регистр %1 %2'"),
			НРег(СтрокаТаблицыРегистров.ЛокализацияВидРегистра), СтрокаТаблицыРегистров.ПредставлениеРегистра);
			
		ЭлементОтбора = "Регистр" + СтрокаТаблицыРегистров.ВидРегистра + "_" + СтрокаТаблицыРегистров.ИмяРегистра;
	
		СписокРегистров.Добавить(ЭлементОтбора, ЗаголовокГруппировки);
	
	КонецЦикла;
	
	Возврат СписокРегистров;
	
КонецФункции

Процедура УстановитьПараметрыДанных(НастройкиКД, СтруктураПараметров)
	
	ПараметрыДанных = НастройкиКД.ПараметрыДанных.Элементы;
	
	Для Каждого Параметр Из СтруктураПараметров Цикл 
	
		ТекущийПараметр   = Новый ПараметрКомпоновкиДанных(Параметр.Ключ);
		ТекущийПараметрКД = ПараметрыДанных.Найти(ТекущийПараметр);
	
		Если ТекущийПараметрКД <> Неопределено Тогда
	
			ТекущийПараметрКД.Использование = Истина;
			ТекущийПараметрКД.Значение      = Параметр.Значение;
	
		Иначе
	
			ПараметрДанных               = НастройкиКД.ПараметрыДанных.Элементы.Добавить();
			ПараметрДанных.Использование = Истина;
			ПараметрДанных.Значение      = Параметр.Значение;
			ПараметрДанных.Параметр      = Новый ПараметрКомпоновкиДанных(Параметр.Ключ);
	
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ВыводСКД

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДокументПараметрСКД = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("ДокументВладелец");
	Если ДокументПараметрСКД = Неопределено Или Не ЗначениеЗаполнено(ДокументПараметрСКД.Значение) Тогда
		Возврат;
	Иначе
		ДокументОтбор = ДокументПараметрСКД.Значение;
	КонецЕсли;
	
	МакетЗаголовка                              = ПолучитьМакет("Заголовок");
	ОбластьЗаголовка                            = МакетЗаголовка.ПолучитьОбласть("ОбластьЗаголовок");
	ОбластьЗаголовка.Параметры.СсылкаНаДокумент = Строка(ДокументОтбор);
	ПустаяОбласть                               = МакетЗаголовка.ПолучитьОбласть("ПустаяОбласть");
	
	ОбластьЗаголовка.ТекущаяОбласть.Расшифровка = ДокументОтбор;
	
	ДокументРезультат.Вывести(ПустаяОбласть);
	ДокументРезультат.Вывести(ОбластьЗаголовка);
	ДокументРезультат.Вывести(ПустаяОбласть);
	
	НастройкиКД = КомпоновщикНастроек.ПолучитьНастройки();
	
	СкрытьНеактуальныеГруппировки(НастройкиКД, ДокументОтбор);
	
	ВосстановитьОтборПоГруппировкамРегистров(НастройкиКД);
	
	ПользовательскоеОформление(СхемаКомпоновкиДанных, НастройкиКД);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки   = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиКД, ДанныеРасшифровки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки, Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	ДоопределитьГотовыйМакет(ДокументРезультат, ДокументОтбор);
	
КонецПроцедуры

Процедура ДоопределитьГотовыйМакет(ДокументРезультат, ДокументРегистратор)
	
	ТаблицаГлобальныхНастроек = ПерехватитьГлобальныеНастройки();
	ТаблицаГлобальныхНастроек.Колонки.Добавить("ТаблицаПримечаний", Новый ОписаниеТипов("ТаблицаЗначений"));
	
	НастройкиКД = КомпоновщикНастроек.ПолучитьНастройки();
	
	Для Каждого Строка Из ТаблицаГлобальныхНастроек Цикл
		Строка.ТаблицаПримечаний = ФормированиеСтрокиПримечания("Регистр" + Строка.ВидРегистра + "_" + Строка.ИмяРегистра, НастройкиКД);
	КонецЦикла;
	
	ШиринаТаблицы = ДокументРезультат.ШиринаТаблицы;
	ВысотаТаблицы = ДокументРезультат.ВысотаТаблицы;
	
	ТаблицаПримечаний = Новый ТаблицаЗначений;
	
	Для ИндексСтрок = 1 По ВысотаТаблицы Цикл
	
		Для ИндексКолонок = 1 По ШиринаТаблицы Цикл
	
			ИмяОбласти = "R" + Формат(ИндексСтрок, "ЧГ=0") + "C" + Формат(ИндексКолонок, "ЧГ=0");
			Область    = ДокументРезультат.Область(ИмяОбласти);
			
			СтрокаГлобальныхНастроек = НайтиНадписьРегистраНаДокументе(ТаблицаГлобальныхНастроек, Область.Текст);
			Если СтрокаГлобальныхНастроек <> Неопределено Тогда
	
				СтруктураРасшифровки = Новый Структура;
				СтруктураРасшифровки.Вставить("ВидРегистра", СтрокаГлобальныхНастроек.ЛокализацияВидРегистра);
				СтруктураРасшифровки.Вставить("ИмяРегистра", СтрокаГлобальныхНастроек.ИмяРегистра);
				СтруктураРасшифровки.Вставить("Регистратор", ДокументРегистратор);
				Область.Расшифровка  = СтруктураРасшифровки;
	
				ОтчетыСервер.ВывестиГиперссылку(Область, СтруктураРасшифровки, Область.Текст);
	
				ТаблицаПримечаний = СтрокаГлобальныхНастроек.ТаблицаПримечаний;
	
				ГраницаПримечаний = 1;
				
				Прервать;
	
			КонецЕсли;
	
		КонецЦикла;
	
		ИмяОбластиНумератора = "R" + Формат(ИндексСтрок, "ЧГ=0") + "C1";
		ОбластьНумератора    = ДокументРезультат.Область(ИмяОбластиНумератора);
	
		Если Не ЭтоЧисло(ОбластьНумератора.Текст) Тогда
	
			Если СтрНайти(ОбластьНумератора.Текст, "№ГруппаЭлементаВыводаСКДТекущегоДокумента") <> 0 Тогда
				ОбластьНумератора.Текст         = "№";
				ОбластьНумератора.ШиринаКолонки = 5;
			КонецЕсли;
	
		Иначе
	
			ОбластьНумератора.Отступ                  = 0;
			ОбластьНумератора.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Лево;
	
			СтрокаПримечания = ТаблицаПримечаний.Найти(ОбластьНумератора.Текст, "Этаж");
			Если СтрокаПримечания <> Неопределено Тогда
	
				ЭтажностьСтроки = СтрокаПримечания.Этажность + 1;
				Если ГраницаПримечаний = ЭтажностьСтроки Тогда
					
					ТекстПримечания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'В этой строке: %1'"),
						СтрокаПримечания.Примечание + ".");
	
					ОбластьНумератора.Примечание.Текст = ТекстПримечания;
	
				Иначе
					ГраницаПримечаний = ГраницаПримечаний + 1;
				КонецЕсли;
	
			КонецЕсли;
	
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область РазделОформления

Процедура ОформитьОтчет(НастройкиКД)
	
	СтруктураНастроек = Новый Структура;
	СтруктураНастроек.Вставить("ВыводитьОтбор",           ТипВыводаТекстаКомпоновкиДанных.НеВыводить);
	СтруктураНастроек.Вставить("ВыводитьПараметрыДанных", ТипВыводаТекстаКомпоновкиДанных.НеВыводить);
	ДополнительнаяОбщаяНастройкаГруппировки(НастройкиКД, СтруктураНастроек);
	
КонецПроцедуры

Процедура ОформитьГруппировкуПоРегистру(ГруппировкаПоРегистру)
	
	СтруктураНастроек = Новый Структура;
	СтруктураНастроек.Вставить("ВыводитьОтбор", ТипВыводаТекстаКомпоновкиДанных.НеВыводить);
	ДополнительнаяОбщаяНастройкаГруппировки(ГруппировкаПоРегистру, СтруктураНастроек);
	
КонецПроцедуры

Процедура ПользовательскоеОформление(СхемаКД, НастройкиКД)
	
	УсловноеОформление = НастройкиКД.УсловноеОформление.Элементы;
	ОформитьОтрицательныеЧисла(УсловноеОформление);
	
	ДополнительныеСвойства = НастройкиКД.ДополнительныеСвойства;
	Если Не ДополнительныеСвойства.Свойство("ГлобальныеНастройки") Тогда
		Возврат;
	КонецЕсли;
	
	Если ЕстьОстаточныйРегистрНакопления(ДополнительныеСвойства.ГлобальныеНастройки) Тогда
		ОформитьВидДвижения(УсловноеОформление, ВидДвиженияНакопления.Приход);
		ОформитьВидДвижения(УсловноеОформление, ВидДвиженияНакопления.Расход);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОформитьОтрицательныеЧисла(УсловноеОформление)
	
	ЕдиницаУсловногоОформления = УсловноеОформление.Добавить();
	ОтборУсловногоОформления = ЕдиницаУсловногоОформления.Отбор;
	
	ЭлементОтбора = ОтборУсловногоОформления.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	
	ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ПараметрыДанных.ОтрицательноеКрасным");
	ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = Истина;
	ЭлементОтбора.Использование  = Истина;
	
	ЕдиницаУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ВыделятьОтрицательные", Истина);
	
	ЕдиницаУсловногоОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	ЕдиницаУсловногоОформления.Использование    = Истина;
	
КонецПроцедуры

Процедура ОформитьВидДвижения(УсловноеОформление, ВидДвижения)
	
	ЦелевоеПоле = Новый ПолеКомпоновкиДанных("ВидДвижения");
	
	ЕдиницаУсловногоОформления = УсловноеОформление.Добавить();
	ОтборУсловногоОформления = ЕдиницаУсловногоОформления.Отбор;
	
	ЭлементОтбора = ОтборУсловногоОформления.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	
	ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ПараметрыДанных.ВыделятьВидДвиженияНакопления");
	ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = Истина;
	ЭлементОтбора.Использование  = Истина;
	
	ЭлементОтбора = ОтборУсловногоОформления.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	
	ЭлементОтбора.ЛевоеЗначение  = ЦелевоеПоле;
	ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = ВидДвижения;
	ЭлементОтбора.Использование  = Истина;
	
	ОформляемыеПоля = ЕдиницаУсловногоОформления.Поля.Элементы;
	ОформляемоеПоле = ОформляемыеПоля.Добавить();
	ОформляемоеПоле.Поле          = ЦелевоеПоле;
	ОформляемоеПоле.Использование = Истина;
	
	ЕдиницаУсловногоОформления.ИспользоватьВЗаголовке = ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	ЕдиницаУсловногоОформления.ИспользоватьВЗаголовкеПолей = ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	
	ЕдиницаУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста",
		?(ВидДвижения = ВидДвиженияНакопления.Приход, ЦветаСтиля.РезультатУспехЦвет, ЦветаСтиля.ЦветОтрицательногоЧисла));
	ЕдиницаУсловногоОформления.Использование = Истина;
	
КонецПроцедуры

Функция ЕстьОстаточныйРегистрНакопления(ГлобальныеНастройки)
	
	Для Каждого ИнформацияПоРегистру Из ГлобальныеНастройки Цикл
		
		Если ИнформацияПоРегистру.ВидРегистра <> "Накопления" Тогда
			Продолжить;
		КонецЕсли;
			
		ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени("Регистр" + ИнформацияПоРегистру.ВидРегистра + "." + ИнформацияПоРегистру.ИмяРегистра);
		Если ОбъектМетаданных.ВидРегистра = Метаданные.СвойстваОбъектов.ВидРегистраНакопления.Остатки Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область ФормированиеПримечаний

Функция ФормированиеСтрокиПримечания(ИмяРегистра, НастройкиКД)
	
	ЭтажиСтроки   = Новый ТаблицаЗначений;
	КолонкиЭтажей = ЭтажиСтроки.Колонки;
	КолонкиЭтажей.Добавить("Этаж",       Новый ОписаниеТипов("Строка"));
	КолонкиЭтажей.Добавить("Примечание", Новый ОписаниеТипов("Строка"));
	КолонкиЭтажей.Добавить("Этажность",  Новый ОписаниеТипов("Число"));
	
	МассивВыбранныхПолей = Новый Массив;
	СтруктураГруппировок = НастройкиКД.Структура;
	НайтиГруппировкуРекурсивно(СтруктураГруппировок, МассивВыбранныхПолей, ИмяРегистра);
	
	ПолеНумератора  = Новый ПолеКомпоновкиДанных("Нумератор");
	ЭтажностьСтроки = ЭтажностьСтроки(ПолеНумератора, МассивВыбранныхПолей);
	
	Для Индекс = 0 По ЭтажностьСтроки - 1 Цикл
	
		СтрокаПримечания = "";
	
		Для Каждого ВыбраннаяГруппа Из МассивВыбранныхПолей Цикл
			Если ВыбраннаяГруппа.Поле <> ПолеНумератора Тогда
	
				Заголовок = "";
				Если ВыбраннаяГруппа.Заголовок = НСтр("ru = 'Стандартные реквизиты'") Тогда
					Заголовок = НСтр("ru = 'Стандартный реквизит'");
				ИначеЕсли ВыбраннаяГруппа.Заголовок = НСтр("ru = 'Измерения'") Тогда
					Заголовок = НСтр("ru = 'Измерение'");
				ИначеЕсли ВыбраннаяГруппа.Заголовок = НСтр("ru = 'Ресурсы'") Тогда
					Заголовок = НСтр("ru = 'Ресурс'");
				ИначеЕсли ВыбраннаяГруппа.Заголовок = НСтр("ru = 'Реквизиты'") Тогда
					Заголовок = НСтр("ru = 'Реквизит'");
				ИначеЕсли ВыбраннаяГруппа.Заголовок = НСтр("ru = 'Данные расчета'") Тогда
					Заголовок = НСтр("ru = 'Данные расчета'");
				КонецЕсли;
	
				Элементы = ВыбраннаяГруппа.Элементы;
	
				Если Индекс <= Элементы.Количество() - 1 Тогда
					ВыбранноеПоле = Элементы[Индекс];
					Если ВыбранноеПоле.Использование Тогда
						СтрокаПримечания = СтрокаПримечания + ?(ЗначениеЗаполнено(СтрокаПримечания), ", ", "")
							+ Элементы[Индекс].Заголовок + " (" + Заголовок + ")";
					КонецЕсли;
				КонецЕсли;
	
			КонецЕсли;
		КонецЦикла;
		
		СтруктураЭтажа = Новый Структура;
		СтруктураЭтажа.Вставить("Этаж",       Формат(Индекс + 1, "ЧГ=0"));
		СтруктураЭтажа.Вставить("Примечание", СтрокаПримечания);
		СтруктураЭтажа.Вставить("Этажность",  ЭтажностьСтроки);
		
		ЗаполнитьЗначенияСвойств(ЭтажиСтроки.Добавить(), СтруктураЭтажа);
	
	КонецЦикла;
	
	Возврат ЭтажиСтроки;
	
КонецФункции

Функция ЭтажностьСтроки(ПолеНумератора, МассивВыбранныхПолей)
	
	ЭтажностьСтроки = 0;
	Для Каждого ВыбраннаяГруппа Из МассивВыбранныхПолей Цикл
	
		Если ВыбраннаяГруппа.Поле = ПолеНумератора Тогда
			Элементы = ВыбраннаяГруппа.Элементы;
			Для Каждого Элемент Из Элементы Цикл
				Если Элемент.Использование Тогда
					ЭтажностьСтроки = ЭтажностьСтроки + 1;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	
	КонецЦикла;
	
	Возврат ЭтажностьСтроки;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЭтоЧисло(Знач ПроверяемоеЗначение)
	
	Если ПроверяемоеЗначение = "0" Тогда
		Возврат Истина;
	Иначе
		ОписаниеТипаЧисло = Новый ОписаниеТипов("Число");
		Возврат ОписаниеТипаЧисло.ПривестиЗначение(ПроверяемоеЗначение) <> 0;
	КонецЕсли;
	
КонецФункции

Функция ОпределитьРасположениеЭлементовУправления()
	
	МассивНастроек              = Новый Массив;
	НастройкиЭлементаУправления = Новый Структура;
	
	НастройкиЭлементаУправления.Вставить("Поле",                     "СписокРегистров");
	НастройкиЭлементаУправления.Вставить("РастягиватьПоГоризонтали", Ложь);
	НастройкиЭлементаУправления.Вставить("АвтоМаксимальнаяШирина",   Истина);
	НастройкиЭлементаУправления.Вставить("Ширина",                   40);
	
	МассивНастроек.Добавить(НастройкиЭлементаУправления);
	
	НастройкиЭлементовУправления = Новый Структура();
	НастройкиЭлементовУправления.Вставить("ПараметрыДанных", МассивНастроек);
	
	Возврат НастройкиЭлементовУправления;
	
КонецФункции

Функция ПерехватитьГлобальныеНастройки()
	
	ГлобальныеНастройки = КомпоновщикНастроек.Настройки.ДополнительныеСвойства.ГлобальныеНастройки;
	
	ТаблицаГлобальныхНастроек = Новый ТаблицаЗначений;
	КолонкиГлобальныхНастроек = ТаблицаГлобальныхНастроек.Колонки;
	КолонкиГлобальныхНастроек.Добавить("ИмяРегистра",
		Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(128)));
	КолонкиГлобальныхНастроек.Добавить("ПредставлениеРегистра",
		Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(128)));
	КолонкиГлобальныхНастроек.Добавить("ВидРегистра",
		Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(32)));
	КолонкиГлобальныхНастроек.Добавить("ЛокализацияВидРегистра",
		Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(32)));
	КолонкиГлобальныхНастроек.Добавить("КоличествоЗаписей"     , Новый ОписаниеТипов("Число"));
	КолонкиГлобальныхНастроек.Добавить("НадписьРезультат"      ,
		Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(256)));
	
	Для Каждого ЭлементГлобальныхНастроек Из ГлобальныеНастройки Цикл
	
		НоваяСтрока = ТаблицаГлобальныхНастроек.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭлементГлобальныхНастроек);
		
		НадписьРезультат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Регистр %1 ""%2""'"),
			СокрЛП(НРег(ЭлементГлобальныхНастроек.ЛокализацияВидРегистра)),
			ЭлементГлобальныхНастроек.ПредставлениеРегистра);
	
		НоваяСтрока.НадписьРезультат = НадписьРезультат;
	
	КонецЦикла;
	
	Возврат ТаблицаГлобальныхНастроек;
	
КонецФункции

Функция УстановитьПараметрВывода(КомпоновщикНастроекГруппировка, ИмяПараметра, Значение)
	
	ПараметрСКД = Новый ПараметрКомпоновкиДанных(ИмяПараметра);
	Если ТипЗнч(КомпоновщикНастроекГруппировка) = Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда	
		ЗначениеПараметра = КомпоновщикНастроекГруппировка.Настройки.ПараметрыВывода.НайтиЗначениеПараметра(ПараметрСКД);
	Иначе
		ЗначениеПараметра = КомпоновщикНастроекГруппировка.ПараметрыВывода.НайтиЗначениеПараметра(ПараметрСКД);
	КонецЕсли;
	
	Если ЗначениеПараметра <> Неопределено Тогда
		ЗначениеПараметра.Использование = Истина;
		ЗначениеПараметра.Значение = Значение;
	КонецЕсли;
	
	Возврат ЗначениеПараметра;
	
КонецФункции

Процедура НайтиГруппировкуРекурсивно(КоллекцияЭлементов, МассивВыбранныхПолей, ЗначениеПоиска)
	
	Для каждого Элемент Из КоллекцияЭлементов Цикл
		Если ТипЗнч(Элемент) = Тип("ГруппировкаКомпоновкиДанных") Тогда
			Если Элемент.Имя = ЗначениеПоиска Тогда
				Если Элемент.Структура.Количество() > 0 Тогда
					ДетальныеЗаписи = Элемент.Структура[0];
					ВыбранныеПоля   = ДетальныеЗаписи.Выбор.Элементы;
					Для Каждого ВыбранноеПоле Из ВыбранныеПоля Цикл
						МассивВыбранныхПолей.Добавить(ВыбранноеПоле);
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
			НайтиГруппировкуРекурсивно(Элемент.Структура, МассивВыбранныхПолей, ЗначениеПоиска);
		ИначеЕсли ТипЗнч(Элемент) = Тип("ТаблицаКомпоновкиДанных") Тогда
			НайтиГруппировкуРекурсивно(Элемент.Строки, МассивВыбранныхПолей, ЗначениеПоиска);
			НайтиГруппировкуРекурсивно(Элемент.Колонки, МассивВыбранныхПолей, ЗначениеПоиска);
		ИначеЕсли ТипЗнч(Элемент) = Тип("ДиаграммаКомпоновкиДанных") Тогда
			НайтиГруппировкуРекурсивно(Элемент.Серии, МассивВыбранныхПолей, ЗначениеПоиска);
			НайтиГруппировкуРекурсивно(Элемент.Точки, МассивВыбранныхПолей, ЗначениеПоиска);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ПараметрыОтчета(НастройкиКД, ПользовательскиеНастройкиКД)
	
	Результат = Новый Структура("ДокументВладелец");
	
	ПараметрДокументВладелец = Неопределено;
	
	Если ТипЗнч(ПользовательскиеНастройкиКД) = Тип("ПользовательскиеНастройкиКомпоновкиДанных") Тогда
		Для Каждого ЭлементКД Из ПользовательскиеНастройкиКД.Элементы Цикл
			Если ТипЗнч(ЭлементКД) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
				ИмяПараметра = Строка(ЭлементКД.Параметр);
				Если ИмяПараметра = "ДокументВладелец" Тогда
					ПараметрДокументВладелец = ЭлементКД;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ПараметрДокументВладелец = Неопределено Тогда
		ПараметрДокументВладелец = НастройкиКД.ПараметрыДанных.Элементы.Найти("ДокументВладелец");
	КонецЕсли;
	
	Если ПараметрДокументВладелец <> Неопределено Тогда
		Результат.ДокументВладелец = ПараметрДокументВладелец.Значение;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Копирует настройки компоновки данных
//
// Параметры:
//   НастройкиПриемник - НастройкиКомпоновкиДанных, НастройкиВложенногоОбъектаКомпоновкиДанных
//      ГруппировкаКомпоновкиДанных, ГруппировкаТаблицыКомпоновкиДанных, ГруппировкаДиаграммыКомпоновкиДанных,
//      ТаблицаКомпоновкиДанных, ДиаграммаКомпоновкиДанных - коллекция настроек КД, куда копируются настройки
//      НастройкиИсточник	- НастройкиКомпоновкиДанных, НастройкиВложенногоОбъектаКомпоновкиДанных
//      ГруппировкаКомпоновкиДанных, ГруппировкаТаблицыКомпоновкиДанных, ГруппировкаДиаграммыКомпоновкиДанных,
//      ТаблицаКомпоновкиДанных, ДиаграммаКомпоновкиДанных - коллекция настроек КД, откуда копируются настройки.
//
Процедура СкопироватьНастройкиКомпоновкиДанных(НастройкиПриемник, НастройкиИсточник)
	
	Если НастройкиИсточник = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(НастройкиПриемник) = Тип("НастройкиКомпоновкиДанных") Тогда
		Для каждого Параметр Из НастройкиИсточник.ПараметрыДанных.Элементы Цикл
			ЗначениеПараметра = НастройкиПриемник.ПараметрыДанных.НайтиЗначениеПараметра(Параметр.Параметр);
			Если ЗначениеПараметра <> Неопределено Тогда
				ЗаполнитьЗначенияСвойств(ЗначениеПараметра, Параметр);
			КонецЕсли;
		КонецЦикла;
		Для Каждого Элемент Из НастройкиИсточник.ДополнительныеСвойства Цикл
			НастройкиПриемник.ДополнительныеСвойства.Вставить(Элемент.Ключ, Элемент.Значение);
		КонецЦикла;
	КонецЕсли;
	
	Если ТипЗнч(НастройкиИсточник) = Тип("НастройкиВложенногоОбъектаКомпоновкиДанных") Тогда
		ЗаполнитьЗначенияСвойств(НастройкиПриемник, НастройкиИсточник);
		СкопироватьНастройкиКомпоновкиДанных(НастройкиПриемник.Настройки, НастройкиИсточник.Настройки);
		Возврат;
	КонецЕсли;
	
	// Копирование настроек
	Если ТипЗнч(НастройкиИсточник) = Тип("НастройкиКомпоновкиДанных") Тогда
		
		ЗаполнитьЭлементы(НастройкиПриемник.ПараметрыДанных, НастройкиИсточник.ПараметрыДанных);
		СкопироватьЭлементы(НастройкиПриемник.ПользовательскиеПоля, НастройкиИсточник.ПользовательскиеПоля, Ложь);
		СкопироватьЭлементы(НастройкиПриемник.Отбор,                НастройкиИсточник.Отбор, Ложь);
		СкопироватьЭлементы(НастройкиПриемник.Порядок,              НастройкиИсточник.Порядок, Ложь);
		
	КонецЕсли;
	
	Если ТипЗнч(НастройкиИсточник) = Тип("ГруппировкаКомпоновкиДанных")
	 ИЛИ ТипЗнч(НастройкиИсточник) = Тип("ГруппировкаТаблицыКомпоновкиДанных")
	 ИЛИ ТипЗнч(НастройкиИсточник) = Тип("ГруппировкаДиаграммыКомпоновкиДанных") Тогда
		
		СкопироватьЭлементы(НастройкиПриемник.ПоляГруппировки, НастройкиИсточник.ПоляГруппировки, Ложь);
		СкопироватьЭлементы(НастройкиПриемник.Отбор,           НастройкиИсточник.Отбор, Ложь);
		СкопироватьЭлементы(НастройкиПриемник.Порядок,         НастройкиИсточник.Порядок, Ложь);
		ЗаполнитьЗначенияСвойств(НастройкиПриемник, НастройкиИсточник);
		
	КонецЕсли;
	
	СкопироватьЭлементы(НастройкиПриемник.Выбор,              НастройкиИсточник.Выбор, Ложь);
	СкопироватьЭлементы(НастройкиПриемник.УсловноеОформление, НастройкиИсточник.УсловноеОформление, Ложь);
	ЗаполнитьЭлементы(НастройкиПриемник.ПараметрыВывода, НастройкиИсточник.ПараметрыВывода);
	
	// Копирование структуры
	Если ТипЗнч(НастройкиИсточник) = Тип("НастройкиКомпоновкиДанных")
	 ИЛИ ТипЗнч(НастройкиИсточник) = Тип("ГруппировкаКомпоновкиДанных") Тогда
		
		Для каждого ЭлементСтруктурыИсточник Из НастройкиИсточник.Структура Цикл
			ЭлементСтруктурыПриемник = НастройкиПриемник.Структура.Добавить(ТипЗнч(ЭлементСтруктурыИсточник));
			СкопироватьНастройкиКомпоновкиДанных(ЭлементСтруктурыПриемник, ЭлементСтруктурыИсточник);
		КонецЦикла;
		
	КонецЕсли;
	
	Если ТипЗнч(НастройкиИсточник) = Тип("ГруппировкаТаблицыКомпоновкиДанных")
	 ИЛИ ТипЗнч(НастройкиИсточник) = Тип("ГруппировкаДиаграммыКомпоновкиДанных") Тогда
		
		Для каждого ЭлементСтруктурыИсточник Из НастройкиИсточник.Структура Цикл
			ЭлементСтруктурыПриемник = НастройкиПриемник.Структура.Добавить();
			СкопироватьНастройкиКомпоновкиДанных(ЭлементСтруктурыПриемник, ЭлементСтруктурыИсточник);
		КонецЦикла;
		
	КонецЕсли;
	
	Если ТипЗнч(НастройкиИсточник) = Тип("ТаблицаКомпоновкиДанных") Тогда
		
		Для каждого ЭлементСтруктурыИсточник Из НастройкиИсточник.Строки Цикл
			ЭлементСтруктурыПриемник = НастройкиПриемник.Строки.Добавить();
			СкопироватьНастройкиКомпоновкиДанных(ЭлементСтруктурыПриемник, ЭлементСтруктурыИсточник);
		КонецЦикла;
		
		Для каждого ЭлементСтруктурыИсточник Из НастройкиИсточник.Колонки Цикл
			ЭлементСтруктурыПриемник = НастройкиПриемник.Колонки.Добавить();
			СкопироватьНастройкиКомпоновкиДанных(ЭлементСтруктурыПриемник, ЭлементСтруктурыИсточник);
		КонецЦикла;
		
	КонецЕсли;
	
	Если ТипЗнч(НастройкиИсточник) = Тип("ДиаграммаКомпоновкиДанных") Тогда
		
		Для каждого ЭлементСтруктурыИсточник Из НастройкиИсточник.Серии Цикл
			ЭлементСтруктурыПриемник = НастройкиПриемник.Серии.Добавить();
			СкопироватьНастройкиКомпоновкиДанных(ЭлементСтруктурыПриемник, ЭлементСтруктурыИсточник);
		КонецЦикла;
		
		Для каждого ЭлементСтруктурыИсточник Из НастройкиИсточник.Точки Цикл
			ЭлементСтруктурыПриемник = НастройкиПриемник.Точки.Добавить();
			СкопироватьНастройкиКомпоновкиДанных(ЭлементСтруктурыПриемник, ЭлементСтруктурыИсточник);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Заполняет одну коллекцию элементов настроек на основании другой
//
// Параметры:
//   ПриемникЗначения - КоллекцияЗначенийПараметровКомпоновкиДанных, ЗначенияПараметровДанныхКомпоновкиДанных -
//                      коллекция элементов КД, куда копируются параметры
//   ИсточникЗначения - КоллекцияЗначенийПараметровКомпоновкиДанных, ЗначенияПараметровДанныхКомпоновкиДанных -
//                      коллекция элементов КД, откуда копируются параметры
//   ПервыйУровень    - ЗначенияПараметровДанныхКомпоновкиДанных - уровень структуры коллекции элементов КД для
//                                                                 копирования параметров.
//
Процедура ЗаполнитьЭлементы(ПриемникЗначения, ИсточникЗначения, ПервыйУровень = Неопределено)
	
	Если ТипЗнч(ПриемникЗначения) = Тип("КоллекцияЗначенийПараметровКомпоновкиДанных") Тогда
		КоллекцияЗначений = ИсточникЗначения;
	Иначе
		КоллекцияЗначений = ИсточникЗначения.Элементы;
	КонецЕсли;
	
	Для каждого ЭлементИсточник Из КоллекцияЗначений Цикл
		Если ПервыйУровень = Неопределено Тогда
			ЭлементПриемник = ПриемникЗначения.НайтиЗначениеПараметра(ЭлементИсточник.Параметр);
		Иначе
			ЭлементПриемник = ПервыйУровень.НайтиЗначениеПараметра(ЭлементИсточник.Параметр);
		КонецЕсли;
		Если ЭлементПриемник = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Если ЭлементИсточник.Использование Тогда
			ЗаполнитьЗначенияСвойств(ЭлементПриемник, ЭлементИсточник);
		КонецЕсли;
		Если ТипЗнч(ЭлементИсточник) = Тип("ЗначениеПараметраКомпоновкиДанных") Тогда
			Если ЭлементИсточник.ЗначенияВложенныхПараметров.Количество() <> 0 Тогда
				ЗаполнитьЭлементы(ЭлементПриемник.ЗначенияВложенныхПараметров, ЭлементИсточник.ЗначенияВложенныхПараметров, ПриемникЗначения);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Копирует элементы из одной коллекции в другую
//
// Параметры:
//   ПриемникЗначения - КоллекцияЗначенийПараметровКомпоновкиДанных, ЗначенияПараметровДанныхКомпоновкиДанных -
//                      коллекция элементов КД, куда копируются параметры
//   ИсточникЗначения - КоллекцияЗначенийПараметровКомпоновкиДанных, ЗначенияПараметровДанныхКомпоновкиДанных -
//                      коллекция элементов КД, откуда копируются параметры
//   ОчищатьПриемник  - Булево - признак необходимости очистки приемника.
//
Процедура СкопироватьЭлементы(ПриемникЗначения, ИсточникЗначения, ОчищатьПриемник = Истина)
	
	Если ТипЗнч(ИсточникЗначения) = Тип("УсловноеОформлениеКомпоновкиДанных")
		ИЛИ ТипЗнч(ИсточникЗначения) = Тип("ВариантыПользовательскогоПоляВыборКомпоновкиДанных")
		ИЛИ ТипЗнч(ИсточникЗначения) = Тип("ОформляемыеПоляКомпоновкиДанных")
		ИЛИ ТипЗнч(ИсточникЗначения) = Тип("ЗначенияПараметровДанныхКомпоновкиДанных") Тогда
		СоздаватьПоТипу = Ложь;
	Иначе
		СоздаватьПоТипу = Истина;
	КонецЕсли;
	ПриемникЭлементов = ПриемникЗначения.Элементы;
	ИсточникЭлементов = ИсточникЗначения.Элементы;
	Если ОчищатьПриемник Тогда
		ПриемникЭлементов.Очистить();
	КонецЕсли;
	
	Для каждого ЭлементИсточник Из ИсточникЭлементов Цикл
		
		Если ТипЗнч(ЭлементИсточник) = Тип("ЭлементПорядкаКомпоновкиДанных") Тогда
			// Элементы порядка добавляем в начало
			Индекс = ИсточникЭлементов.Индекс(ЭлементИсточник);
			ЭлементПриемник = ПриемникЭлементов.Вставить(Индекс, ТипЗнч(ЭлементИсточник));
		Иначе
			Если СоздаватьПоТипу Тогда
				ЭлементПриемник = ПриемникЭлементов.Добавить(ТипЗнч(ЭлементИсточник));
			Иначе
				ЭлементПриемник = ПриемникЭлементов.Добавить();
			КонецЕсли;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ЭлементПриемник, ЭлементИсточник);
		// В некоторых коллекциях необходимо заполнить другие коллекции
		Если ТипЗнч(ИсточникЭлементов) = Тип("КоллекцияЭлементовУсловногоОформленияКомпоновкиДанных") Тогда
			СкопироватьЭлементы(ЭлементПриемник.Поля, ЭлементИсточник.Поля);
			СкопироватьЭлементы(ЭлементПриемник.Отбор, ЭлементИсточник.Отбор);
			ЗаполнитьЭлементы(ЭлементПриемник.Оформление, ЭлементИсточник.Оформление); 
		ИначеЕсли ТипЗнч(ИсточникЭлементов)	= Тип("КоллекцияВариантовПользовательскогоПоляВыборКомпоновкиДанных") Тогда
			СкопироватьЭлементы(ЭлементПриемник.Отбор, ЭлементИсточник.Отбор);
		КонецЕсли;
		
		// В некоторых элементах коллекции необходимо заполнить другие коллекции
		Если ТипЗнч(ЭлементИсточник) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			СкопироватьЭлементы(ЭлементПриемник, ЭлементИсточник);
		ИначеЕсли ТипЗнч(ЭлементИсточник) = Тип("ГруппаВыбранныхПолейКомпоновкиДанных") Тогда
			СкопироватьЭлементы(ЭлементПриемник, ЭлементИсточник);
		ИначеЕсли ТипЗнч(ЭлементИсточник) = Тип("ПользовательскоеПолеВыборКомпоновкиДанных") Тогда
			СкопироватьЭлементы(ЭлементПриемник.Варианты, ЭлементИсточник.Варианты);
		ИначеЕсли ТипЗнч(ЭлементИсточник) = Тип("ПользовательскоеПолеВыражениеКомпоновкиДанных") Тогда
			ЭлементПриемник.УстановитьВыражениеДетальныхЗаписей (ЭлементИсточник.ПолучитьВыражениеДетальныхЗаписей());
			ЭлементПриемник.УстановитьВыражениеИтоговыхЗаписей(ЭлементИсточник.ПолучитьВыражениеИтоговыхЗаписей());
			ЭлементПриемник.УстановитьПредставлениеВыраженияДетальныхЗаписей(ЭлементИсточник.ПолучитьПредставлениеВыраженияДетальныхЗаписей ());
			ЭлементПриемник.УстановитьПредставлениеВыраженияИтоговыхЗаписей(ЭлементИсточник.ПолучитьПредставлениеВыраженияИтоговыхЗаписей ());
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция НайтиНадписьРегистраНаДокументе(ТаблицаГлобальныхНастроек, Знач ТекстОбласти)
	
	ТекстОбласти = ВРег(СокрЛП(ТекстОбласти));
	Если Не ЗначениеЗаполнено(ТекстОбласти) Тогда
		Возврат Неопределено;
	КонецЕсли;
		
	Для Каждого ГлобальнаяНастройка Из ТаблицаГлобальныхНастроек Цикл
		ЗначениеВТаблице = ВРег(СокрЛП(ГлобальнаяНастройка.НадписьРезультат));
		Если СтрНачинаетсяС(ТекстОбласти, ЗначениеВТаблице) Тогда
			Возврат ГлобальнаяНастройка;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

Процедура ВосстановитьОтборПоГруппировкамРегистров(НастройкиКД)
	
	СтруктураОтчета = НастройкиКД.Структура;
	Для Каждого ЭлементСтруктуры Из СтруктураОтчета Цикл
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ЭлементСтруктуры.Отбор,
			"ИмяРегистра", ЭлементСтруктуры.Имя, ВидСравненияКомпоновкиДанных.Равно,
			НСтр("ru = 'Служебный отбор'"), Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	КонецЦикла;
	
КонецПроцедуры

Процедура ПерерасчетКоличестваДвижений(СписокРегистров, ДокументОтбор, ГлобальныеНастройки)
	
	МассивИспользуемыхРегистров     = МассивИспользуемыхРегистров(ДокументОтбор, ДокументОтбор.Метаданные().Движения);
	ПредварительныйРасчетКоличества = ПредварительныйРасчетКоличества(ДокументОтбор, МассивИспользуемыхРегистров);
	
	ОтмеченныеРегистры = Новый СписокЗначений;
	Если ТипЗнч(СписокРегистров) = Тип("Строка") Тогда
		ОтмеченныеРегистры.Добавить(СписокРегистров);
	ИначеЕсли ТипЗнч(СписокРегистров) = Тип("СписокЗначений") Тогда
		ОтмеченныеРегистры = СписокРегистров;
	КонецЕсли;
	
	Для Каждого ГлобальнаяНастройка Из ГлобальныеНастройки Цикл
		
		ПолноеИмяРегистра = "Регистр" + ГлобальнаяНастройка.ВидРегистра + "_" + ГлобальнаяНастройка.ИмяРегистра;
		ГлобальнаяНастройка.Вставить("КоличествоЗаписей", ПредварительныйРасчетКоличества[ПолноеИмяРегистра]);
		
		Если ОтмеченныеРегистры.НайтиПоЗначению(ПолноеИмяРегистра) <> Неопределено Тогда
			ГлобальнаяНастройка.Вставить("Использование", Истина);
		Иначе
			ГлобальнаяНастройка.Вставить("Использование", Ложь);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СкрытьНеактуальныеГруппировки(НастройкиКД, ДокументОтбор)
	
	ДополнительныеСвойства = НастройкиКД.ДополнительныеСвойства;
	Если Не ДополнительныеСвойства.Свойство("ГлобальныеНастройки") Тогда
		Возврат;
	КонецЕсли;
	ГлобальныеНастройки = ДополнительныеСвойства.ГлобальныеНастройки;
	
	СписокРегистровПараметрСКД = НастройкиКД.ПараметрыДанных.Элементы.Найти("СписокРегистров");
	Если СписокРегистровПараметрСКД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	СписокРегистров = СписокРегистровПараметрСКД.Значение;
	
	ПерерасчетКоличестваДвижений(СписокРегистров, ДокументОтбор, ГлобальныеНастройки);
	
	СтруктураКД = НастройкиКД.Структура;
	Для Каждого Элемент Из СтруктураКД Цикл
		
		Для Каждого ГлобальнаяНастройка Из ГлобальныеНастройки Цикл
			
			Если ВРег(СокрЛП("Регистр" + ГлобальнаяНастройка.ВидРегистра + "_" + ГлобальнаяНастройка.ИмяРегистра)) <> ВРег(СокрЛП(Элемент.Имя)) Тогда
				Продолжить;
			КонецЕсли;
			
			Если ГлобальнаяНастройка.КоличествоЗаписей = 0 Или (СписокРегистровПараметрСКД.Использование И Не ГлобальнаяНастройка.Использование) Тогда
				Элемент.Состояние = СостояниеЭлементаНастройкиКомпоновкиДанных.Отключен;
				Элемент.Структура.Получить(0).Состояние = СостояниеЭлементаНастройкиКомпоновкиДанных.Отключен;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли